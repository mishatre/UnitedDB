
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ВидЭД = РеквизитЭД(ПараметрКоманды, "ВидЭД");

	ЭДВладелец = ВладелецЭД(ПараметрКоманды);
	Если ЭтоДокументФНС(ЭДВладелец) Тогда
		ОткрытьДеревоЭД(ПараметрКоманды, ПараметрыВыполненияКоманды);
		
	ИначеЕсли ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПроизвольныйЭД") Тогда
		ОткрытьСтруктуруПодчиненности(ПараметрКоманды, ПараметрыВыполненияКоманды);
		
	Иначе
		ОткрытьСписокЭД(ПараметрКоманды, ПараметрыВыполненияКоманды);
		
	КонецЕсли;
	
КонецПроцедуры


// Служебные процедуры и функции
//
&НаКлиенте
Процедура ОткрытьСписокЭД(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ВладелецЭД = РеквизитЭД(ПараметрКоманды, "ВладелецФайла");
	
	ПараметрыСписка = ПараметрыОткрытияОкна(ПараметрКоманды, ПараметрыВыполненияКоманды);
	
	ЭлектронныеДокументыКлиент.ОткрытьСписокЭД(ВладелецЭД, ПараметрыСписка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДеревоЭД(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ВладелецФайла = РеквизитЭД(ПараметрКоманды, "ВладелецФайла");
	
	Если Не ЗначениеЗаполнено(ВладелецФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСписка = ПараметрыОткрытияОкна(ПараметрКоманды, ПараметрыВыполненияКоманды);
		
	ЭлектронныеДокументыКлиент.ОткрытьДеревоЭД(ВладелецФайла, ПараметрыСписка, Ложь);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОткрытияОкна(ПараметрКоманды, ПараметрыВыполненияКоманды);
	
	ПараметрыОкна = Новый Структура;
	ПараметрыОкна.Вставить("ИсходныйДокумент", ПараметрКоманды);
	ПараметрыОкна.Вставить("Источник", ПараметрыВыполненияКоманды.Источник);
	ПараметрыОкна.Вставить("Окно", ПараметрыВыполненияКоманды.Окно);
	ПараметрыОкна.Вставить("Уникальность", ПараметрКоманды);
	
	Возврат ПараметрыОкна;
	
КонецФункции

&НаСервере
Функция ВладелецЭД(ЭлектронныйДокумент)
		
	ЭДВладелец = ?(ЗначениеЗаполнено(ЭлектронныйДокумент.ЭлектронныйДокументВладелец),
		ЭлектронныйДокумент.ЭлектронныйДокументВладелец,
		ЭлектронныйДокумент);
	
	Если ЗначениеЗаполнено(ЭДВладелец.ЭлектронныйДокументВладелец) тогда
		Возврат ВладелецЭД(ЭДВладелец);
	Иначе
		Возврат ЭДВладелец;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция РеквизитЭД(СсылкаЭД, ИмяРеквизита)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаЭД, ИмяРеквизита);
	
КонецФункции

&НаСервере
Функция ЭтоДокументФНС(ЭДВладелец)
	
	ВидЭД = РеквизитЭД(ЭДВладелец, "ВидЭД");
	Возврат ЭлектронныеДокументыСлужебный.ЭтоФНС(ВидЭД);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСтруктуруПодчиненности(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ВладелецФайла = РеквизитЭД(ПараметрКоманды, "ВладелецФайла");

	ОткрытьФорму("ОбщаяФорма.СтруктураПодчиненности",Новый Структура("ОбъектОтбора", ВладелецФайла),
				ПараметрыВыполненияКоманды.Источник,
				ПараметрыВыполненияКоманды.Источник.КлючУникальности,
				ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры




