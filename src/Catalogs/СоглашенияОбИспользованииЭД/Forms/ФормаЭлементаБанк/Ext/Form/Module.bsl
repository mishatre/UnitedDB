////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимОткрытияОкна") Тогда
		РежимОткрытияОкна = Параметры.РежимОткрытияОкна;
	КонецЕсли;
	
	ОбъектЭлемента = РеквизитФормыВЗначение("Объект");
	
	ЗаполнитьВидыЭДДоступнымиЗначениями();
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда // новый
		Объект.СтатусСоглашения = Перечисления.СтатусыСоглашенийЭД.НеСогласовано;
		Объект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезВебРесурсБанка;
		Объект.РесурсВходящихДокументов = "";
		Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.АльфаБанкОнлайн;
		Объект.ИспользуетсяКриптография = Истина;
		ЗаполнитьЗначенияСвойств(Объект, Параметры);
	Иначе
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
		Попытка
			ДвоичныеДанныеСертификата  = ДокументОбъект.СертификатКонтрагентаДляШифрования.Получить();
			Если ДвоичныеДанныеСертификата <> Неопределено Тогда
				СертификатКриптографии = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
				ПредставлениеСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ПолучитьПредставлениеПользователя(
				                                                                     СертификатКриптографии.Субъект);
				СертификатБанка = ПредставлениеСертификата;
			КонецЕсли;
		Исключение
			ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке())
							+ НСтр("ru = ' (подробности см. в Журнале регистрации).'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронныеДокументы.ОбработатьИсключениеПоЭДНаСервере(
										НСтр("ru = 'открытие формы соглашения'"),
										ТекстОшибки,
										ТекстСообщения,
										1);
		КонецПопытки;

	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Объект.Контрагент = ЭлектронныеДокументыПовтИсп.ПолучитьПустуюСсылку("Банки");
	КонецЕсли;
	
	Элементы.СтраницыВидыБанковскихСистем.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ВариантАвторизации = Число(Объект.ИспользуетсяКриптография);
	
	ВключеныДополнительныеОтчетыИОбработки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
	                                                                          "ИспользоватьДополнительныеОтчетыИОбработки");
	
	Элементы.ДополнительнаяОбработка.Доступность = ВключеныДополнительныеОтчетыИОбработки;
	Элементы.ВидБанковскойСистемы.Видимость = ВключеныДополнительныеОтчетыИОбработки;
	
	ПереключитьСтраницы(ЭтаФорма, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография);
	Элементы.ВариантАвторизации.ТолькоПросмотр = ЭтаФорма.ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПереключитьСтраницы(ЭтаФорма, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОчиститьСообщения();
	
	Если Объект.СтатусСоглашения = ПредопределенноеЗначение("Перечисление.СтатусыСоглашенийЭД.Действует") Тогда
		ТекстОшибкиАктуальности = "";
		ПроверитьАктуальностьДанныхСоглашения(ТекстОшибкиАктуальности);
		Если НЕ ПустаяСтрока(ТекстОшибкиАктуальности) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиАктуальности, , , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезВебРесурсБанка");
	
	Если ЗначениеЗаполнено(Объект.АдресСервера) И НЕ ПравильныйФорматАдреса()
		И Объект.СтатусСоглашения = ПредопределенноеЗначение("Перечисление.СтатусыСоглашенийЭД.Действует") Тогда
		
		ТекстСообщения = НСтр("ru = 'Адрес сервера банка должен начинаться с """"https://"""" или """"http://""""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "АдресСервера", "Объект", Отказ);
		
	КонецЕсли;
	
	УдалитьПустыеСтрокиТаблиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененоСоглашениеЭД", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
			И Объект.СтатусСоглашения = Перечисления.СтатусыСоглашенийЭД.Действует Тогда
		ПроверяемыеРеквизиты.Добавить("СертификатБанка");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияОтправительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяОрганизация = Объект.Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтправительПриИзменении(Элемент)
	
	Если Объект.СертификатыПодписейОрганизации.Количество() И Не ТекущаяОрганизация = Объект.Организация
		И Объект.ИспользуетсяКриптография Тогда
		
		ТекстВопроса = Нстр("ru = 'После изменения организации список сертификатов будет очищен.'");
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(Истина, Нстр("ru = 'Сменить организацию'"));
		Кнопки.Добавить(Ложь, Нстр("ru = 'Отменить изменение'"));
		Результат = Вопрос(ТекстВопроса, Кнопки, , Истина, Нстр("ru = 'Изменение организации'"));
		
		Если Результат = Истина Тогда
			Объект.СертификатыПодписейОрганизации.Очистить();
		Иначе
			Объект.Организация = ТекущаяОрганизация;
		КонецЕсли;
	ИначеЕсли Не ТекущаяОрганизация = Объект.Организация Тогда
		Объект.СертификатыПодписейОрганизации.Очистить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтправительОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.СертификатыПодписейОрганизации.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ЗаполнитьТаблицуЭтапов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидБанковскойСистемыПриИзменении(Элемент)
	
	Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
		ИЛИ Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Объект.ИспользуетсяКриптография = Истина;
	КонецЕсли;
	ПереключитьСтраницы(ЭтаФорма, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография);
	ОбновитьТабличныеЧасти();
	
	Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
		И НЕ ВключеныДополнительныеОтчетыИОбработки Тогда
			ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы(
																					"ДОПОЛНИТЕЛЬНЫЕОТЧЕТЫИОБРАБОТКИ");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатБанкаПриИзменении(Элемент)
	
	Если ПустаяСтрока(Элемент.ТекстРедактирования) Тогда
		ПоместитьВХранилищеСертификат();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатБанкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если СоглашениеЗаписано() Тогда
		Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
			Возврат;
		КонецЕсли;
		
		Режим = РежимДиалогаВыбораФайла.Открытие;
		Диалог = Новый ДиалогВыбораФайла(Режим);
		Диалог.Фильтр = "Файлы сертификатов(*.cer)|*.cer";
		Диалог.Заголовок = НСтр("ru = 'Выберите файл сертификата банка'");
		МассивПомещенных = Новый Массив;
		Если ПоместитьФайлы( ,МассивПомещенных, Диалог) Тогда
			ПоместитьСертификатВСоглашение(МассивПомещенных);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СертификатБанкаОчистка(Элемент, СтандартнаяОбработка)
	
	ПоместитьВХранилищеСертификат();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантАвторизацииПриИзменении(Элемент)
	
	Объект.ИспользуетсяКриптография = Булево(ВариантАвторизации);
	ПереключитьСтраницы(ЭтаФорма, Объект.ПрограммаБанка, Объект.ИспользуетсяКриптография);
	ОбновитьТабличныеЧасти()
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СертификатыНеобходимыхПодписейТиповые

&НаКлиенте
Процедура СертификатыНеобходимыхПодписейТиповыеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	ВнешнийПодключаемыйМодуль = ЭлектронныеДокументыСлужебныйКлиент.ВнешнийПодключаемыйМодуль(Объект.Ссылка);
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСертификата = ЭлектронныеДокументыСлужебныйКлиент.ДанныеСертификатаЧерезДопОбработку(ВнешнийПодключаемыйМодуль,
	                                                                                           ВыбранноеЗначение);

	Если Не ЗначениеЗаполнено(ДанныеСертификата) Тогда
		Возврат;
	КонецЕсли;
	
	НовыйСертификат = НовыйСертификат(ВыбранноеЗначение, ДанныеСертификата, Объект.Организация);
	
	СтрокаСоглашения = Объект.СертификатыПодписейОрганизации.Добавить();
	СтрокаСоглашения.Сертификат = НовыйСертификат;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ИСХОДЯЩИЕ ДОКУМЕНТЫ

&НаКлиенте
Процедура ИсходящиеДокументыПриАктивизацииСтроки(Элемент)
	
	ЗаполнитьТаблицуЭтапов();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ВХОДЯЩИЕ ДОКУМЕНТЫ

&НаКлиенте
Процедура ВходящиеДокументыПриАктивизацииСтроки(Элемент)
	
	ЗаполнитьТаблицуЭтапов();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЖурналАудита(Команда)

	ОткрытьФорму("РегистрСведений.ЖурналАудитаСбербанк.ФормаСписка", Новый Структура("СоглашениеЭД", Объект.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификат(Команда)
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = Нстр("ru = 'Необходимо выбрать организацию'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Организация", , Отказ);
	КонецЕсли;
	
	Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
		И НЕ ЗначениеЗаполнено(Объект.ДополнительнаяОбработка) Тогда
		
		ТекстСообщения = Нстр("ru = 'Необходимо выбрать дополнительную обработку'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.ДополнительнаяОбработка", , Отказ);
		
	КонецЕсли;

	Если Отказ ИЛИ НЕ СоглашениеЗаписано() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ПараметрыОткрытия = Новый Структура("СоглашениеЭД", Объект.Ссылка);
		ОткрытьФорму("Обработка.ОбменЭлектроннымиДокументамиСБанком.Форма.ПолучениеСертификата",
		             ПараметрыОткрытия,
		             Элементы.СертификатыНеобходимыхПодписейТиповые);
	ИначеЕсли Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ЗагрузитьСертификатСТокена();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестНастроек(Команда)
	
	Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		#Если ВебКлиент ИЛИ ТонкийКлиент Тогда
			ТекстСообщения = Нстр("ru = 'Тест возможен только в толстом клиенте'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		#КонецЕсли
	КонецЕсли;
	ЭлектронныеДокументыСлужебныйКлиент.ПроверитьНаличиеСвязиСБанком(
		Объект.Ссылка,
		УникальныйИдентификатор);
		
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ЗаполнитьТаблицуЭтапов()
	
	СтруктураПараметров = СтруктураПараметровВидаЭД();
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.ГруппаВходящиеДокументы Тогда
		ЗаполнитьСтруктуруПараметров(СтруктураПараметров, Элементы.ВходящиеДокументы.ТекущиеДанные);
		УстановитьЗначенияЭтаповОбменаПоНастройкам(Ложь, СтруктураПараметров);
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.ГруппаИсходящиеДокументы Тогда
		ЗаполнитьСтруктуруПараметров(СтруктураПараметров, Элементы.ИсходящиеДокументы.ТекущиеДанные);
		УстановитьЗначенияЭтаповОбменаПоНастройкам(СтруктураПараметров);
	КонецЕсли	

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыЭДДоступнымиЗначениями()
	
	АктуальныеВидыЭД = ЭлектронныеДокументыПовтИсп.ПолучитьАктуальныеВидыЭД();
	
	Для Каждого ЗначениеПеречисления Из АктуальныеВидыЭД Цикл
		Если ЗначениеПеречисления = Перечисления.ВидыЭД.ПлатежноеПоручение 
			ИЛИ ЗначениеПеречисления = Перечисления.ВидыЭД.ЗапросВыписки Тогда
				МассивСтрок = Объект.ИсходящиеДокументы.НайтиСтроки(Новый Структура("ИсходящийДокумент", ЗначениеПеречисления));
				Если МассивСтрок.Количество() = 0 Тогда 
					НоваяСтрока = Объект.ИсходящиеДокументы.Добавить();
					НоваяСтрока.ИсходящийДокумент = ЗначениеПеречисления;
					НоваяСтрока.Формировать       = Истина;
					НоваяСтрока.ИспользоватьЭЦП   = Истина;
				КонецЕсли;
		ИначеЕсли ЗначениеПеречисления = Перечисления.ВидыЭД.ВыпискаБанка Тогда
				МассивСтрок = Объект.ВходящиеДокументы.НайтиСтроки(Новый Структура("ВходящийДокумент", ЗначениеПеречисления));
				Если МассивСтрок.Количество() = 0 Тогда 
					НоваяСтрока = Объект.ВходящиеДокументы.Добавить();
					НоваяСтрока.ВходящийДокумент = ЗначениеПеречисления;
					НоваяСтрока.Формировать       = Истина;
					НоваяСтрока.ИспользоватьЭЦП   = Истина;
				КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияЭтаповОбменаПоНастройкам(НастройкиИсходящие = Ложь, НастройкиВходящие = Ложь)
	
	Если НастройкиИсходящие <> Ложь Тогда
		МассивСтатусов = ЭлектронныеДокументыСлужебный.ВернутьМассивСтатусовЭД(НастройкиИсходящие);
		ТаблицаЗаполнения = ТаблицаЭтаповИсходящие;
	КонецЕсли;
	
	Если НастройкиВходящие <> Ложь Тогда
		МассивСтатусов = ЭлектронныеДокументыСлужебный.ВернутьМассивСтатусовЭД(НастройкиВходящие);
		ТаблицаЗаполнения = ТаблицаЭтаповВходящие;
	КонецЕсли;
	
	Если ТаблицаЗаполнения <> Неопределено Тогда
		ТаблицаЗаполнения.Очистить();
		
		Для Каждого Элемент Из МассивСтатусов Цикл
			ТаблицаЗаполнения.Добавить(Элемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураПараметровВидаЭД()
	СтруктураВозврата = Новый Структура("Направление, ВидЭД, СпособОбмена, ИспользоватьПодпись, ИспользуетсяНесколькоПодписей, ПрограммаБанка");
	Возврат СтруктураВозврата;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСтруктуруПараметров(СтруктураПараметров,ДанныеФормыКоллекция)
	
	Если ДанныеФормыКоллекция = Неопределено ИЛИ НЕ ДанныеФормыКоллекция.Формировать Тогда
		СтруктураПараметров = Неопределено; 
	Иначе
		СтруктураПараметров.ИспользоватьПодпись           = Объект.ИспользуетсяКриптография;
		СтруктураПараметров.СпособОбмена                  = Объект.СпособОбменаЭД;
		
		Если ДанныеФормыКоллекция.Свойство("ИсходящийДокумент") Тогда
			СтруктураПараметров.Направление = ПредопределенноеЗначение("Перечисление.НаправленияЭД.Исходящий");
			СтруктураПараметров.ВидЭД = ДанныеФормыКоллекция.ИсходящийДокумент;
		Иначе
			СтруктураПараметров.Направление = ПредопределенноеЗначение("Перечисление.НаправленияЭД.Входящий");
			СтруктураПараметров.ВидЭД = ДанныеФормыКоллекция.ВходящийДокумент;
		КонецЕсли;
		
		СтруктураПараметров.ПрограммаБанка = Объект.ПрограммаБанка;
		
		СтруктураПараметров.ИспользуетсяНесколькоПодписей = Объект.СертификатыПодписейОрганизации.Количество() > 1;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьАктуальностьДанныхСоглашения(ТекстОшибкиАктуальности)
	
	ЗапросПоСоглашениям = Новый Запрос;
	ЗапросПоСоглашениям.УстановитьПараметр("СтатусСоглашения",  Перечисления.СтатусыСоглашенийЭД.Действует);
	ЗапросПоСоглашениям.УстановитьПараметр("ТекущееСоглашение", Объект.Ссылка);
	ЗапросПоСоглашениям.УстановитьПараметр("Организация",       Объект.Организация);
	ЗапросПоСоглашениям.УстановитьПараметр("Контрагент",        Объект.Контрагент);
	ЗапросПоСоглашениям.Текст = "ВЫБРАТЬ
	                            |	СоглашенияОбИспользованииЭДИсходящиеДокументы.ИсходящийДокумент КАК ТипДокумента,
	                            |	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка КАК Соглашение
	                            |ИЗ
	                            |	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	                            |ГДЕ
	                            |	СоглашенияОбИспользованииЭДИсходящиеДокументы.Формировать = ИСТИНА
	                            |	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СтатусСоглашения = &СтатусСоглашения
	                            |	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПометкаУдаления = ЛОЖЬ
	                            |	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка <> &ТекущееСоглашение
	                            |	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
	                            |	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Контрагент = &Контрагент
	                            |	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезВебРесурсБанка)";
	Результат = ЗапросПоСоглашениям.Выполнить().Выгрузить();
	
	ПроверитьУникальностьДокументов(Объект.ИсходящиеДокументы, Результат, ТекстОшибкиАктуальности);
		
КонецПроцедуры

&НаСервере
Процедура ПроверитьУникальностьДокументов(ТабличнаяЧастьДокументов, РезультатПроверки, ТекстОшибки)
			
	Для Каждого ТекущийДокументСоглашения Из ТабличнаяЧастьДокументов Цикл
		Если ТекущийДокументСоглашения.Формировать Тогда
			Для Каждого ДокументВДругихСоглашениях Из РезультатПроверки Цикл
				Если ТекущийДокументСоглашения.ИсходящийДокумент = ДокументВДругихСоглашениях.ТипДокумента Тогда
					ТекстОшибки = НСтр("ru = 'По виду электронных документов %1 %2 
					|уже существует действующее соглашение между участниками %3 - %4:
					|%5.
					|'");
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстОшибки, 
						ДокументВДругихСоглашениях.ТипДокумента, 
						"Исходящий", 
						Объект.Организация, 
						Объект.Контрагент, 
						ДокументВДругихСоглашениях.Соглашение
						);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПравильныйФорматАдреса()
	
	Если НРег(Лев(Объект.АдресСервера, 7)) = "http://"
			ИЛИ НРег(Лев(Объект.АдресСервера, 8)) = "https://" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьСтраницы(Форма, Знач ПрограммаБанка, Знач ИспользуетсяКриптография)
	
	ЭтоПрограммаСбербанка = (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн"));
	ЭтоОбменЧерезДопОбработку = (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку"));
	Форма.Элементы.ЗагрузитьСертификатИзХранилища.Видимость = ЭтоОбменЧерезДопОбработку ИЛИ ЭтоПрограммаСбербанка;
	
	Если ЭтоОбменЧерезДопОбработку Тогда
		Форма.Элементы.СтраницыОбработкаКриптография.ТекущаяСтраница = Форма.Элементы.СтраницаОбработка;
	Иначе
		Форма.Элементы.СтраницыОбработкаКриптография.ТекущаяСтраница = Форма.Элементы.СтраницаПустая;
	КонецЕсли;
	
	Форма.Элементы.СтраницыВидыБанковскихСистем.Видимость = НЕ ЭтоОбменЧерезДопОбработку;
	Форма.Элементы.ГруппаЛогинПароль.Доступность = НЕ ИспользуетсяКриптография;
	
	Если ЭтоПрограммаСбербанка Тогда
		Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаСбербанк;
		Форма.Элементы.СертификатыНеобходимыхПодписейНомерСтроки.Видимость = Ложь;
		Форма.Элементы.СертификатыНеобходимыхПодписейТиповыеНомерКонтейнера.Видимость = Истина;
	Иначе
		Форма.Элементы.СтраницыВидыБанковскихСистем.ТекущаяСтраница = Форма.Элементы.СтраницаТиповой;
		Форма.Элементы.СертификатыНеобходимыхПодписейНомерСтроки.Видимость = Истина;
		Форма.Элементы.СертификатыНеобходимыхПодписейТиповыеНомерКонтейнера.Видимость = Ложь;
	КонецЕсли;
	
	Форма.Элементы.ТестНастроек.Видимость                = ЭтоПрограммаСбербанка ИЛИ ЭтоОбменЧерезДопОбработку;
	Форма.Элементы.ЖурналАудита.Видимость                = ЭтоПрограммаСбербанка;
	Форма.Элементы.СтраницаСертификатыТиповые.Видимость  = ИспользуетсяКриптография;
	
	ВключеныДополнительныеОтчетыИОбработки = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции(
	                                                                          "ИспользоватьДополнительныеОтчетыИОбработки");
	Форма.Элементы.ДополнительнаяОбработка.Доступность = ВключеныДополнительныеОтчетыИОбработки;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПустыеСтрокиТаблиц()
	
	СписокСтрокКУдалению = Новый СписокЗначений;
	Для каждого СтрокаСертификата ИЗ Объект.СертификатыПодписейОрганизации Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаСертификата.Сертификат) Тогда
			СписокСтрокКУдалению.Добавить(СтрокаСертификата.НомерСтроки);
		КонецЕсли;
	КонецЦикла;

	СписокСтрокКУдалению.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	Для Каждого Запись ИЗ СписокСтрокКУдалению Цикл
		Объект.СертификатыПодписейОрганизации.Удалить(Запись.Значение-1);
	КонецЦикла

КонецПроцедуры

&НаСервере
Процедура ПоместитьВХранилищеСертификат(ДвоичныеДанные = Неопределено, ПредставлениеСертификата=Неопределено)
	
	ХранилищеЗначения  = Новый ХранилищеЗначения(ДвоичныеДанные);
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	СправочникОбъект.СертификатКонтрагентаДляШифрования = ХранилищеЗначения;
	СправочникОбъект.Записать();
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	Прочитать();
	
	Если ДвоичныеДанные <> Неопределено Тогда
		Попытка
			СертификатКриптографии = Новый СертификатКриптографии(ДвоичныеДанные);
		Исключение
			ВремФайл = ПолучитьИмяВременногоФайла();
			Попытка
				ДвоичныеДанные.Записать(ВремФайл);
				ТекстовыйДокумент = Новый ТекстовыйДокумент;
				ТекстовыйДокумент.Прочитать(ВремФайл);
				СтрокаBase64 = ТекстовыйДокумент.ПолучитьТекст();
				СтрокаBase64 = СтрЗаменить(СтрокаBase64, "-----BEGIN CERTIFICATE-----" + Символы.ПС,""); 
				СтрокаBase64 = СтрЗаменить(СтрокаBase64, Символы.ПС + "-----END CERTIFICATE-----","");
				ДвоичныеДанныеСертификата = Base64Значение(СтрокаBase64);
				ХранилищеЗначения  = Новый ХранилищеЗначения(ДвоичныеДанныеСертификата);
				СправочникОбъект = РеквизитФормыВЗначение("Объект");
				СправочникОбъект.СертификатКонтрагентаДляШифрования = ХранилищеЗначения;
				СправочникОбъект.Записать();
				ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
				Прочитать();
				СертификатКриптографии = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
			Исключение
				СправочникОбъект = РеквизитФормыВЗначение("Объект");
				СправочникОбъект.СертификатКонтрагентаДляШифрования = Неопределено;
				СправочникОбъект.Записать();
				ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
				Прочитать();
				УдалитьФайлы(ВремФайл);
				ТекстСообщения = НСтр("ru = 'Не удалось прочитать файл сертификата, операция прервана.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				ПредставлениеСертификата = "";
				Возврат;
			КонецПопытки;
			УдалитьФайлы(ВремФайл);
			
		КонецПопытки;
		ПредставлениеСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ПолучитьПредставлениеПользователя(
																				СертификатКриптографии.Субъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСоглашение()
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	СправочникОбъект.Записать();
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Функция СоглашениеЗаписано()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И НЕ Модифицированность Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ТекстВопроса = НСтр("ru = 'Внешние сертификаты можно выбирать только в записанном соглашении.
							|Записать соглашение?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	ИначеЕсли Объект.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ТекстВопроса = НСтр("ru = 'Загружать сертификаты можно выбирать только в записанном соглашении.
							|Записать соглашение?'");
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		Ответ = КодВозвратаДиалога.Да;
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьСоглашение();
	КонецЕсли;
	
	Возврат Ответ = КодВозвратаДиалога.Да;
	
КонецФункции

&НаСервере
Процедура ПоместитьСертификатВСоглашение(МассивАдресовФайла)

	АдресВременногоХранилища = МассивАдресовФайла[0].Хранение;
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	ПредставлениеСертификата = Неопределено;
	ПоместитьВХранилищеСертификат(ДанныеФайла, ПредставлениеСертификата);
	СертификатБанка = ПредставлениеСертификата;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыйСертификат(Знач СертификатКлючаXML,
						Знач ДанныеСертификата,
						Знач Организация)
	
	НовСерт = Справочники.СертификатыЭЦП.СоздатьЭлемент();
	НовСерт.ДолжностьПоСертификату = ДанныеСертификата.ВладелецДолжность;
	ЭлектронныеДокументы.ФамилияИнициалыФизЛица(ДанныеСертификата.ВладелецФИО,
												НовСерт.Фамилия,
												НовСерт.Имя,
												НовСерт.Отчество);
	НовСерт.ДатаОкончания   = ДанныеСертификата.ДатаОкончания;
	НазначениеСертификата   = Нстр("ru = 'Владелец: %1
										|Действует до: %2'");
	НазначениеСертификата   = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
														НазначениеСертификата,
														ДанныеСертификата.ВладелецФИО,
														ДанныеСертификата.ДатаОкончания);
	НовСерт.Назначение      = НазначениеСертификата;
	НовСерт.Наименование    = ДанныеСертификата.ВладелецФИО + " (" + ДанныеСертификата.Псевдоним + ")";
	НовСерт.Организация     = Организация;
	НовСерт.ПрограммаБанка  = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку;
	НовСерт.ФайлСертификата = Новый ХранилищеЗначения(СертификатКлючаXML);
	НовСерт.Отпечаток       = ДанныеСертификата.Отпечаток;
	ВидЭД = НовСерт.ВидыДокументов.Добавить();
	ВидЭД.ВидДокумента = Перечисления.ВидыЭД.ПлатежноеПоручение;
	ВидЭД.ИспользоватьДляПодписи = Истина;
	НовСерт.Записать();
	Возврат НовСерт.Ссылка;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьТабличныеЧасти()
	
	Для Каждого Строка Из Объект.ВходящиеДокументы Цикл
		Строка.ИспользоватьЭЦП = Объект.ИспользуетсяКриптография;
	КонецЦикла;
	
	Для Каждого Строка Из Объект.ИсходящиеДокументы Цикл
		Строка.ИспользоватьЭЦП = Объект.ИспользуетсяКриптография;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификатСТокена()
	
	ДанныеСертификата = ЭлектронныеДокументыСлужебныйКлиент.ДанныеСертификатаСТокенаСбербанка();
	
	Если ДанныеСертификата = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Попытка
		НовыйСертификат = Новый СертификатКриптографии(ДанныеСертификата);
	Исключение
		Предупреждение(НСтр("ru = 'Не удалось прочитать файл сертификата, операция прервана.'"));
		Возврат;
	КонецПопытки;
	
	СтруктураСертификата = ЭлектроннаяЦифроваяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(НовыйСертификат);
	
	СтруктураСертификата.Вставить("ДвоичныеДанныеСертификата", ДанныеСертификата);
	СтруктураСертификата.Вставить("Организация", Объект.Организация);
		
	ОписаниеОшибки = "";
	НовыйСертификат = СоздатьСертификат(СтруктураСертификата, ОписаниеОшибки);
	Если НЕ ЗначениеЗаполнено(НовыйСертификат) Тогда
		Предупреждение(ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	СтрокаСоглашения = Объект.СертификатыПодписейОрганизации.Добавить();
	СтрокаСоглашения.Сертификат = НовыйСертификат;
	СтрокаСоглашения.НомерКонтейнера = ЭлектронныеДокументыСлужебныйКлиент.ЗначениеИзКэша("НомерКонтейнера");
	
	Оповестить("ОбновитьСписокСертификатов");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьСертификат(СтруктураСертификата, ОписаниеОшибки)
	
	НовыйСертификат = ЭлектронныеДокументыСлужебныйВызовСервера.ЗагрузитьСертификат(СтруктураСертификата, ОписаниеОшибки);
	
	Если ЗначениеЗаполнено(НовыйСертификат) Тогда
		СертификатОбъект = НовыйСертификат.ПолучитьОбъект();
		НовСтрока = СертификатОбъект.ВидыДокументов.Добавить();
		НовСтрока.ВидДокумента = Перечисления.ВидыЭД.ПлатежноеПоручение;
		НовСтрока.ИспользоватьДляПодписи = Истина;
		НовСтрока = СертификатОбъект.ВидыДокументов.Добавить();
		НовСтрока.ВидДокумента = Перечисления.ВидыЭД.ЗапросВыписки;
		НовСтрока.ИспользоватьДляПодписи = Истина;
		СертификатОбъект.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн;
		СертификатОбъект.Записать();
	КонецЕсли;
	
	Возврат НовыйСертификат;
	
КонецФункции



