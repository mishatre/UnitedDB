////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаПриСозданииНаСервере(ЭтаФорма, "ОбменУправлениеТорговлей103БухгалтерияПредприятия30");
	ЗаполнитьСписокОграниченийДляПользователей();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтаФорма);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоОрганизациямПриИзменении(Элемент)
	УстановитьДоступностьЭлементамФормы(Элементы.Организации);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоСкладамПриИзменении(Элемент)
	УстановитьДоступностьЭлементамФормы(Элементы.Склады);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоПодразделениямПриИзменении(Элемент)
	УстановитьДоступностьЭлементамФормы(Элементы.Подразделения);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементамФормы(ТекущаяСтраница = Неопределено)
	ЗначенияФлаговИСтраниц = Новый Соответствие();
	ЗначенияФлаговИСтраниц.Вставить(Элементы.Организации, ИспользоватьОтборПоОрганизациям);
	ЗначенияФлаговИСтраниц.Вставить(Элементы.Склады, ИспользоватьОтборПоСкладам);
	ЗначенияФлаговИСтраниц.Вставить(Элементы.Подразделения, ИспользоватьОтборПоПодразделениям);
	
	СтраницыОтбора = Новый Массив();
	Если ИспользоватьОтборПоОрганизациям Тогда
		СтраницыОтбора.Добавить(Элементы.Организации);
	КонецЕсли;
	Если ИспользоватьОтборПоСкладам Тогда
		СтраницыОтбора.Добавить(Элементы.Склады);
	КонецЕсли;
	Если ИспользоватьОтборПоПодразделениям Тогда
		СтраницыОтбора.Добавить(Элементы.Подразделения);
	КонецЕсли;
	Элементы.Страницы.Доступность = СтраницыОтбора.Количество() > 0.00;
	
	Если СтраницыОтбора.Количество() = 0.00 Тогда
		возврат;
	КонецЕсли;
	
	Элементы.Организации.Доступность = ИспользоватьОтборПоОрганизациям;
	Элементы.Подразделения.Доступность = ИспользоватьОтборПоПодразделениям;
	Элементы.Склады.Доступность = ИспользоватьОтборПоСкладам;
	Если ТекущаяСтраница <> Неопределено Тогда
		Если ЗначенияФлаговИСтраниц[ТекущаяСтраница] = Ложь И Элементы.Страницы.ТекущаяСтраница = ТекущаяСтраница Тогда
			Элементы.Страницы.ТекущаяСтраница = СтраницыОтбора[0];
		ИначеЕсли ЗначенияФлаговИСтраниц[ТекущаяСтраница] = Истина Тогда
			Элементы.Страницы.ТекущаяСтраница = ТекущаяСтраница;
		КонецЕсли;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = СтраницыОтбора[0];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьЭлементамФормы();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПодготовитьДанныеДляЗакрытияФормы();
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ ПРОЦЕДУРЫ И ФУНКЦИИ
&НаСервере
Процедура ЗаполнитьСписокОграниченийДляПользователей()
	Запрос = Новый Запрос();
	ТекстЗапроса = "
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяОрганизации.Организация,
	|	ВременнаяОрганизации.Выбран
	// 0
	|ПОМЕСТИТЬ ВременнаяОрганизации
	|ИЗ
	|	&Организации КАК ВременнаяОрганизации
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяСклады.Склад,
	|	ВременнаяСклады.Выбран
	// 1
	|ПОМЕСТИТЬ ВременнаяСклады
	|ИЗ
	|	&Склады КАК ВременнаяСклады
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяПодразделения.Подразделение,
	|	ВременнаяПодразделения.Выбран
	// 2
	|ПОМЕСТИТЬ ВременнаяПодразделения
	|ИЗ
	|	&Подразделения КАК ВременнаяПодразделения
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяКассы.Касса,
	|	ВременнаяКассы.Выбран
	// 3
	|ПОМЕСТИТЬ ВременнаяКассы
	|ИЗ
	|	&Кассы КАК ВременнаяКассы
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ВЫБОР	КОГДА ВременнаяОрганизации.Организация ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|	КОНЕЦ              КАК Выбран
	// 4
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВременнаяОрганизации
	|ПО
	|	ВременнаяОрганизации.Организация = Организации.Ссылка
	|ГДЕ
	|	(НЕ Организации.ПометкаУдаления)
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад,
	|	ВЫБОР	КОГДА ВременнаяСклады.Склад ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|	КОНЕЦ              КАК Выбран
	// 5
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВременнаяСклады
	|ПО
	|	ВременнаяСклады.Склад = Склады.Ссылка
	|ГДЕ
	|	(НЕ Склады.ЭтоГруппа)
	|	И (НЕ Склады.ПометкаУдаления)
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Подразделения.Ссылка КАК Подразделение,
	|	ВЫБОР	КОГДА ВременнаяПодразделения.Подразделение ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|	КОНЕЦ                КАК Выбран
	// 6
	|ИЗ
	|	Справочник.Подразделения КАК Подразделения
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВременнаяПодразделения
	|ПО
	|	ВременнаяПодразделения.Подразделение = Подразделения.Ссылка
	|ГДЕ
	|	(НЕ Подразделения.ПометкаУдаления)
	|;
	/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодЗапрос.Касса    КАК Касса,
	|	МАКСИМУМ(
	|	ВЫБОР	КОГДА ВременнаяКассы.Касса ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|	КОНЕЦ)                КАК Выбран
	// 7
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|(	ВЫБРАТЬ
	|		Кассы.Ссылка КАК Касса,
	|		Кассы.Владелец КАК Владелец
	|	ИЗ
	|		Справочник.Кассы КАК Кассы
	|	ГДЕ
	|		(НЕ Кассы.ПометкаУдаления)
	|		И (НЕ Кассы.ЭтоГруппа)
	|) КАК ПодЗапрос
	|ПО Организации.Ссылка = ПодЗапрос.Владелец
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВременнаяКассы
	|ПО
	|	ВременнаяКассы.Касса = ПодЗапрос.Касса
	|ГДЕ
	|	(НЕ Организации.ПометкаУдаления)
	|
	|СГРУППИРОВАТЬ ПО
	|	Организации.Ссылка,
	|	ПодЗапрос.Касса
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ПодЗапрос.Касса) > 0
	|";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организации", Организации.Выгрузить());
	Запрос.УстановитьПараметр("Склады", Склады.Выгрузить());
	Запрос.УстановитьПараметр("Подразделения", Подразделения.Выгрузить());
	Запрос.УстановитьПараметр("Кассы", Кассы.Выгрузить());
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Организации.Загрузить(РезультатЗапроса[4].Выгрузить());
	Склады.Загрузить(РезультатЗапроса[5].Выгрузить());
	Подразделения.Загрузить(РезультатЗапроса[6].Выгрузить());
	Кассы.Загрузить(РезультатЗапроса[7].Выгрузить());
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеДляЗакрытияФормы()
	УдалитьСтрокиИзТЧ("Организации");
	УдалитьСтрокиИзТЧ("Склады");
	УдалитьСтрокиИзТЧ("Подразделения");
	УдалитьСтрокиИзТЧ("Кассы");
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиИзТЧ(ИмяТЧ)
	
	МассивСтрок = ЭтаФорма[ИмяТЧ].НайтиСтроки(Новый Структура("Выбран", Ложь));
	Для каждого СтрокаТЧ Из МассивСтрок Цикл
		ЭтаФорма[ИмяТЧ].Удалить(СтрокаТЧ);
	КонецЦикла;
	
КонецПроцедуры