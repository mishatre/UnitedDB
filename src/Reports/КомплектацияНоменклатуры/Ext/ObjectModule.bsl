#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ НАЧАЛЬНОЙ НАСТРОЙКИ ОТЧЕТА

// Процедура установки начальных настроек отчета с использованием текста запроса
//
Процедура УстановитьНачальныеНастройки(ДополнительныеПараметры = Неопределено) Экспорт
	
	// Настройка общих параметров универсального отчета
	
	// Содержит название отчета, которое будет выводиться в шапке.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.мНазваниеОтчета = "Название отчета";
	УниверсальныйОтчет.мНазваниеОтчета = СокрЛП(ЭтотОбъект.Метаданные().Синоним);
	
	// Содержит признак необходимости отображения надписи и поля выбора раздела учета в форме настройки.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	 УниверсальныйОтчет.мВыбиратьИмяРегистра = Ложь;
	
	// Содержит имя регистра, по метаданным которого будет выполняться заполнение настроек отчета.
	// Тип: Строка.
	// Пример:
	// УниверсальныйОтчет.ИмяРегистра = "ТоварыНаСкладах";
	
	// Содержит признак необходимости вывода отрицательных значений показателей красным цветом.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ОтрицательноеКрасным = Истина;
	
	// Содержит признак необходимости вывода в отчет общих итогов.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.ВыводитьОбщиеИтоги = Ложь;
	
	// Содержит признак необходимости вывода детальных записей в отчет.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ВыводитьДетальныеЗаписи = Истина;
	
	// Содержит признак необходимости отображения флага использования свойств и категорий в форме настройки.
	// Тип: Булево.
	// Значение по умолчанию: Истина.
	// Пример:
	// УниверсальныйОтчет.мВыбиратьИспользованиеСвойств = Ложь;
	
	// Содержит признак использования свойств и категорий при заполнении настроек отчета.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.ИспользоватьСвойстваИКатегории = Истина;
	
	// Содержит признак использования простой формы настроек отчета без группировок колонок.
	// Тип: Булево.
	// Значение по умолчанию: Ложь.
	// Пример:
	// УниверсальныйОтчет.мРежимФормыНастройкиБезГруппировокКолонок = Истина;
	
	// Дополнительные параметры, переданные из отчета, вызвавшего расшифровку.
	// Информация, передаваемая в переменной ДополнительныеПараметры, может быть использована
	// для реализации специфичных для данного отчета параметрических настроек.
	
	// Описание исходного текста запроса.
	// При написании текста запроса рекомендуется следовать правилам, описанным в следующем шаблоне текста запроса:
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Запрос.Организация                   КАК Организация,
	|	Запрос.Регистратор                   КАК Регистратор,
	|	Запрос.Склад                         КАК Склад,
	|	Запрос.Комплект                      КАК Комплект,
	|	Запрос.ХарактеристикаКомплекта       КАК ХарактеристикаКомплекта,
	|	Запрос.Комплектующая                 КАК Комплектующая,
	|	Запрос.ХарактеристикаКомплектующей   КАК ХарактеристикаКомплектующей,
	|	Запрос.Период                        КАК Период,
	|	Запрос.КомплектКоличествоПриход      КАК КомплектКоличествоПриход,
	|	Запрос.КомплектКоличествоРасход      КАК КомплектКоличествоРасход,
	|	Запрос.КомплектующаяКоличествоРасход КАК КомплектующаяКоличествоРасход,
	|	Запрос.КомплектующаяКоличествоПриход КАК КомплектующаяКоличествоПриход
	|	//ПОЛЯ_СВОЙСТВА
	|	//ПОЛЯ_КАТЕГОРИИ
	|{ВЫБРАТЬ
	|	Организация.*                         КАК Организация,
	|	Склад.*                               КАК Склад,
	|	Регистратор.*                         КАК Регистратор,
	|	Комплект.*                            КАК Комплект,
	|	ХарактеристикаКомплекта.*             КАК ХарактеристикаКомплекта,
	|	Комплектующая.*                       КАК Комплектующая,
	|	ХарактеристикаКомплектующей.*         КАК ХарактеристикаКомплектующей,
	|	КомплектКоличествоПриход              КАК КомплектКоличествоПриход,
	|	КомплектКоличествоРасход              КАК КомплектКоличествоРасход,
	|	КомплектующаяКоличествоПриход         КАК КомплектующаяКоличествоПриход,
	|	КомплектующаяКоличествоРасход         КАК КомплектующаяКоличествоРасход,
	|	НАЧАЛОПЕРИОДА(Запрос.Период, День)    КАК ПериодДень,
	|	НАЧАЛОПЕРИОДА(Запрос.Период, Неделя)  КАК ПериодНеделя,
	|	НАЧАЛОПЕРИОДА(Запрос.Период, Месяц)   КАК ПериодМесяц,
	|	НАЧАЛОПЕРИОДА(Запрос.Период, Квартал) КАК ПериодКвартал,
	|	НАЧАЛОПЕРИОДА(Запрос.Период, Год)     КАК ПериодГод
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИЗ
	|(ВЫБРАТЬ
	|	Документ.Организация                                         КАК Организация,
	|	Документ.Ссылка                                              КАК Регистратор,
	|	Документ.Склад                                               КАК Склад,
	|	Документ.Номенклатура                                        КАК Комплект,
	|	Документ.ХарактеристикаНоменклатуры                          КАК ХарактеристикаКомплекта,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)               КАК Комплектующая,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаКомплектующей,
	|	Документ.Дата                                                КАК Период,
	|	МАКСИМУМ(ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|					ТОГДА Документ.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                               КАК КомплектКоличествоПриход,
	|	МАКСИМУМ(ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|					ТОГДА Документ.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                               КАК КомплектКоличествоРасход,
	|	0.00                                                         КАК КомплектующаяКоличествоРасход,
	|	0.00                                                         КАК КомплектующаяКоличествоПриход
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК Документ
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Документ.КомплектацияНоменклатуры.Комплектующие КАК ТаблицаКомплектующих
	|ПО
	|	ТаблицаКомплектующих.Ссылка = Документ.Ссылка
	|ГДЕ
	|	Документ.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И Документ.Проведен
	|СГРУППИРОВАТЬ ПО
	|	Документ.Организация,
	|	Документ.Ссылка,
	|	Документ.Склад,
	|	Документ.Номенклатура,
	|	Документ.ХарактеристикаНоменклатуры
	|{ГДЕ
	|	Документ.Организация.*                            КАК Организация,
	|	Документ.Ссылка.*                                 КАК Регистратор,
	|	Документ.Номенклатура                             КАК Комплект,
	|	Документ.ХарактеристикаНоменклатуры               КАК ХарактеристикаКомплекта,
	|	ТаблицаКомплектующих.Номенклатура.*               КАК Комплектующая,
	|	ТаблицаКомплектующих.ХарактеристикаНоменклатуры.* КАК ХарактеристикаКомплектующей,
	|	МАКСИМУМ(ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|					ТОГДА Документ.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                    КАК КомплектКоличествоПриход,
	|	МАКСИМУМ(ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|					ТОГДА Документ.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                    КАК КомплектКоличествоРасход,
	|	Документ.Дата                                     КАК Период,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, День)                КАК ПериодДень,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Неделя)              КАК ПериодНеделя,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Месяц)               КАК ПериодМесяц,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Квартал)             КАК ПериодКвартал,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Год)                 КАК ПериодГод
	|}
	|///////////////////////////////////////////////////////////////////////////////
	|ОБЪЕДИНИТЬ ВСЕ
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Организация                                          КАК Организация,
	|	Документ.Ссылка                                               КАК Регистратор,
	|	Документ.Склад                                                КАК Склад,
	|	Документ.Номенклатура                                         КАК Комплект,
	|	Документ.ХарактеристикаНоменклатуры                           КАК ХарактеристикаКомплекта,
	|	ТаблицаКомплектующие.Номенклатура                             КАК Комплектующая,
	|	ТаблицаКомплектующие.ХарактеристикаНоменклатуры               КАК ХарактеристикаКомплектующей,
	|	Документ.Дата                                                 КАК Период,
	|	0.00                                                          КАК КомплектКоличествоПриход,
	|	0.00                                                          КАК КомплектКоличествоРасход,
	|	СУММА(	ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|					ТОГДА ТаблицаКомплектующие.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                                КАК КомплектующаяКоличествоРасход,
	|	СУММА(	ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|					ТОГДА ТаблицаКомплектующие.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                                КАК КомплектующаяКоличествоПриход
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК Документ
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Документ.КомплектацияНоменклатуры.Комплектующие КАК ТаблицаКомплектующие
	|ПО
	|	ТаблицаКомплектующие.Ссылка = Документ.Ссылка
	|ГДЕ
	|	Документ.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
	|	И Документ.Проведен
	|{ГДЕ
	|	Документ.Организация.*                          КАК Организация,
	|	Документ.Ссылка.*                               КАК Регистратор,
	|	Документ.Номенклатура.*                         КАК Комплект,
	|	Документ.ХарактеристикаНоменклатуры.*           КАК ХарактеристикаКомплекта,
	|	ТаблицаКомплектующие.Номенклатура               КАК Комплектующая,
	|	ТаблицаКомплектующие.ХарактеристикаНоменклатуры КАК ХарактеристикаКомплектующей,
	|	СУММА(	ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Комплектация)
	|					ТОГДА ТаблицаКомплектующие.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                  КАК КомплектующаяКоличествоРасход,
	|	СУММА(	ВЫБОР	КОГДА Документ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияНоменклатуры.Разукомплектация)
	|					ТОГДА ТаблицаКомплектующие.Количество
	|					ИНАЧЕ 0.00
	|			КОНЕЦ)                                  КАК КомплектующаяКоличествоПриход,
	|	Документ.Дата                                   КАК Период,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, День)              КАК ПериодДень,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Неделя)            КАК ПериодНеделя,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Месяц)             КАК ПериодМесяц,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Квартал)           КАК ПериодКвартал,
	|	НАЧАЛОПЕРИОДА(Документ.Дата, Год)               КАК ПериодГод
	|}
	|СГРУППИРОВАТЬ ПО
	|	Документ.Организация,
	|	Документ.Ссылка,
	|	Документ.Склад,
	|	Документ.Номенклатура,
	|	Документ.ХарактеристикаНоменклатуры,
	|	ТаблицаКомплектующие.Номенклатура,
	|	ТаблицаКомплектующие.ХарактеристикаНоменклатуры
	|) КАК Запрос
	|//СОЕДИНЕНИЯ
	|{ГДЕ Запрос.Склад.* КАК Склад
	|	//УСЛОВИЯ_СВОЙСТВА
	|	//УСЛОВИЯ_КАТЕГОРИИ
	|}
	|{УПОРЯДОЧИТЬ ПО
	|	Комплект.*                            КАК Комплект,
	|	ХарактеристикаКомплекта.*             КАК ХарактеристикаКомплекта,
	|	Комплектующая.*                       КАК Комплектующая,
	|	ХарактеристикаКомплектующей.*         КАК ХарактеристикаКомплектующей,
	|	Период                                КАК Период,
	|	Регистратор.*                         КАК Регистратор,
	|	НачалоПериода(Запрос.Период, День)    КАК ПериодДень,
	|	НачалоПериода(Запрос.Период, Неделя)  КАК ПериодНеделя,
	|	НачалоПериода(Запрос.Период, Месяц)   КАК ПериодМесяц,
	|	НачалоПериода(Запрос.Период, Квартал) КАК ПериодКвартал,
	|	НачалоПериода(Запрос.Период, Год)     КАК ПериодГод,
	|	КомплектКоличествоПриход              КАК КомплектКоличествоПриход,
	|	КомплектКоличествоРасход              КАК КомплектКоличествоРасход,
	|	КомплектующаяКоличествоПриход         КАК КомплектующаяКоличествоПриход,
	|	КомплектующаяКоличествоРасход         КАК КомплектующаяКоличествоРасход
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|{ИТОГИ ПО
	|	Организация.*                         КАК Организация,
	|	Комплект.*                            КАК Комплект,
	|	Склад.*                               КАК Склад,
	|	ХарактеристикаКомплекта.*             КАК ХарактеристикаКомплекта,
	|	Комплектующая.*                       КАК Комплектующая,
	|	ХарактеристикаКомплектующей.*         КАК ХарактеристикаКомплектующей,
	|	Период                                КАК Период,
	|	Регистратор.*                         КАК Регистратор,
	|	НачалоПериода(Запрос.Период, День)    КАК ПериодДень,
	|	НачалоПериода(Запрос.Период, Неделя)  КАК ПериодНеделя,
	|	НачалоПериода(Запрос.Период, Месяц)   КАК ПериодМесяц,
	|	НачалоПериода(Запрос.Период, Квартал) КАК ПериодКвартал,
	|	НачалоПериода(Запрос.Период, Год)     КАК ПериодГод
	|	//ПСЕВДОНИМЫ_СВОЙСТВА
	|	//ПСЕВДОНИМЫ_КАТЕГОРИИ
	|}
	|ИТОГИ
	|	СУММА(КомплектКоличествоПриход),
	|	СУММА(КомплектКоличествоРасход),
	|	СУММА(КомплектующаяКоличествоПриход),
	|	СУММА(КомплектующаяКоличествоРасход)
	|	//ИТОГИ_СВОЙСТВА
	|	//ИТОГИ_КАТЕГОРИИ
	|ПО ОБЩИЕ,
	|	Комплект,
	|	Комплектующая
	|АВТОУПОРЯДОЧИВАНИЕ
	|";
	// В универсальном отчете включен флаг использования свойств и категорий.
	Если УниверсальныйОтчет.ИспользоватьСвойстваИКатегории Тогда
		
		// Добавление свойств и категорий поля запроса в таблицу полей.
		// Необходимо вызывать для каждого поля запроса, предоставляющего возможность использования свойств и категорий.
		
		// УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля(<ПсевдонимТаблицы>.<Поле> , <ПсевдонимПоля>, <Представление>, <Назначение>);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Запрос.Комплект",                  "Комплект",                    "Комплект",                     ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Запрос.ХарактеристикаКомплекта",    "ХарактеристикаКомплекта",     "Характеристика комплекта",     ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Запрос.Комплектующая",               "Комплектующая",               "Комплектующая",                ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура);
		УниверсальныйОтчет.ДобавитьСвойстваИКатегорииДляПоля("Запрос.ХарактеристикаКомплектующей", "ХарактеристикаКомплектующей", "Характеристика комплектующей", ПланыВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры);
		
		// Добавление свойств и категорий в исходный текст запроса.
		УниверсальныйОтчет.ДобавитьВТекстЗапросаСвойстваИКатегории(ТекстЗапроса);
		
	КонецЕсли;
		
	// Инициализация текста запроса построителя отчета
	УниверсальныйОтчет.ПостроительОтчета.Текст = ТекстЗапроса;
	
	// Представления полей отчета.
	// Необходимо вызывать для каждого поля запроса.
	// УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить(<ИмяПоля>, <ПредставлениеПоля>);
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ХарактеристикаКомплекта",       "Характеристика комплекта");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("ХарактеристикаКомплектующей",   "Характеристика комплектующей");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("КомплектКоличествоПриход",      "Комплект к-во приход");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("КомплектКоличествоРасход",      "Комплект к-во расход");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("КомплектующаяКоличествоПриход", "Комплектующая к-во приход");
	УниверсальныйОтчет.мСтруктураПредставлениеПолей.Вставить("КомплектующаяКоличествоРасход", "Комплектующая к-во расход");
	
	// Добавление показателей
	// Необходимо вызывать для каждого добавляемого показателя.
	// УниверсальныйОтчет.ДобавитьПоказатель(<ИмяПоказателя>, <ПредставлениеПоказателя>, <ВключенПоУмолчанию>, <Формат>, <ИмяГруппы>, <ПредставлениеГруппы>);
	УниверсальныйОтчет.ДобавитьПоказатель("КомплектКоличествоПриход",      "Комплект к-во приход", Истина, "ЧЦ=15; ЧДЦ=3");
	УниверсальныйОтчет.ДобавитьПоказатель("КомплектКоличествоРасход",      "Комплект к-во расход", Истина, "ЧЦ=15; ЧДЦ=3");
	УниверсальныйОтчет.ДобавитьПоказатель("КомплектующаяКоличествоПриход", "Комплектующая к-во приход", Истина, "ЧЦ=15; ЧДЦ=3");
	УниверсальныйОтчет.ДобавитьПоказатель("КомплектующаяКоличествоРасход", "Комплектующая к-во расход", Истина, "ЧЦ=15; ЧДЦ=3");
	
	// Добавление предопределенных группировок строк отчета.
	// Необходимо вызывать для каждой добавляемой группировки строки.
	// УниверсальныйОтчет.ДобавитьИзмерениеСтроки(<ПутьКДанным>);
	
	// Добавление предопределенных группировок колонок отчета.
	// Необходимо вызывать для каждой добавляемой группировки колонки.
	// УниверсальныйОтчет.ДобавитьИзмерениеКолонки(<ПутьКДанным>);
	
	// Добавление предопределенных отборов отчета.
	// Необходимо вызывать для каждого добавляемого отбора.
	// УниверсальныйОтчет.ДобавитьОтбор(<ПутьКДанным>);
	УниверсальныйОтчет.ДобавитьОтбор("Комплект");
	УниверсальныйОтчет.ДобавитьОтбор("Комплектующая");
	
	// Добавление предопределенных полей порядка отчета.
	// Необходимо вызывать для каждого добавляемого поля порядка.
	// УниверсальныйОтчет.ДобавитьПорядок(<ПутьКДанным>);
	
	// Установка связи подчиненных и родительских полей
	// УниверсальныйОтчет.УстановитьСвязьПолей(<ПутьКДанным>, <ПутьКДаннымРодитель>);
	
	// Установка связи полей и измерений
	// УниверсальныйОтчет.УстановитьСвязьПоляИИзмерения(<ИмяПоля>, <ИмяИзмерения>);
	
	// Установка представлений полей
	УниверсальныйОтчет.УстановитьПредставленияПолей(УниверсальныйОтчет.мСтруктураПредставлениеПолей, УниверсальныйОтчет.ПостроительОтчета);
	
	// Установка типов значений свойств в отборах отчета
	УниверсальныйОтчет.УстановитьТипыЗначенийСвойствДляОтбора();
	
	// Заполнение начальных настроек универсального отчета
	УниверсальныйОтчет.УстановитьНачальныеНастройки(Ложь);
	
	// Добавление дополнительных полей
	// Необходимо вызывать для каждого добавляемого дополнительного поля.
	// УниверсальныйОтчет.ДобавитьДополнительноеПоле(<ПутьКДанным>, <Размещение>, <Положение>);
	
КонецПроцедуры // УстановитьНачальныеНастройки()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ОТЧЕТА 
	
// Процедура формирования отчета
//
Процедура СформироватьОтчет(ТабличныйДокумент) Экспорт
	
	// Перед формирование отчета можно установить необходимые параметры универсального отчета.
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("ВидОперацииКомплектации", Перечисления.ВидыОперацийКомплектацияНоменклатуры.Комплектация);
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("ПустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
	УниверсальныйОтчет.ПостроительОтчета.Параметры.Вставить("ПустаяДата", '00010101000000');
	
	УниверсальныйОтчет.СформироватьОтчет(ТабличныйДокумент);

КонецПроцедуры // СформироватьОтчет()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура обработки расшифровки
//
Процедура ОбработкаРасшифровки(Расшифровка, Объект) Экспорт
	
	// Дополнительные параметры в расшифровывающий отчет можно передать
	// посредством инициализации переменной "ДополнительныеПараметры".
	
	ДополнительныеПараметры = Неопределено;
	УниверсальныйОтчет.ОбработкаРасшифровкиУниверсальногоОтчета(Расшифровка, Объект, ДополнительныеПараметры);
	
КонецПроцедуры // ОбработкаРасшифровки()

// Формирует структуру для сохранения настроек отчета
//
Процедура СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками) Экспорт
	
	УниверсальныйОтчет.СформироватьСтруктуруДляСохраненияНастроек(СтруктураСНастройками);
	
КонецПроцедуры // СформироватьСтруктуруДляСохраненияНастроек()

// Заполняет настройки отчета из структуры сохраненных настроек
//
Функция ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками) Экспорт
	
	Возврат УниверсальныйОтчет.ВосстановитьНастройкиИзСтруктуры(СтруктураСНастройками, ЭтотОбъект);
	
КонецФункции // ВосстановитьНастройкиИзСтруктуры()

// Содержит значение используемого режима ввода периода.
// Тип: Число.
// Возможные значения: 0 - произвольный период, 1 - на дату, 2 - неделя, 3 - декада, 4 - месяц, 5 - квартал, 6 - полугодие, 7 - год
// Значение по умолчанию: 0
// Пример:
// УниверсальныйОтчет.мРежимВводаПериода = 1;

#КонецЕсли
