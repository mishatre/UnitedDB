////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
Перем мВалютаРегламентированногоУчета Экспорт;

// Хранит структуру, содержащую параметры для определения договора, доступного в данном документе:
//    список допустимых видов договоров;
//    список допустимых способов ведения взаиморасчетов.
Перем мСтруктураПараметровДляПолученияДоговора Экспорт;

// Содержит значение функциональной опции "ИспользоватьОбменЭД"
Перем мИспользоватьОбменЭД Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой заказа или счета,
// разработанного методистами
//
// Возвращаемое значение:
//  Табличный документ - сформированная печатная форма
//
Функция ПечатьСчетаЗаказа(Тип)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номер,
	|	Дата,
	|	ДатаПоступления,
	|	ДоговорКонтрагента,
	|	Контрагент КАК Поставщик,
	|	Организация,
	|	СуммаДокумента,
	|	ВалютаДокумента,
	|	УчитыватьНДС,
	|	СуммаВключаетНДС
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика
	|
	|ГДЕ
	|	СчетНаОплатуПоставщика.Ссылка = &ТекущийДокумент";
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	Запрос = Новый Запрос;

	Макет = ПолучитьМакет("СчетЗаказ");

	// Определим параметры запроса и табличного документа  
	// в зависимости от необходимости отображения артикула поставщика  
	Если Тип = "ЗаказПоДаннымПоставщика" Тогда

		ОбластьШапки  = Макет.ПолучитьОбласть("ШапкаСАртикулом");
		ОбластьСтроки = Макет.ПолучитьОбласть("СтрокаСАртикулом");

		Запрос.УстановитьПараметр("Контрагент", ЭтотОбъект.Контрагент);

		ТекстПоляТовараДляТоваров = "
		|	ВЫБОР КОГДА (НаименованиеКонтрагента ЕСТЬ NULL ИЛИ НаименованиеКонтрагента = """") ТОГДА ВЫРАЗИТЬ (ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК Строка(100)) ИНАЧЕ НаименованиеКонтрагента КОНЕЦ КАК Товар,";

		ТекстПоляТовараДляУслуг = "
		|	ВЫБОР КОГДА (ВЫРАЗИТЬ (НаименованиеНоменклатурыКонтрагента КАК Строка(100)) ЕСТЬ NULL ИЛИ ВЫРАЗИТЬ (НаименованиеНоменклатурыКонтрагента КАК Строка(100)) = """") ТОГДА ВЫРАЗИТЬ(ЗаказПоставщику.Номенклатура.НаименованиеПолное КАК Строка(100)) ИНАЧЕ ВЫРАЗИТЬ (НаименованиеНоменклатурыКонтрагента КАК Строка(100)) КОНЕЦ КАК Товар,";

		ТекстПоляАртикула = ",
			|	АртикулКонтрагента";

		ТекстВыборкиАртикула = ", 
			|	АртикулНоменклатурыКонтрагента КАК АртикулКонтрагента";

		ТекстВыборкиНоменклатуры = ", 
			|	ВЫРАЗИТЬ(НаименованиеНоменклатурыКонтрагента КАК Строка(100)) КАК НаименованиеКонтрагента";

		ТекстИсточникАртикула = "
			|	ЛЕВОЕ ВНЕШНЕЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
			|	ПО НоменклатураКонтрагентов.Номенклатура = ЗаказПоставщику.Номенклатура
			|	И  НоменклатураКонтрагентов.Контрагент = &Контрагент";

		ТекстГруппировкиАртикулаИНоменклатуры = ", 
			|	АртикулНоменклатурыКонтрагента,
			|	ВЫРАЗИТЬ(НаименованиеНоменклатурыКонтрагента КАК Строка(100))";

	Иначе

		ТекстПоляТовараДляТоваров = "
		|	ВЫРАЗИТЬ (ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК Строка(100)) КАК Товар,";

		СтрокаВыборкиПоляСодержания = ОбработкаТабличныхЧастей.ПолучитьЧастьЗапросаДляВыбораСодержания("ЗаказПоставщику");
		
		ТекстПоляТовараДляУслуг = "
		|	" + СтрокаВыборкиПоляСодержания + " КАК Товар,";

		ОбластьШапки  = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтроки = Макет.ПолучитьОбласть("Строка");

		ТекстПоляАртикула                     = "";
		ТекстВыборкиАртикула                  = "";
		ТекстИсточникАртикула                 = "";
		ТекстГруппировкиАртикулаИНоменклатуры = "";

	КонецЕсли;

	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.Текст ="
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,"+ТекстПоляТовараДляТоваров+"
	|	ВложенныйЗапрос.Количество,
	|   ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление  КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Цена,
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.СуммаНДС,
	|	(1)                                             КАК Метка,
	|	ВложенныйЗапрос.Характеристика,
	|	NULL КАК Серия" + ТекстПоляАртикула + "
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ЗаказПоставщику.Номенклатура,
	|		ЗаказПоставщику.ЕдиницаИзмерения,
	|		ЗаказПоставщику.Цена                        КАК Цена,
	|		МИНИМУМ(ЗаказПоставщику.НомерСтроки)        КАК НомерСтроки,
	|		СУММА(ЗаказПоставщику.Количество)           КАК Количество,
	|		СУММА(ЗаказПоставщику.Сумма     )           КАК Сумма,
	|		СУММА(ЗаказПоставщику.СуммаНДС  )           КАК СуммаНДС,
	|		ЗаказПоставщику.ХарактеристикаНоменклатуры  КАК Характеристика" + ТекстВыборкиАртикула + ТекстВыборкиНоменклатуры + "
	|	ИЗ
	|		Документ.СчетНаОплатуПоставщика.Товары КАК ЗаказПоставщику" + ТекстИсточникАртикула + "
	|
	|	ГДЕ
	|		ЗаказПоставщику.Ссылка = &ТекущийДокумент
	|
	|	СГРУППИРОВАТЬ ПО
	|		ЗаказПоставщику.Номенклатура,
	|		ЗаказПоставщику.ЕдиницаИзмерения,
	|		ЗаказПоставщику.Цена,
	|		ЗаказПоставщику.ХарактеристикаНоменклатуры" + ТекстГруппировкиАртикулаИНоменклатуры + "
	|	) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказПоставщику.Номенклатура," + ТекстПоляТовараДляУслуг + "
	|	ЗаказПоставщику.Количество,
	|   ЗаказПоставщику.НомерСтроки КАК НомерСтроки,
	|	ЗаказПоставщику.Номенклатура.ЕдиницаХраненияОстатков,
	|	ЗаказПоставщику.Цена,
	|	ЗаказПоставщику.Сумма,
	|	ЗаказПоставщику.СуммаНДС,
	|	(3)                           КАК Метка,
	|	NULL                          КАК Характеристика,
	|	NULL КАК Серия" + ТекстВыборкиАртикула + "
	|	
	|ИЗ (ВЫБРАТЬ 
	|		Номенклатура 							КАК Номенклатура,
	|		ВЫРАЗИТЬ(Содержание КАК Строка(100)) 	КАК Содержание,
	|		Цена 									КАК Цена,
	|		СтавкаНДС 								КАК СтавкаНДС,
	|		Сумма(Количество) 						КАК Количество,
	|		Сумма(Сумма) 							КАК Сумма,
	|		Сумма(СуммаНДС) 						КАК СуммаНДС,
	|		Минимум(НомерСтроки) 					КАК НомерСтроки
	|	ИЗ  Документ.СчетНаОплатуПоставщика.Услуги 
	|	ГДЕ Ссылка = &ТекущийДокумент
	|	СГРУППИРОВАТЬ ПО Номенклатура, ВЫРАЗИТЬ(Содержание КАК Строка(100)), Цена, СтавкаНДС
	|	) КАК ЗаказПоставщику "+ ТекстИсточникАртикула + "
	|
	|
	|УПОРЯДОЧИТЬ ПО Метка ВОЗР, НомерСтроки
	|
	|";

	ЗапросТовары = Запрос.Выполнить().Выгрузить();

	ТабДокумент = Новый ТабличныйДокумент;
	Если Тип = "ЗаказПоДаннымПоставщика" Тогда
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетНаОплатуПоставщика_СчетЗаказПоДаннымПоставщика";
	Иначе
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетНаОплатуПоставщика_СчетЗаказ";
	КонецЕсли;

	Руководители = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизации(Шапка.Организация, Шапка.Дата,);
	Руководитель = Руководители.Руководитель;
	Бухгалтер    = Руководители.ГлавныйБухгалтер;

	// Выводим шапку накладной

	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, "Счет на оплату поставщика");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ОбластьМакета.Параметры.ПредставлениеПоставщика  = ФормированиеПечатныхФорм.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Поставщик, Шапка.Дата), "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ТабДокумент.Вывести(ОбластьМакета);

	ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
	ОбластьМакета.Параметры.ПредставлениеПолучателя = ФормированиеПечатныхФорм.ОписаниеОрганизации(УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата), "ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,");
	ОбластьМакета.Параметры.Получатель = Шапка.Организация;
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести табличную часть
	ТабДокумент.Вывести(ОбластьШапки);

	Сумма    = 0;
	СуммаНДС = 0;

	Для каждого ВыборкаСтрокТовары Из ЗапросТовары Цикл

		Если не ЗначениеЗаполнено(ВыборкаСтрокТовары.Номенклатура) Тогда
			Сообщить("В одной из строк не заполнено значение номенклатуры - строка при печати пропущена.", СтатусСообщения.Важное);
			Продолжить;
		КонецЕсли;

		ОбластьСтроки.Параметры.Заполнить(ВыборкаСтрокТовары);
		ОбластьСтроки.Параметры.НомерСтроки = ЗапросТовары.Индекс(ВыборкаСтрокТовары) + 1;
		ОбластьСтроки.Параметры.Товар = СокрЛП(ВыборкаСтрокТовары.Товар) + ФормированиеПечатныхФорм.ПредставлениеСерий(ВыборкаСтрокТовары);

		ТабДокумент.Вывести(ОбластьСтроки);
		Сумма    = Сумма    + ВыборкаСтрокТовары.Сумма;
		СуммаНДС = СуммаНДС + ВыборкаСтрокТовары.СуммаНДС;

	КонецЦикла;

	// Вывести Итого
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");
	ОбластьМакета.Параметры.Всего = ОбщегоНазначения.ФорматСумм(Сумма);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести ИтогоНДС
	Если Шапка.УчитыватьНДС Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоНДС");
		ОбластьМакета.Параметры.ВсегоНДС = ОбщегоНазначения.ФорматСумм(ЗапросТовары.Итог("СуммаНДС"));
		ОбластьМакета.Параметры.НДС = ?(Шапка.СуммаВключаетНДС, "В том числе НДС:", "Сумма НДС:");
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЕсли;

	// Вывести Сумму прописью
	ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
	СуммаКПрописи = Сумма + ?(Шапка.СуммаВключаетНДС, 0, СуммаНДС);
	ОбластьМакета.Параметры.ИтоговаяСтрока ="Всего наименований " + ЗапросТовары.Количество()
	+ ", на сумму " + ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Шапка.ВалютаДокумента);
	ОбластьМакета.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(СуммаКПрописи, Шапка.ВалютаДокумента);
	ТабДокумент.Вывести(ОбластьМакета);

	// Вывести подписи
	ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
	ОбластьМакета.Параметры.ФИОРуководителя = "/"+ Руководитель + "/";
	ОбластьМакета.Параметры.ФИОБухгалтера   = "/"+ Бухгалтер    + "/";
	ТабДокумент.Вывести(ОбластьМакета);

	Возврат ТабДокумент;

КонецФункции // ПечатьСчетаЗаказа()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	Если ЭтоНовый() Тогда
		Предупреждение("Документ можно распечатать только после его записи");
		Возврат;
	КонецЕсли;

	Если Не РаботаСДиалогами.ПроверитьМодифицированность(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	// Получить экземпляр документа на печать
	Если ИмяМакета      = "Заказ" Тогда

		// печать заявки от покупателя
		ТабДокумент     = ПечатьСчетаЗаказа(ИмяМакета);

	ИначеЕсли ИмяМакета      = "ЗаказПоДаннымПоставщика" Тогда

		// печать заявки от покупателя
		ТабДокумент     = ПечатьСчетаЗаказа(ИмяМакета);
		
	ИначеЕсли ТипЗнч(ИмяМакета) = Тип("ДвоичныеДанные") Тогда

		ТабДокумент = УниверсальныеМеханизмы.НапечататьВнешнююФорму(Ссылка, ИмяМакета);
		
		Если ТабДокумент = Неопределено Тогда
			Возврат
		КонецЕсли; 
		
	КонецЕсли; 

	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, ""), Ссылка);

КонецПроцедуры // Печать

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	СтруктураМакетов = Новый Структура("Заказ", "Счет на оплату поставщика");
	СтруктураМакетов.Вставить("ЗаказПоДаннымПоставщика", "Счет на оплату поставщика (по данным поставщика)");

	Возврат СтруктураМакетов;
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	СтруктураОбязательныхПолей = 
	Новый Структура("Организация, ВалютаДокумента, СтруктурнаяЕдиница,
					|Контрагент, ДоговорКонтрагента, КратностьВзаиморасчетов");

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок);

	//Организация в документе должна совпадать с организацией, указанной в договоре взаиморасчетов.
	УправлениеВзаиморасчетами.ПроверитьСоответствиеОрганизацииДоговоруВзаиморасчетов(Организация, ДоговорКонтрагента, ДоговорКонтрагента.Организация, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()


// Процедура проверяет правильность заполнения табличной части "Товары".
//
Процедура ПроверитьЗаполнениеТабличнойЧастиТовары(Отказ, Заголовок)
	
	СтруктураОбязательныхРеквизитов = Новый Структура("Номенклатура, Количество, Цена, СтавкаНДС, ЕдиницаИзмерения, Коэффициент");
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Товары", СтруктураОбязательныхРеквизитов, Отказ, Заголовок);	
	
КонецПроцедуры	

// Процедура выполняет проверку правильности заполнения табличной части "Услуги"
//
Процедура ПроверитьЗаполнениеТабличнойЧастиУслуги(Отказ, Заголовок)
	
	СтруктураОбязательныхРеквизитов = Новый Структура("Номенклатура, Количество, Цена, СтавкаНДС, Содержание");
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Услуги", СтруктураОбязательныхРеквизитов, Отказ, Заголовок);	
	
КонецПроцедуры	

// Процедура выполняет проверку правильности заполнения табличной части "ВозвратнаяТара"
//
Процедура ПроверитьЗаполнениеТабличнойЧастиВозвратнаяТара(Отказ, Заголовок)
	
	СтруктураОбязательныхРеквизитов = Новый Структура("Номенклатура, Количество, Цена");
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "ВозвратнаяТара", СтруктураОбязательныхРеквизитов, Отказ, Заголовок);	
	
КонецПроцедуры	

// Очищает ненужные строки табличных частей
//
Процедура ОчиститьНенужныеТабличныеЧасти() Экспорт

	// Если договор с комитентом, то надо почистить закладку "Услуги".
	Если Услуги.Количество() > 0
	   И ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда

		Услуги.Очистить();

	КонецЕсли;

КонецПроцедуры // ОчиститьНенужныеТабличныеЧасти()

// Процедура заполнения таб.части Товары
//
Процедура ЗаполнитьТабЧастиТоварыНаОсновании(ДокОснование)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегЗаказы.Номенклатура КАК Номенклатура,
	|	РегЗаказы.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	РегЗаказы.Цена,
	|	СУММА(РегЗаказы.КоличествоОстаток) КАК КолОстаток,
	|	СУММА(РегЗаказы.КоличествоОстаток * РегЗаказы.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент / ДокЗаказ.Коэффициент) КАК КолОстатокЕдиницыДокумента,
	|	ДокЗаказ.СтавкаНДС,
	|	ДокЗаказ.ЕдиницаИзмерения,
	|	ДокЗаказ.Коэффициент,
	|	СУММА(РегЗаказы.СуммаВзаиморасчетовОстаток) КАК СуммаВзаиморасчетовОстаток,
	|	СУММА(РегЗаказы.СуммаУпрОстаток) КАК СуммаУпрОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокЗаказы.Номенклатура КАК Номенклатура,
	|		ДокЗаказы.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		ДокЗаказы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ДокЗаказы.Коэффициент КАК Коэффициент,
	|		ДокЗаказы.Цена КАК Цена,
	|		ДокЗаказы.СтавкаНДС КАК СтавкаНДС,
	|		СУММА(ДокЗаказы.СуммаНДС) КАК СуммаНДС
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДокЗаказТовары.Номенклатура КАК Номенклатура,
	|			ДокЗаказТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|			ДокЗаказТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|			ДокЗаказТовары.Коэффициент КАК Коэффициент,
	|			ДокЗаказТовары.Цена КАК Цена,
	|			ДокЗаказТовары.СтавкаНДС КАК СтавкаНДС,
	|			ДокЗаказТовары.СуммаНДС КАК СуммаНДС
	|		ИЗ
	|			Документ.ЗаказПоставщику.Товары КАК ДокЗаказТовары
	|		
	|		ГДЕ
	|			ДокЗаказТовары.Ссылка = &Заказ И
	|			ДокЗаказТовары.Ссылка.Проведен И
	|			Не ДокЗаказТовары.Номенклатура.Услуга И
	|			НЕ ДокЗаказТовары.Ссылка.ПометкаУдаления
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокКорректировкаТовары.Номенклатура,
	|			ДокКорректировкаТовары.ХарактеристикаНоменклатуры,
	|			ДокКорректировкаТовары.ЕдиницаИзмерения,
	|			ДокКорректировкаТовары.Коэффициент,
	|			ДокКорректировкаТовары.Цена,
	|			ДокКорректировкаТовары.СтавкаНДС,
	|			ДокКорректировкаТовары.СуммаНДС
	|		ИЗ
	|			Документ.КорректировкаЗаказаПоставщику.Товары КАК ДокКорректировкаТовары
	|		
	|		ГДЕ
	|			ДокКорректировкаТовары.Ссылка.ЗаказПоставщику = &Заказ И
	|			Не ДокКорректировкаТовары.Номенклатура.Услуга И
	|			НЕ ДокКорректировкаТовары.Ссылка.ПометкаУдаления И
	|			ДокКорректировкаТовары.Ссылка.Проведен) КАК ДокЗаказы
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокЗаказы.Номенклатура,
	|		ДокЗаказы.ХарактеристикаНоменклатуры,
	|		ДокЗаказы.ЕдиницаИзмерения,
	|		ДокЗаказы.Коэффициент,
	|		ДокЗаказы.Цена,
	|		ДокЗаказы.СтавкаНДС) КАК ДокЗаказ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаОстатков, ЗаказПоставщику = &Заказ И СтатусПартии = &Статус) КАК РегЗаказы
	|		ПО ДокЗаказ.Номенклатура               = РегЗаказы.Номенклатура
	|		 И ДокЗаказ.ХарактеристикаНоменклатуры = РегЗаказы.ХарактеристикаНоменклатуры
	|		 И ДокЗаказ.Цена                       = РегЗаказы.Цена
	|		 И ДокЗаказ.СтавкаНДС                  = РегЗаказы.СтавкаНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	РегЗаказы.Номенклатура,
	|	РегЗаказы.ХарактеристикаНоменклатуры,
	|	РегЗаказы.Цена,
	|	ДокЗаказ.СтавкаНДС,
	|	ДокЗаказ.ЕдиницаИзмерения,
	|	ДокЗаказ.Коэффициент";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ДатаОстатков", ОбщегоНазначения.ПолучитьДатуОстатков(ЭтотОбъект));
	Запрос.УстановитьПараметр("Заказ",   ДокОснование);
	
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыПартийТоваров.Купленный);
	
	РезультатЗапроса = Запрос.Выполнить();
	Обход = РезультатЗапроса.Выбрать();
	
	Пока Обход.Следующий() Цикл

		Если Обход.КолОстаток <= 0 Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрока = Товары.Добавить();
		НоваяСтрока.Номенклатура     = Обход.Номенклатура;
		НоваяСтрока.ЕдиницаИзмерения = Обход.ЕдиницаИзмерения;
		НоваяСтрока.Коэффициент      = Обход.Коэффициент;
		НоваяСтрока.Количество       = Обход.КолОстатокЕдиницыДокумента;
		НоваяСтрока.ХарактеристикаНоменклатуры = Обход.ХарактеристикаНоменклатуры;
		НоваяСтрока.СтавкаНДС        = Обход.СтавкаНДС;
		НоваяСтрока.Цена             = Ценообразование.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Обход.Цена,
				ДокОснование.ВалютаДокумента, ВалютаДокумента,
				ЗаполнениеДокументов.КурсДокумента(ДокОснование, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
				ЗаполнениеДокументов.КратностьДокумента(ДокОснование, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета)),
				Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов,
				ДокОснование.СуммаВключаетНДС,
				УчитыватьНДС,
				СуммаВключаетНДС,
				УчетНДС.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
			
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(НоваяСтрока, ЭтотОбъект);
		
		ОбработкаТабличныхЧастей.РассчитатьКоличествоМестТабЧасти( НоваяСтрока, ЭтотОбъект); 
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти      ( НоваяСтрока, ЭтотОбъект);
		
	КонецЦикла;
	
	ОбработкаТабличныхЧастей.ЗаполнитьПлановуюСебестоимостьНаОсновании(ЭтотОбъект, ДокОснование, мВалютаРегламентированногоУчета);
	
КонецПроцедуры // ЗаполнитьТабЧастиТоварыНаОсновании()

// Процедура заполнения таб.части Услуги
//
Процедура ЗаполнитьТабЧастиУслугиНаОсновании(ДокОснование)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегЗаказы.Номенклатура КАК Номенклатура,
	|	ДокЗаказ.Содержание КАК Содержание,
	|	РегЗаказы.Цена,
	|	СУММА(РегЗаказы.КоличествоОстаток) КАК КолОстаток,
	|	СУММА(РегЗаказы.СуммаУпрОстаток) КАК СуммаУпрОстаток,
	|	СУММА(РегЗаказы.СуммаВзаиморасчетовОстаток) КАК СуммаВзаиморасчетовОстаток,
	|	ДокЗаказ.СтавкаНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокЗаказы.Номенклатура КАК Номенклатура,
	|		ДокЗаказы.Содержание КАК Содержание,
	|		ДокЗаказы.Цена КАК Цена,
	|		ДокЗаказы.СтавкаНДС КАК СтавкаНДС,
	|		СУММА(ДокЗаказы.СуммаНДС) КАК СуммаНДС
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДокЗаказУслуги.Номенклатура КАК Номенклатура,
	|			ВЫРАЗИТЬ(ДокЗаказУслуги.Содержание КАК Строка(1000)) КАК Содержание,
	|			ДокЗаказУслуги.Цена КАК Цена,
	|			ДокЗаказУслуги.СтавкаНДС КАК СтавкаНДС,
	|			ДокЗаказУслуги.СуммаНДС КАК СуммаНДС
	|		ИЗ
	|			Документ.ЗаказПоставщику.Услуги КАК ДокЗаказУслуги
	|		
	|		ГДЕ
	|			ДокЗаказУслуги.Ссылка = &Заказ И
	|			ДокЗаказУслуги.Ссылка.Проведен И
	|			ДокЗаказУслуги.Номенклатура.Услуга И
	|			НЕ ДокЗаказУслуги.Ссылка.ПометкаУдаления
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокКорректировкаУслуги.Номенклатура,
	|			ВЫРАЗИТЬ(ДокКорректировкаУслуги.Содержание КАК Строка(1000)) КАК Содержание,
	|			ДокКорректировкаУслуги.Цена,
	|			ДокКорректировкаУслуги.СтавкаНДС,
	|			ДокКорректировкаУслуги.СуммаНДС
	|		ИЗ
	|			Документ.КорректировкаЗаказаПоставщику.Услуги КАК ДокКорректировкаУслуги
	|		
	|		ГДЕ
	|			ДокКорректировкаУслуги.Ссылка.ЗаказПоставщику = &Заказ И
	|			НЕ ДокКорректировкаУслуги.Ссылка.ПометкаУдаления И
	|			ДокКорректировкаУслуги.Номенклатура.Услуга И
	|			ДокКорректировкаУслуги.Ссылка.Проведен) КАК ДокЗаказы
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокЗаказы.Номенклатура,
	|		ДокЗаказы.Содержание,
	|		ДокЗаказы.Цена,
	|		ДокЗаказы.СтавкаНДС) КАК ДокЗаказ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаОстатков, ЗаказПоставщику = &Заказ И СтатусПартии = &Статус) КАК РегЗаказы
	|		ПО ДокЗаказ.Номенклатура = РегЗаказы.Номенклатура И ДокЗаказ.Цена = РегЗаказы.Цена
	|          И ДокЗаказ.СтавкаНДС  = РегЗаказы.СтавкаНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	РегЗаказы.Номенклатура,
	|	ДокЗаказ.Содержание,
	|	РегЗаказы.Цена,
	|	ДокЗаказ.СтавкаНДС";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ДатаОстатков", ОбщегоНазначения.ПолучитьДатуОстатков(ЭтотОбъект));
	Запрос.УстановитьПараметр("Заказ",   ДокОснование);
	Запрос.УстановитьПараметр("Статус",  Перечисления.СтатусыПартийТоваров.Купленный);

	РезультатЗапроса = Запрос.Выполнить();
	Обход = РезультатЗапроса.Выбрать();

	Пока Обход.Следующий() Цикл

		Если Обход.КолОстаток <= 0 Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрока = Услуги.Добавить();
		НоваяСтрока.Номенклатура = Обход.Номенклатура;
		НоваяСтрока.Содержание   = СокрЛП(Обход.Содержание);
		НоваяСтрока.Количество   = Обход.КолОстаток;
		НоваяСтрока.СтавкаНДС    = Обход.СтавкаНДС;
		НоваяСтрока.Цена         = Ценообразование.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Обход.Цена,
				ДокОснование.ВалютаДокумента, ВалютаДокумента,
				ЗаполнениеДокументов.КурсДокумента(ДокОснование, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
				ЗаполнениеДокументов.КратностьДокумента(ДокОснование, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета)),
				Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов,
				ДокОснование.СуммаВключаетНДС,
				УчитыватьНДС,
				СуммаВключаетНДС,
				УчетНДС.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
			
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(НоваяСтрока, ЭтотОбъект);

		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти( НоваяСтрока, ЭтотОбъект);

	КонецЦикла;

КонецПроцедуры // ЗаполнитьТабЧастиТоварыНаОсновании()

// Процедура заполнения таб.части Тара
//
Процедура ЗаполнитьТабЧастиТараНаОсновании(ДокОснование)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегЗаказы.Номенклатура КАК Номенклатура,
	|	РегЗаказы.Цена,
	|	СУММА(РегЗаказы.КоличествоОстаток) КАК КолОстаток,
	|	РегЗаказы.СуммаВзаиморасчетовОстаток,
	|	РегЗаказы.СуммаУпрОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокЗаказы.Номенклатура КАК Номенклатура,
	|		ДокЗаказы.Цена КАК Цена
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДокЗаказТовары.Номенклатура КАК Номенклатура,
	|			ДокЗаказТовары.Цена КАК Цена
	|		ИЗ
	|			Документ.ЗаказПоставщику.ВозвратнаяТара КАК ДокЗаказТовары
	|		
	|		ГДЕ
	|			ДокЗаказТовары.Ссылка = &Заказ И
	|			(ДокЗаказТовары.Ссылка.Проведен) И
	|			(НЕ(ДокЗаказТовары.Ссылка.ПометкаУдаления)) И
	|			(НЕ(ДокЗаказТовары.Номенклатура.Услуга))
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокКорректировкаТовары.Номенклатура,
	|			ДокКорректировкаТовары.Цена
	|		ИЗ
	|			Документ.КорректировкаЗаказаПоставщику.ВозвратнаяТара КАК ДокКорректировкаТовары
	|		
	|		ГДЕ
	|			ДокКорректировкаТовары.Ссылка.ЗаказПоставщику = &Заказ И
	|			(НЕ(ДокКорректировкаТовары.Ссылка.ПометкаУдаления)) И
	|			(ДокКорректировкаТовары.Ссылка.Проведен) И
	|			(НЕ(ДокКорректировкаТовары.Номенклатура.Услуга))) КАК ДокЗаказы) КАК ДокЗаказ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(&ДатаОстатков, ЗаказПоставщику = &Заказ И СтатусПартии = &Статус) КАК РегЗаказы
	|		ПО ДокЗаказ.Номенклатура = РегЗаказы.Номенклатура И ДокЗаказ.Цена = РегЗаказы.Цена
	|
	|СГРУППИРОВАТЬ ПО
	|	РегЗаказы.Номенклатура,
	|	РегЗаказы.Цена,
	|	РегЗаказы.СуммаВзаиморасчетовОстаток,
	|	РегЗаказы.СуммаУпрОстаток";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("ДатаОстатков", ОбщегоНазначения.ПолучитьДатуОстатков(ЭтотОбъект));
	Запрос.УстановитьПараметр("Заказ",   ДокОснование);
	Запрос.УстановитьПараметр("Статус",  Перечисления.СтатусыПартийТоваров.ВозвратнаяТара);

	РезультатЗапроса = Запрос.Выполнить();
	Обход = РезультатЗапроса.Выбрать();

	Пока Обход.Следующий() Цикл

		Если Обход.КолОстаток <= 0 Тогда
			Продолжить;
		КонецЕсли;

		НоваяСтрока = ВозвратнаяТара.Добавить();
		НоваяСтрока.Количество   = Обход.КолОстаток;
		НоваяСтрока.Номенклатура = Обход.Номенклатура;
		НоваяСтрока.Цена         = Ценообразование.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Обход.Цена,
				ДокОснование.ВалютаДокумента, ВалютаДокумента,
				ЗаполнениеДокументов.КурсДокумента(ДокОснование, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
				ЗаполнениеДокументов.КратностьДокумента(ДокОснование, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета)),
				Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов,
				Ложь, Ложь, Ложь, 0);
			
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(НоваяСтрока, ЭтотОбъект);

	КонецЦикла;

КонецПроцедуры // ЗаполнитьТабЧастиТараНаОсновании()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если НЕ ОбменДанными.Загрузка  Тогда
		
		// Заголовок для сообщений об ошибках.
		Заголовок = "Невозможно записать документ:";

		// Сформируем структуру реквизитов шапки документа
		СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

		// Проверим правильность заполнения шапки документа
		ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
		
		ОчиститьНенужныеТабличныеЧасти();

		// Проверка заполнения единицы измерения мест и количества мест
		ОбработкаТабличныхЧастей.ПриЗаписиПроверитьЕдиницуИзмеренияМест(Товары);
		
		ОбработкаТабличныхЧастей.ПриЗаписиПроверитьСтавкуНДС(ЭтотОбъект, Товары);
		ОбработкаТабличныхЧастей.ПриЗаписиПроверитьСтавкуНДС(ЭтотОбъект, Услуги);
		
		// Проверка правильности заполнения табличных частей.
		ПроверитьЗаполнениеТабличнойЧастиТовары(Отказ, Заголовок);
		ПроверитьЗаполнениеТабличнойЧастиУслуги(Отказ, Заголовок);
        ПроверитьЗаполнениеТабличнойЧастиВозвратнаяТара(Отказ, Заголовок);

		// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
		СуммаДокумента = УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);
		
	КонецЕсли; 
	
КонецПроцедуры // ПередЗаписью

// Процедура - обработчик события "ОбработкаЗаполнения".
//
Процедура ОбработкаЗаполнения(Основание)
	ДокументОснование = Основание;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Событие") Тогда
		
		// Заполнение шапки
		Комментарий       = Основание.Комментарий;
		Ответственный     = Основание.Ответственный;
		КонтактноеЛицоКонтрагента    = Основание.КонтактноеЛицо;
		Контрагент        = Основание.Контрагент;
		Организация = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию( глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");

		СтруктураПараметровДляПолученияДоговора = ЗаполнениеДокументов.ПолучитьСтруктуруПараметровДляПолученияДоговораПокупки();
        ДоговорКонтрагента 	= ЗаполнениеДокументов.ПолучитьДоговорПоОрганизацииИКонтрагенту(Организация, Контрагент, СтруктураПараметровДляПолученияДоговора);

	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию( ЭтотОбъект, Основание);
		УправлениеЗаказами.УстановитьДатуОплатыПоДоговору(ЭтотОбъект);
		ЗаполнитьТабЧастиТоварыНаОсновании( Основание);
		ЗаполнитьТабЧастиУслугиНаОсновании( Основание);
		ЗаполнитьТабЧастиТараНаОсновании  ( Основание);
		
		КонтактноеЛицоКонтрагента = Основание.КонтактноеЛицоКонтрагента;
		
	КонецЕсли;
	
КонецПроцедуры

мВалютаРегламентированногоУчета = глЗначениеПеременной("ВалютаРегламентированногоУчета");

мИспользоватьОбменЭД = ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД");
