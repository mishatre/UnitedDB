////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
Процедура ВывестиЭДНаПросмотр(СтрокаДанных)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НаименованиеФайла");
	СтруктураПараметров.Вставить("НаправлениеЭД");
	СтруктураПараметров.Вставить("Контрагент");
	СтруктураПараметров.Вставить("УникальныйИдентификатор");
	СтруктураПараметров.Вставить("ВладелецЭД");
	СтруктураПараметров.Вставить("ЭлектронныйДокумент");
	СтруктураПараметров.Вставить("АдресХранилища");
	СтруктураПараметров.Вставить("ФайлАрхива");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, СтрокаДанных);
	Если ЗначениеЗаполнено(СтруктураПараметров.ЭлектронныйДокумент) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЭлектронныйДокумент", СтруктураПараметров.ЭлектронныйДокумент);
		ПараметрыФормы.Вставить("ВладелецЭД",          СтруктураПараметров.ВладелецЭД);
		ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаЗагрузкиПросмотраЭД", ПараметрыФормы, ЭтаФорма);
	Иначе
		ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаЗагрузкиПросмотраЭД",
			Новый Структура("СтруктураЭД", СтруктураПараметров), ЭтаФорма, СтруктураПараметров.УникальныйИдентификатор);
	КонецЕсли;
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ТаблицаДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВывестиЭДНаПросмотр(ТаблицаДанных[ВыбраннаяСтрока]);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтруктурЭД = "";
	Если Параметры.Свойство("СтруктураЭД", МассивСтруктурЭД) Тогда
		Для Каждого СтруктураОбмена Из МассивСтруктурЭД Цикл
			НоваяСтрока = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураОбмена);
			Если ЗначениеЗаполнено(НоваяСтрока.ЭлектронныйДокумент) Тогда
				НоваяСтрока.ПредставлениеЭД = ЭлектронныеДокументыСлужебный.ПолучитьПредставлениеЭД(НоваяСтрока.ЭлектронныйДокумент);
			Иначе
				НоваяСтрока.ПредставлениеЭД = НоваяСтрока.НаименованиеФайла;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Параметры.Свойство("МассивСсылокЭД", МассивСтруктурЭД) Тогда
		Соответствие = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСтруктурЭД, "ВладелецФайла, НаправлениеЭД");
		Для Каждого КлючИЗначение Из Соответствие Цикл
			НоваяСтрока = ТаблицаДанных.Добавить();
			НоваяСтрока.ЭлектронныйДокумент = КлючИЗначение.Ключ;
			НоваяСтрока.ВладелецЭД = КлючИЗначение.Значение.ВладелецФайла;
			НоваяСтрока.НаправлениеЭД = КлючИЗначение.Значение.НаправлениеЭД;
			НоваяСтрока.ПредставлениеЭД = ЭлектронныеДокументыСлужебный.ПолучитьПредставлениеЭД(НоваяСтрока.ЭлектронныйДокумент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


