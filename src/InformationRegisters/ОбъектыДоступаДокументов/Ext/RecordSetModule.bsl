
//хранит промежуточную таблицу записей регистра, нужна для возможности выполнения свертки
Перем мТаблицаОбъектов;

// Процедура добавления в таблицу движений новый объектов доступа документа
// Объекты – может быть значением реквизита документа, например, Организация
// или массивом таких значений (результат выгрузки колонки ТЧ)
Процедура ДобавитьОбъектыДоступа(Объекты) Экспорт
	
	Если мТаблицаОбъектов = Неопределено Тогда
		мТаблицаОбъектов = Новый ТаблицаЗначений;
		мТаблицаОбъектов.Колонки.Добавить("ДокументСсылка");
		мТаблицаОбъектов.Колонки.Добавить("ОбъектДоступа");		
	КонецЕсли;
	
	Если ТипЗнч(Объекты) = Тип("Массив") Тогда
		Для Каждого ОбъектДоступа ИЗ Объекты Цикл
			Если ЗначениеЗаполнено(ОбъектДоступа) И БазовыйОбъектДоступа(ОбъектДоступа) Тогда
				СтрОбъекта = мТаблицаОбъектов.Добавить();
				СтрОбъекта.ОбъектДоступа = ОбъектДоступа;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если ЗначениеЗаполнено(Объекты) И БазовыйОбъектДоступа(Объекты) Тогда
			СтрОбъекта = мТаблицаОбъектов.Добавить();
			СтрОбъекта.ОбъектДоступа = Объекты;
		КонецЕсли;
	КонецЕсли;
	
	мТаблицаОбъектов.Свернуть("ДокументСсылка, ОбъектДоступа");
						
КонецПроцедуры

// Процедура переноса значений из промежуточной таблицы
Процедура ДвижениеПоРегистру() Экспорт
	
	мТаблицаОбъектов.ЗаполнитьЗначения(Отбор.ДокументСсылка.Значение, "ДокументСсылка");
	Загрузить(мТаблицаОбъектов);
	
КонецПроцедуры

// Проверка, что данный объект «непустой», и относится к объектам доступа.
Функция БазовыйОбъектДоступа(ОбъектДоступа)
			
	Возврат ОбъектДоступа <> Неопределено И ЭтотОбъект.Метаданные().Измерения.ОбъектДоступа.Тип.СодержитТип(ТипЗнч(ОбъектДоступа));
	
КонецФункции
