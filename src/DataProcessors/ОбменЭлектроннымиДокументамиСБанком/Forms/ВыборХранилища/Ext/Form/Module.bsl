&НаКлиенте
Перем ВнешнийПодключаемыйМодуль;
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Хранилище Из Параметры.Хранилища Цикл
		Элементы.ИдентификаторХранилища.СписокВыбора.Добавить(Хранилище);
	КонецЦикла;
	
	Если Параметры.Хранилища.Количество() = 1 Тогда
		Элементы.ИдентификаторХранилища.Доступность = Ложь;
		ТекущийЭлемент = Элементы.ПИН;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ВнешнийПодключаемыйМодуль = ЭлектронныеДокументыСлужебныйКлиент.ВнешнийПодключаемыйМодуль(Параметры.СоглашениеЭД);
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ХранилищеПриИзменении(Элемент)

	УстановитьДоступностьЭлементов();
	ПИН = "";
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ИдентификаторХранилища) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбрано хранилище'"), , "ИдентификаторХранилища");
		Возврат;
	КонецЕсли;
	
	
	Если Элементы.ПИН.Доступность Тогда
		ПинКодУстановлен = ЭлектронныеДокументыСлужебныйКлиент.УстановитьPINКодХранилища(ВнешнийПодключаемыйМодуль,
		                                                                                 ИдентификаторХранилища,
		                                                                                 ПИН)
	Иначе
		ПинКодУстановлен = Истина;
	КонецЕсли;
	
	Если ПинКодУстановлен Тогда
		ОповеститьОВыборе(ИдентификаторХранилища);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()

	Элементы.ПИН.Доступность = ЗначениеЗаполнено(ИдентификаторХранилища)
		И ЭлектронныеДокументыСлужебныйКлиент.ТребуетсяУстановкаPINКодаХранилища(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища) = Истина
		И НЕ ЭлектронныеДокументыСлужебныйКлиент.УстановленPINКодХранилища(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища) = Истина;

КонецПроцедуры


