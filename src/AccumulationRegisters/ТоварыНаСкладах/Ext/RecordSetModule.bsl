Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений
// Переменные, используемые в процедурах контроля остатков
Перем МетаданныеДокумента, МетаданныеТабЧасти, ИмяДокумента, ИмяТабличнойЧасти, ИмяТаблицы, Заголовок, СтруктураШапкиДокумента, Отказ; 
Перем ИспользоватьУказаниеСерийНоменклатурыПриРезервировании;


// Выполняет приход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьПриход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Приход);

КонецПроцедуры // ВыполнитьПриход()

// Выполняет расход по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьРасход() Экспорт

	ОбщегоНазначения.ВыполнитьДвижениеПоРегистру(ЭтотОбъект, ВидДвиженияНакопления.Расход);

КонецПроцедуры // ВыполнитьРасход()

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщение.
//
// Параметры:
//  ДокументОбъект     - объект проводимого документа, 
//  ИмяТабЧасти        - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапки     - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  ОтказПроведения    - флаг отказа в проведении,
//  ЗаголовокСообщения - строка, заголовок сообщения об ошибке проведения.
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабЧасти, СтруктураШапки, ОтказПроведения, ЗаголовокСообщения) Экспорт
    Если ИмяТабЧасти<>"" Тогда  //Имя таб части может быть не указана для документа КомплектацияНоменклатуры с видом операции Разукомплектация
		Если ДокументОбъект[ИмяТабЧасти].Количество()=0 Тогда //если в таб.части ничего нет - не надо проверять
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если УправлениеДопПравамиПользователей.РазрешеноПревышениеОстаткаТоваровНаСкладе() Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураШапкиДокумента = СтруктураШапки;
	ИмяТабличнойЧасти 	= ИмяТабЧасти;
	Отказ 				= ОтказПроведения;
	Заголовок 			= ЗаголовокСообщения;

	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;

	Если ИмяТабЧасти <>"" Тогда
		ИмяТаблицы = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	Иначе
		ИмяТаблицы = ИмяДокумента;
	КонецЕсли;

	ИспользоватьУказаниеСерийНоменклатурыПриРезервировании = Константы.ИспользоватьУказаниеСерийНоменклатурыПриРезервировании.Получить();
	Если ИмяТабЧасти <>"" Тогда
		МетаданныеТабЧасти       = МетаданныеДокумента.ТабличныеЧасти[ИмяТабЧасти];
	КонецЕсли;
	

	Если ИмяДокумента = "ПеремещениеТоваров" Тогда
		КонтрольОстатков_ПеремещениеТоваров();
	ИначеЕсли 	ИмяДокумента = "РеализацияТоваровУслуг" Тогда
		Если ИмяТабличнойЧасти = "ВозвратнаяТара" Тогда
			КонтрольОстатков_ЗаказВТабЧасти();
		Иначе	
			КонтрольОстатков_Реализация_ОтчетОРознПродажах_ЧекККМ(ДокументОбъект);
		КонецЕсли;

	ИначеЕсли ИмяДокумента = "ОтчетОРозничныхПродажах" ИЛИ
			ИмяДокумента = "ЧекККМ" Тогда
		КонтрольОстатков_Реализация_ОтчетОРознПродажах_ЧекККМ(ДокументОбъект);
	ИначеЕсли ИмяДокумента = "ВозвратТоваровПоставщику" Тогда
		КонтрольОстатков_ЗаказВТабЧасти();
	ИначеЕсли 	ИмяДокумента = "ТребованиеНакладная"  ИЛИ
				ИмяДокумента = "РасходныйОрдерНаТовары"  ИЛИ
				ИмяДокумента = "СписаниеТоваров"  Тогда
		КонтрольОстатков_ЗаказВТабЧасти();
	ИначеЕсли ИмяДокумента = "ПередачаТоваров" Тогда
		КонтрольОстатков_СкладВШапке_ЗаказВШапке();
		
	ИначеЕсли ИмяДокумента = "КомплектацияНоменклатуры" Тогда
		КонтрольОстатков_СкладВШапке_ЗаказВШапке();
	ИначеЕсли ИмяДокумента = "КорректировкаСерийИХарактеристикТоваров" ИЛИ
		ИмяДокумента = "КорректировкаКачестваТоваров" Тогда
		КонтрольОстатков_БезЗаказа();
		
	Иначе
		 Сообщить("Для документа "+ДокументОбъект+" не предусмотрен вызов процедуры 'Контроль остатков' модуля регистра 'Товары на складах'");
	КонецЕсли;
	
	//вернем обратно признак отказа от проведения документа 
	ОтказПроведения = Отказ;
	ЗаголовокСообщения = Заголовок;

КонецПроцедуры // КонтрольОстатков()

//Процедура контролирует остаток по данному регистру для документа ПеремещениеТоваров
//Особенность данного документа: 
//1) необходимость дополнительного контроля по регистру ТоварыКПолучениюНаСклады
//2) разные критерии определения количества, списываемого из резерва, в зависимости от того указан ли в шапке внутренний заказ
Процедура КонтрольОстатков_ПеремещениеТоваров()
	
	ЕстьСерия           = МетаданныеТабЧасти.Реквизиты.Найти("СерияНоменклатуры")            <> Неопределено;
	ЕстьХарактеристика  = МетаданныеТабЧасти.Реквизиты.Найти("ХарактеристикаНоменклатуры")	 <> Неопределено;
	ЕстьКоэффициент     = МетаданныеТабЧасти.Реквизиты.Найти("Коэффициент")                  <> Неопределено;
	ЕстьКачество        = МетаданныеТабЧасти.Реквизиты.Найти("Качество")                  	 <> Неопределено;
	
	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ  Ссылка = &ДокументСсылка
	|";

	Запрос = Новый Запрос;

	// Установим параметры запроса
	ЗаполнитьОбщиеПараметрыЗапроса(Запрос);
	Запрос.УстановитьПараметр("Склад",                 СтруктураШапкиДокумента.Склад);
	Запрос.УстановитьПараметр("ПустойОрдер",           Документы.ПриходныйОрдерНаТовары.ПустаяСсылка());
	
	МассивПустыхСсылок = ОбщегоНазначения.МассивПустыхЗначений(МетаданныеТабЧасти.Реквизиты.ДокументРезерва.Тип);
	Запрос.УстановитьПараметр("МассивПустыхЗаказов",		 МассивПустыхСсылок);


	ТекстЗапроса = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура                                       	КАК Номенклатура,
	|	Док.Номенклатура.Представление                         	КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление 	КАК ЕдиницаХраненияОстатковПредставление,
	|   %ПОЛЕ_Док_Характеристика% 								КАК ХарактеристикаНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Характеристика%) 				КАК ХарактеристикаНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Серия% 										КАК СерияНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Серия%) 						КАК СерияНоменклатурыПредставление,
	|	Док.ДокументРезерва                                    	КАК ДокументРезерва,
	|   &Склад                                                 	КАК Склад, 
	|   %ПОЛЕ_Док_Качество% 									КАК Качество,
	|   %ПОЛЕ_Док_Количество% 									КАК ДокументКоличество,
	|   %ПОЛЕ_ОстатокБезСерии_Количество% 						КАК ОстатокБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(Остатки.КоличествоОстаток),0)         КАК ОстатокКоличество,
	|	ЕстьNull(МАКСИМУМ(Резервы.КоличествоОстаток),0)         КАК РезервыКоличество,
	|	%ПОЛЕ_РезервыПоСерии_Количество%                    	КАК РезервыПоСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПередаче.КоличествоОстаток),0) КАК КПередачеКоличество,
	|	%ПОЛЕ_КПередачеБезСерии_Количество% 					КАК КПередачеБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПолучению.КоличествоОстаток),0)КАК КПолучению,
	|	Сумма(ВЫБОР КОГДА 
	|			Док.ДокументРезерва ССЫЛКА Документ.ПриходныйОрдерНаТовары 
	|		ТОГДА %ПОЛЕ_Док_Количество_Зап% 
	|		ИНАЧЕ 0 
	|		КОНЕЦ)												КАК КПолучениюПоДокументуКоличество,
	|	ЕстьNull(МАКСИМУМ(РезервыПоДокументу.КоличествоОстаток),0) КАК РезервыПоДокументуКоличество,
	|	%ПОЛЕ_РезервыПоДокументуБезСерии_Количество% 			КАК РезервыПоДокументуБезСерииКоличество,
	|	0 														КАК КПередачеПоДокументуКоличество,
	|	0              											КАК КПередачеПоДокументуБезСерииКоличество


	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	
	//таблица остатков на складах с учетом серий номенклатуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад И
	|		%ВыборкаПоНоменклатуре% 
	|		//УСЛОВИЕ_Качество
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура
	| //СОЕДИНЕНИЕ_Характеристика_Остатки
	| //СОЕДИНЕНИЕ_Серия_Остатки
	| //СОЕДИНЕНИЕ_Качество_Остатки
	
	//таблица остатков на складах без учета серий номенклатуры
	|//ЗАПРОС_ОстаткиБезСерии

	//таблица товаров в резерве на складе без учета серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
	|		%ВыборкаПоНоменклатуре%) КАК Резервы
	|ПО 
	|	Док.Номенклатура                = Резервы.Номенклатура
	| //СОЕДИНЕНИЕ_Характеристика_Резервы
	| //СОЕДИНЕНИЕ_ДокументРезерва_Резервы
	| //СОЕДИНЕНИЕ_Качество_Резервы
	
	//таблица товаров в резерве на складе с учетом серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
	|//ЗАПРОС_РезервыПоСерии

	//таблица резервов по документу: количество, которое списывается за счет резерва
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ ТЧ.Номенклатура
	|	//ПОЛЕ_ТЧ_Характеристика
	|	//ПОЛЕ_ТЧ_Серия
	|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
	|	,ВЫБОР КОГДА Сумма(ТЧ.ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
	|   	Сумма(ТЧ.ДокументКоличество)
	|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
	|	КОНЕЦ КАК КоличествоОстаток
	|	ИЗ
	|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
	|		(ВЫБРАТЬ  
	|			Номенклатура 
	|			//ПОЛЕ_Характеристика
	|			//ПОЛЕ_Серия
	|			,%ПОЛЕ_Заказ%
	|			,%ПОЛЕ_Количество% КАК ДокументКоличество
	|		ИЗ 
	|			Документ." + ИмяТаблицы +"
	|		ГДЕ  Ссылка = &ДокументСсылка
	|			И %УсловиеДокументРезерва%
	|		СГРУППИРОВАТЬ ПО
	|			Номенклатура 
	|			,%ПОЛЕ_Заказ%
	|			//ПОЛЕ_Характеристика
	|			//ПОЛЕ_Серия
	|		) КАК ТЧ
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
	|			%ВыборкаПоНоменклатуре%
	|			) КАК ВремРезервы
	|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
	|		//СОЕДИНЕНИЕ_Характеристика_ВремРезервы
	|		//СОЕДИНЕНИЕ_Серия_ВремРезервы
	|		//СОЕДИНЕНИЕ_ДокументРезерва_ВремРезервы
	|	СГРУППИРОВАТЬ ПО 
	|		ТЧ.Номенклатура 
	|		//ПОЛЕ_ТЧ_Характеристика
	|		//ПОЛЕ_ТЧ_Серия
	|) КАК РезервыПоДокументу
	|ПО 
	|Док.Номенклатура                 = РезервыПоДокументу.Номенклатура
	|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументу
	|//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу
	|//Док_УсловиеДокументРезерва
	
	//таблица резервов по документу без учета серий: количество, которое списывается за счет резерва 
    |//ЗАПРОС_РезервыПоДокументуБезСерии

	
	//товары к передаче со складов
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад = &Склад И
	|		%ВыборкаПоНоменклатуре%
	|	//УСЛОВИЕ_Качество
	|	) КАК ТоварыКПередаче
	|ПО 
	|	Док.Номенклатура                = ТоварыКПередаче.Номенклатура
	| //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередаче
	| //СОЕДИНЕНИЕ_Серия_ТоварыКПередаче
	| //СОЕДИНЕНИЕ_Качество_ТоварыКПередаче
	
	//товары к передаче со складов без учета серий
	|//ЗАПРОС_ТоварыКПередачеБезСерии
	
	// Если мы перемещаем товар, то товар поступивший по ордеру не может быть перемещен без указания этого
	// ордера в качестве документа резрерва, поэтому остатки регистра "Товары к получению на склады" надо
	// вычесть из свободного остатка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПолучениюНаСклады.Остатки(, Склад = &Склад И
	|		%ВыборкаПоНоменклатуре% 
	|		// Документ получения может быть только ордером, причем ордера без права продажи не учитываем,
	|       // поскольку они уже учтены в регистре резервов
	|		И ДокументПолучения ССЫЛКА Документ.ПриходныйОрдерНаТовары
	|       И НЕ ДокументПолучения.БезПраваПродажи
	|		//УСЛОВИЕ_Качество
	|		) КАК ТоварыКПолучению
	|ПО 
	|	Док.Номенклатура                = ТоварыКПолучению.Номенклатура
	| //СОЕДИНЕНИЕ_Характеристика_ТоварыКПолучению
	| //СОЕДИНЕНИЕ_Серия_ТоварыКПолучению
	| //СОЕДИНЕНИЕ_Качество_ТоварыКПолучению
	| И (Док.ДокументРезерва = &ПустойОрдер ИЛИ Не Док.ДокументРезерва ССЫЛКА Документ.ПриходныйОрдерНаТовары)
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И Не Док.Номенклатура.Комплект
	|	И Не Док.Номенклатура.Услуга
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|   %ПОЛЕ_Док_Характеристика%,
	|   %ПОЛЕ_Док_Серия%,
	|   %ПОЛЕ_Док_Качество%, 									
	|	 Док.ДокументРезерва 
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыНаСкладах.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|ИТОГИ СУММА (ДокументКоличество), МАКСИМУМ(ОстатокБезСерииКоличество), МАКСИМУМ(РезервыКоличество),
	|         МАКСИМУМ(КПередачеКоличество), МАКСИМУМ(РезервыПоДокументуКоличество), Максимум(КПередачеБезСерииКоличество),
	|			МАКСИМУМ(РезервыПоДокументуБезСерииКоличество), МАКСИМУМ(КПередачеПоДокументуКоличество), 
	|		Максимум(КПередачеПоДокументуБезСерииКоличество), Максимум(КПолучениюПоДокументуКоличество)
	|ПО Номенклатура
	|   //ПОЛЕ_Характеристика
	|	//ПОЛЕ_Качество
	|";

	ТекстЗапросаОстаткиБезСерии = "";
	ТекстЗапросаКПередачеБезСерии = "";
	ТекстЗапросаРезервыПоСерии = "";
	ТекстЗапросаРезервыПоДокументуБезСерии = "";

	Если ЕстьСерия Тогда
		 ТекстЗапросаОстаткиБезСерии = "
		 |ЛЕВОЕ СОЕДИНЕНИЕ
		 |	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад И
		 |		%ВыборкаПоНоменклатуре% 
		 |		//УСЛОВИЕ_Качество
		 |	) КАК ОстаткиБезСерии
		 |ПО 
		 |	Док.Номенклатура                = ОстаткиБезСерии.Номенклатура
		 | //СОЕДИНЕНИЕ_Характеристика_ОстаткиБезСерии
		 | //СОЕДИНЕНИЕ_Качество_ОстаткиБезСерии";
		 
		 ТекстЗапросаКПередачеБезСерии = "
		 |ЛЕВОЕ СОЕДИНЕНИЕ
		 |	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад = &Склад И
		 |		%ВыборкаПоНоменклатуре%
		 |	//УСЛОВИЕ_Качество
		 |	) КАК ТоварыКПередачеБезСерии
		 |ПО 
		 |	Док.Номенклатура                = ТоварыКПередачеБезСерии.Номенклатура
		 | //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередачеБезСерии
 		 | //СОЕДИНЕНИЕ_Качество_ТоварыКПередачеБезСерии ";


		 //резервирование может происходить в разрезе серий
		 Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
			 ТекстЗапросаРезервыПоСерии = "
			 |ЛЕВОЕ СОЕДИНЕНИЕ
			 |	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
			 |		%ВыборкаПоНоменклатуре%) КАК РезервыПоСерии
			 |ПО 
			 |	Док.Номенклатура                = РезервыПоСерии.Номенклатура
			 | //СОЕДИНЕНИЕ_Характеристика_РезервыПоСерии
			 | //СОЕДИНЕНИЕ_Серия_РезервыПоСерии
			 | //СОЕДИНЕНИЕ_Качество_Резервы
			 | //СОЕДИНЕНИЕ_ДокументРезерва_Резервы ";
			 
			 ТекстЗапросаРезервыПоДокументуБезСерии = "
			 |ЛЕВОЕ СОЕДИНЕНИЕ
			 |	(ВЫБРАТЬ ТЧ.Номенклатура
			 |	//ПОЛЕ_ТЧ_Характеристика
			 |	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
			 |	,ВЫБОР КОГДА Сумма(ТЧ.ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
			 |   	Сумма(ТЧ.ДокументКоличество)
			 |	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
			 |	КОНЕЦ КАК КоличествоОстаток
			 |	ИЗ
			 |		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
			 |		(ВЫБРАТЬ  
			 |			Номенклатура 
			 |			//ПОЛЕ_Характеристика
			 |			,%ПОЛЕ_Заказ%
			 |			,%ПОЛЕ_Количество% КАК ДокументКоличество
			 |		ИЗ 
			 |			Документ." + ИмяТаблицы +"
			 |		ГДЕ  Ссылка = &ДокументСсылка
			 |			И %УсловиеДокументРезерва%
			 |		СГРУППИРОВАТЬ ПО
			 |			Номенклатура 
			 |			,%ПОЛЕ_Заказ%
			 |			//ПОЛЕ_Характеристика
			 |		) КАК ТЧ
			 |	ЛЕВОЕ СОЕДИНЕНИЕ 
			 |		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
			 |			%ВыборкаПоНоменклатуре%
			 |			) КАК ВремРезервы
			 |	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
			 |		//СОЕДИНЕНИЕ_Характеристика_ВремРезервы
			 |		//СОЕДИНЕНИЕ_ДокументРезерва_ВремРезервы
			 |	СГРУППИРОВАТЬ ПО 
			 |		ТЧ.Номенклатура 
			 |		//ПОЛЕ_ТЧ_Характеристика
			 |) КАК РезервыПоДокументуБезСерии
			 |ПО 
			 |Док.Номенклатура                 = РезервыПоДокументуБезСерии.Номенклатура
			 |//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументуБезСерии
			 |//Док_УсловиеДокументРезерва
			 |";

          КонецЕсли;
	 КонецЕсли; //Если ЕстьСерия Тогда

	 ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ОстаткиБезСерии",			ТекстЗапросаОстаткиБезСерии);
	 ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ТоварыКПередачеБезСерии",	ТекстЗапросаКПередачеБезСерии);
     ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоСерии",				ТекстЗапросаРезервыПоСерии);
	 ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоДокументуБезСерии",	ТекстЗапросаРезервыПоДокументуБезСерии);


	 ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_ОстатокБезСерии_Количество%",				?(ТекстЗапросаОстаткиБезСерии<>"",				"ЕстьNull(МАКСИМУМ(ОстаткиБезСерии.КоличествоОстаток),0)",		"0")); 
	 ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_КПередачеБезСерии_Количество%",				?(ТекстЗапросаКПередачеБезСерии<>"",			"ЕстьNull(МАКСИМУМ(ТоварыКПередачеБезСерии.КоличествоОстаток),0)",	"0")); 
     ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоСерии_Количество%",				?(ТекстЗапросаРезервыПоСерии<>"",				"ЕстьNull(МАКСИМУМ(РезервыПоСерии.КоличествоОстаток),0)",		"0")); 
     ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументуБезСерии_Количество%",	?(ТекстЗапросаРезервыПоДокументуБезСерии<>"",	"ЕстьNull(МАКСИМУМ(РезервыПоДокументуБезСерии.КоличествоОстаток),0)",	"0")); 

	ДополнитьТекстЗапроса(ТекстЗапроса,ЕстьХарактеристика,,ЕстьСерия,,ЕстьКоэффициент,ЕстьКачество,ложь,ТекстЗапросаСписокНоменклатуры); 
	
	Если ЕстьСерия Тогда
		Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда 

			//резервы по документу имеет смысл определять с точностью до серии - заказ указывается в таб части, т.е. может быть заказ покупателя
			//(в шапке может быть указан внутренний заказ, в этом случае резервы по документу всегда без серий)
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_ТЧ_Серия",							",ТЧ.СерияНоменклатуры");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Серия",								",СерияНоменклатуры");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_ВремРезервы",			"И (ТЧ.СерияНоменклатуры = ВремРезервы.СерияНоменклатуры ИЛИ НЕ ЕстьNull(ВЫРАЗИТЬ(ТЧ.ДокументРезерва КАК Документ.ЗаказПокупателя).ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей,ложь))");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу",	"И Док.СерияНоменклатуры = РезервыПоДокументу.СерияНоменклатуры");
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_ДокументРезерва_Резервы",		" И Док.ДокументРезерва  в (&МассивПустыхЗаказов)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Заказ%",								"ДокументРезерва");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_ДокументРезерва_ВремРезервы",	"И ТЧ.ДокументРезерва = ВремРезервы.ДокументРезерва");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%УсловиеДокументРезерва%",					" ДокументРезерва НЕ В (&МассивПустыхЗаказов)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//Док_УсловиеДокументРезерва",				"И Док.ДокументРезерва НЕ В (&МассивПустыхЗаказов)");

	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	ОбработатьРезультатЗапроса(Выборка,ЕстьСерия,ТекстЗапросаРезервыПоДокументуБезСерии<>"");

КонецПроцедуры

//Процедура контролирует остаток по данному регистру для документов РеализацияТоваровИУслуг, ЧекККМ и ОтчетОРозничныхПродажах (таб части Товары и СоставНабора)
//Особенность документа - наличие дополнительной табличной части "Состав набора"
//
// Параметры:
//  ДокументОбъект     - объект проводимого документа, 
//
Процедура КонтрольОстатков_Реализация_ОтчетОРознПродажах_ЧекККМ(ДокументОбъект)
	ЕстьСоставНабора       = Ложь;
	Если ДокументОбъект.СоставНабора.Количество() > 0 Тогда
		ЕстьСоставНабора = Истина;
	КонецЕсли;
	ЕстьКачество           = МетаданныеТабЧасти.Реквизиты.Найти("Качество")                  	<> Неопределено;
    ЕстьСпособСписания     = МетаданныеТабЧасти.Реквизиты.Найти("СпособСписанияОстаткаТоваров")	<> Неопределено;
    ЕстьСкладВТабЧасти     = МетаданныеТабЧасти.Реквизиты.Найти("Склад")	<> Неопределено;


	// Текст вложенного запроса, ограничивающего номенклатуру при получении остатков
	Если ЕстьСоставНабора Тогда
		ТекстЗапросаСписокНоменклатуры = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Док.Номенклатура КАК Номенклатура
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		Док.Номенклатура
		|	ИЗ
		|		Документ." + ИмяТаблицы +" КАК Док
		|	ГДЕ
		|		Док.Ссылка = &ДокументСсылка
		|		И НЕ Док.Номенклатура.Комплект
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Док.Номенклатура
		|	ИЗ
		|		Документ." + ИмяДокумента + ".СоставНабора КАК Док
		|	ГДЕ
		|		Док.Ссылка = &ДокументСсылка
		|
		|	) КАК Док
		|";

		ТекстЗапросаРеквизитыДокумента = "
		|	(ВЫБРАТЬ
		//строки ТЧ Товары которые не являются набором
		|		Ссылка,
		|		Номенклатура,
		|		%ПОЛЕ_Склад%,
		|		%ПОЛЕ_СпособСписанияОстаткаТоваров% КАК СпособСписанияОстаткаТоваров,   
		|		%ПОЛЕ_Качество% КАК Качество,                       
		|		ХарактеристикаНоменклатуры,
		|		СерияНоменклатуры,
		|		Коэффициент,
		|		%ПОЛЕ_ЗаказПокупателя% КАК ЗаказПокупателя, 
		|		Количество
		|		ИЗ
		|			Документ." + ИмяТаблицы + " 
		|		ГДЕ
		|			Ссылка = &ДокументСсылка
		|			И НЕ Номенклатура.Комплект
		|
		//строки ТЧ СоставНабора 
		|		ОБЪЕДИНИТЬ ВСЕ
		|
		|		ВЫБРАТЬ
		|		Ссылка,
		|		Номенклатура,
		|		Склад,
		|		СпособСписанияОстаткаТоваров,
		|		Качество,
		|		ХарактеристикаНоменклатуры,
		|		СерияНоменклатуры,
		|		Коэффициент,
		|		ЗаказПокупателя,
		|		Количество
		|		ИЗ
		|			(ВЫБРАТЬ
		|				ДокНаб.Ссылка,
		|				ДокНаб.Номенклатура,
		|               %ПОЛЕ_Набор_Склад% КАК Склад,
		|				%ПОЛЕ_Набор_СпособСписанияОстаткаТоваров% КАК СпособСписанияОстаткаТоваров,   
		|				%ПОЛЕ_Набор_Качество% КАК Качество,                       
		|				ДокНаб.ХарактеристикаНоменклатуры,
		|				ДокНаб.СерияНоменклатуры,
		|				ДокНаб.ЕдиницаИзмерения.Коэффициент КАК Коэффициент,
		|				%ПОЛЕ_Набор_ЗаказПокупателя% КАК ЗаказПокупателя,
		|				ДокНаб.Количество * ДокТов.Количество * ДокТов.Коэффициент КАК Количество
		|			ИЗ
		|				Документ." + ИмяДокумента + ".СоставНабора   КАК ДокНаб
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ." + ИмяТаблицы + " КАК ДокТов
		|					ПО ДокТов.КлючСтроки = ДокНаб.КлючСтроки
		|					 И ДокТов.Ссылка     = &ДокументСсылка
		|			ГДЕ
		|				ДокНаб.Ссылка = &ДокументСсылка
		|			) КАК Набор
		|
		|		) 
		|";
		
		//есть состав набора - склады берутся из двух табличных частей
		ЗапросСклады = новый Запрос;
		ЗапросСклады.Текст = "Выбрать РАЗЛИЧНЫЕ Склад 
		|ИЗ Документ."+ИмяТаблицы+"
		|ГДЕ Ссылка=&ДокументСсылка 
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ Склад ИЗ 
		|Документ." + ИмяДокумента + ".СоставНабора 
		|ГДЕ Ссылка=&ДокументСсылка И Склад<>&ПустойСклад
		|";
		ЗапросСклады.УстановитьПараметр("ДокументСсылка",СтруктураШапкиДокумента.Ссылка);
        ЗапросСклады.УстановитьПараметр("ПустойСклад",Справочники.Склады.ПустаяСсылка());
	Иначе //Если ЕстьСоставНабора Тогда

		ТекстЗапросаСписокНоменклатуры = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Номенклатура 
		|ИЗ
		|	Документ." + ИмяТаблицы +"
		|ГДЕ  Ссылка = &ДокументСсылка
		|";

        ТекстЗапросаРеквизитыДокумента = "
		|	Документ." + ИмяТаблицы + " 
		|";

		//нет набора - склады берутся из одной табличной части
		ЗапросСклады = новый Запрос;
		ЗапросСклады.Текст = "Выбрать различные Склад ИЗ Документ."+ИмяТаблицы+"
		|ГДЕ Ссылка=&ДокументСсылка";
		ЗапросСклады.УстановитьПараметр("ДокументСсылка",СтруктураШапкиДокумента.Ссылка);

	КонецЕсли; //Если ЕстьСоставНабора Тогда

	Запрос = Новый Запрос;

	Если ЕстьСкладВТабЧасти Тогда
		СписокСкладов = ЗапросСклады.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
	Иначе
		СписокСкладов = новый Массив;
		СписокСкладов.Добавить(СтруктураШапкиДокумента.Склад);
	КонецЕсли;
	
	
	// Установим параметры запроса
	ЗаполнитьОбщиеПараметрыЗапроса(Запрос);
	Запрос.УстановитьПараметр("СписокСкладов",         СписокСкладов);
	Запрос.УстановитьПараметр("ПустойЗаказПокупателя", Документы.ЗаказПокупателя.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойСклад",           Справочники.Склады.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойСпособСписания",  Перечисления.СпособыСписанияОстаткаТоваров.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеКачество",        Справочники.Качество.ПустаяСсылка());

	ТекстЗапроса = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура                                       		КАК Номенклатура,
	|	Док.Номенклатура.Представление                         		КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление 		КАК ЕдиницаХраненияОстатковПредставление,
	|   Док.ХарактеристикаНоменклатуры 								КАК ХарактеристикаНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(Док.ХарактеристикаНоменклатуры) 				КАК ХарактеристикаНоменклатурыПредставление,
	|   Док.СерияНоменклатуры 										КАК СерияНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(Док.СерияНоменклатуры) 						КАК СерияНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Склад%                                            КАК Склад, 
	|   %ПОЛЕ_Док_Качество% 										КАК Качество,
	|   СУММА(ВЫРАЗИТЬ(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3))) 
	|																КАК ДокументКоличество,
	|	ЕстьNull(МАКСИМУМ(ОстаткиБезСерии.КоличествоОстаток),0)     КАК ОстатокБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(Остатки.КоличествоОстаток),0)             КАК ОстатокКоличество,
	|	ЕстьNull(МАКСИМУМ(Резервы.КоличествоОстаток),0)             КАК РезервыКоличество,
	|	%ПОЛЕ_РезервыПоСерииКоличество%       						КАК РезервыПоСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПередаче.КоличествоОстаток),0)     КАК КПередачеКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПередачеБезСерии.КоличествоОстаток),0) КАК КПередачеБезСерииКоличество,
	|	%ПОЛЕ_РезервыПоДокументуКоличество% 						КАК РезервыПоДокументуКоличество,
	|	%ПОЛЕ_РезервыПоДокументуБезСерии_Количество% 				КАК РезервыПоДокументуБезСерииКоличество,
	|	0 															КАК КПолучению,
	|	0                 											КАК КПолучениюПоДокументуКоличество,
	|	0 															КАК КПередачеПоДокументуКоличество,
	|	0              												КАК КПередачеПоДокументуБезСерииКоличество

	|ИЗ 
	|	"+ ТекстЗапросаРеквизитыДокумента + " КАК Док
	|
	//таблица остатков товаров с учетом серий
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%  
	|		//УСЛОВИЕ_Качество
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура
	| И Док.ХарактеристикаНоменклатуры  = Остатки.ХарактеристикаНоменклатуры
	| И Док.СерияНоменклатуры 			= Остатки.СерияНоменклатуры
	| //СОЕДИНЕНИЕ_Качество_Остатки
	| //СОЕДИНЕНИЕ_Склад_Остатки
	//таблица остатков товаров без учета серий
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%
	|		//УСЛОВИЕ_Качество
	|	) КАК ОстаткиБезСерии
	|ПО 
	|	Док.Номенклатура                = ОстаткиБезСерии.Номенклатура
	| И Док.ХарактеристикаНоменклатуры 	= ОстаткиБезСерии.ХарактеристикаНоменклатуры
	| //СОЕДИНЕНИЕ_Качество_ОстаткиБезСерии
	| //СОЕДИНЕНИЕ_Склад_ОстаткиБезСерии
	|
	//таблица товаров в резерве на складе без учета серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)

	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%) КАК Резервы
	|ПО 
	|	Док.Номенклатура                = Резервы.Номенклатура
	| И Док.ХарактеристикаНоменклатуры 	= Резервы.ХарактеристикаНоменклатуры
	| //СОЕДИНЕНИЕ_ДокументРезерва_Резервы
	| //СОЕДИНЕНИЕ_Склад_Резервы
	| //СОЕДИНЕНИЕ_Качество_Резервы
	
	//таблица товаров в резерве на складе с учетом серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
	|"+?(ИспользоватьУказаниеСерийНоменклатурыПриРезервировании,"
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%) КАК РезервыПоСерии
	|ПО 
	|	Док.Номенклатура                = РезервыПоСерии.Номенклатура
	| И Док.ХарактеристикаНоменклатуры 	= РезервыПоСерии.ХарактеристикаНоменклатуры
	| И Док.СерияНоменклатуры 			= РезервыПоСерии.СерияНоменклатуры
	| //СОЕДИНЕНИЕ_ДокументРезерва_Резервы
	| //СОЕДИНЕНИЕ_Склад_РезервыПоСерии
	| //СОЕДИНЕНИЕ_Качество_Резервы
	|","")+"
	|"+?(ИмяДокумента = "РеализацияТоваровУслуг","
	//таблица резервов по документу: количество, которое списывается за счет резерва (эта часть требуется только для документа Реализация)
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ ТЧ.Номенклатура,
	|	ТЧ.Склад,
	|	ТЧ.ХарактеристикаНоменклатуры
	|	//ПОЛЕ_ТЧ_Серия
	|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
	|	,ВЫБОР КОГДА Сумма(ТЧ.ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
	|   	Сумма(ТЧ.ДокументКоличество)
	|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
	|	КОНЕЦ КАК КоличествоОстаток
	|	ИЗ
	|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
	|		(ВЫБРАТЬ  
	|			Номенклатура,
	|			Склад,
	|			ЗаказПокупателя,
	|			ХарактеристикаНоменклатуры
	|			//ПОЛЕ_Серия
	|			,СУММА(ВЫРАЗИТЬ(Количество * Коэффициент /Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3))) КАК ДокументКоличество
	|		ИЗ 
	|			" + ТекстЗапросаРеквизитыДокумента +" КАК ВремДок
	|		ГДЕ  ВремДок.Ссылка = &ДокументСсылка
	|			И ВремДок.ЗаказПокупателя <> &ПустойЗаказПокупателя и ВремДок.СпособСписанияОстаткаТоваров = &ИзРезерва
	|		СГРУППИРОВАТЬ ПО
	|			Номенклатура,
	|			Склад,
	|			ЗаказПокупателя,
	|			ХарактеристикаНоменклатуры
	|			//ПОЛЕ_Серия
	|		) КАК ТЧ
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|			%ВыборкаПоНоменклатуре%
	|			) КАК ВремРезервы
	|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
	|		И ТЧ.Склад = ВремРезервы.Склад
	|		И ТЧ.ЗаказПокупателя = ВремРезервы.ДокументРезерва
	|		И ТЧ.ХарактеристикаНоменклатуры = ВремРезервы.ХарактеристикаНоменклатуры
	|		//СОЕДИНЕНИЕ_Серия_ВремРезервы
	|	СГРУППИРОВАТЬ ПО 
	|		ТЧ.Номенклатура,
	|		ТЧ.Склад,
	|		ТЧ.ХарактеристикаНоменклатуры
	|		//ПОЛЕ_ТЧ_Серия
	|) КАК РезервыПоДокументу
	|ПО 
	|Док.Номенклатура                 = РезервыПоДокументу.Номенклатура
	|И Док.Склад = РезервыПоДокументу.Склад
	|И Док.ЗаказПокупателя <> &ПустойЗаказПокупателя И Док.СпособСписанияОстаткаТоваров = &ИзРезерва
	|И Док.ХарактеристикаНоменклатуры = РезервыПоДокументу.ХарактеристикаНоменклатуры
	|//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу
	
	//таблица резервов по документу без учета серий: количество, которое списывается за счет резерва 
    |//ЗАПРОС_РезервыПоДокументуБезСерии

    |","")+"
	
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%
	|		//УСЛОВИЕ_Качество
	|	) КАК ТоварыКПередаче
	|ПО 
	|	Док.Номенклатура                = ТоварыКПередаче.Номенклатура
	| И Док.ХарактеристикаНоменклатуры 	= ТоварыКПередаче.ХарактеристикаНоменклатуры
	| И Док.СерияНоменклатуры 			= ТоварыКПередаче.СерияНоменклатуры
	| //СОЕДИНЕНИЕ_Склад_ТоварыКПередаче
	| //СОЕДИНЕНИЕ_Качество_ТоварыКПередаче
	
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%
	|		//УСЛОВИЕ_Качество
	|	) КАК ТоварыКПередачеБезСерии
	|ПО 
	|	Док.Номенклатура                = ТоварыКПередачеБезСерии.Номенклатура
	| И Док.ХарактеристикаНоменклатуры 	= ТоварыКПередачеБезСерии.ХарактеристикаНоменклатуры
	| //СОЕДИНЕНИЕ_Склад_ТоварыКПередачеБезСерии
	| //СОЕДИНЕНИЕ_Качество_ТоварыКПередачеБезСерии
	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И Не Док.Номенклатура.Комплект
	|	И Не Док.Номенклатура.Услуга 
	// остатки по услугам контролировать не надо.
	|	//УСЛОВИЕ_Склад
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|   Док.ХарактеристикаНоменклатуры,
	|   Док.СерияНоменклатуры,
	|   %ПОЛЕ_Док_Качество%, 									
	|    %ПОЛЕ_Док_Склад%
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыНаСкладах.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|
	|ИТОГИ СУММА (ДокументКоличество), МАКСИМУМ(ОстатокБезСерииКоличество), МАКСИМУМ(РезервыКоличество),
	|         МАКСИМУМ(КПередачеКоличество), МАКСИМУМ(РезервыПоДокументуКоличество), Максимум(РезервыПоСерииКоличество), 
	|		Максимум(КПередачеБезСерииКоличество), МАКСИМУМ(РезервыПоДокументуБезСерииКоличество),
	|		Максимум(КПолучению), Максимум(КПередачеПоДокументуКоличество), Максимум(КПередачеПоДокументуБезСерииКоличество), Максимум(КПолучениюПоДокументуКоличество)
	|ПО Номенклатура,
	|   ХарактеристикаНоменклатуры,
	|    Склад
	|	//ПОЛЕ_Качество
	|";
	
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоСерииКоличество%",	?(ИспользоватьУказаниеСерийНоменклатурыПриРезервировании,"ЕстьNull(МАКСИМУМ(РезервыПоСерии.КоличествоОстаток),0)","0"));
	ТекстЗапросаРезервыПоДокументуБезСерии = "";

	Если ИмяДокумента = "РеализацияТоваровУслуг" Тогда
		//есть реквизит ЗаказПокупателя, документ может списывать товар из резерва
		Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
			ТекстЗапросаРезервыПоДокументуБезСерии = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	(ВЫБРАТЬ ТЧ.Номенклатура,
			|	ТЧ.Склад,
			|	ТЧ.ХарактеристикаНоменклатуры
			|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
			|	,ВЫБОР КОГДА Сумма(ТЧ.ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
			|   	Сумма(ТЧ.ДокументКоличество)
			|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
			|	КОНЕЦ КАК КоличествоОстаток
			|	ИЗ
			|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
			|		(ВЫБРАТЬ  
			|			Номенклатура,
			|			Склад,
			|			ЗаказПокупателя,
			|			ХарактеристикаНоменклатуры
			|			,СУММА(ВЫРАЗИТЬ(Количество * Коэффициент /Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3))) КАК ДокументКоличество
			|		ИЗ 
			|			" + ТекстЗапросаРеквизитыДокумента +" КАК ВремДок
			|		ГДЕ  ВремДок.Ссылка = &ДокументСсылка
			|			И ВремДок.ЗаказПокупателя <> &ПустойЗаказПокупателя и ВремДок.СпособСписанияОстаткаТоваров = &ИзРезерва
			|		СГРУППИРОВАТЬ ПО
			|			Номенклатура,
			|			Склад,
			|			ЗаказПокупателя,
			|			ХарактеристикаНоменклатуры
			|		) КАК ТЧ
			|	ЛЕВОЕ СОЕДИНЕНИЕ 
			|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
			|			%ВыборкаПоНоменклатуре%
			|			) КАК ВремРезервы
			|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
			|		И ТЧ.Склад = ВремРезервы.Склад
			|		И ТЧ.ЗаказПокупателя = ВремРезервы.ДокументРезерва
			|		И ТЧ.ХарактеристикаНоменклатуры = ВремРезервы.ХарактеристикаНоменклатуры
			|	СГРУППИРОВАТЬ ПО 
			|		ТЧ.Номенклатура,
			|		ТЧ.Склад,
			|		ТЧ.ХарактеристикаНоменклатуры
			|) КАК РезервыПоДокументуБезСерии
			|ПО 
			|Док.Номенклатура                 = РезервыПоДокументуБезСерии.Номенклатура
			|И Док.Склад = РезервыПоДокументуБезСерии.Склад
			|И Док.ЗаказПокупателя <> &ПустойЗаказПокупателя И Док.СпособСписанияОстаткаТоваров = &ИзРезерва
			|И Док.ХарактеристикаНоменклатуры = РезервыПоДокументуБезСерии.ХарактеристикаНоменклатуры
			|";

			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоДокументуБезСерии",	ТекстЗапросаРезервыПоДокументуБезСерии);
			
			//резервы по документу имеет смысл определять с точностью до серии
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_ТЧ_Серия",							",ТЧ.СерияНоменклатуры");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Серия",								",СерияНоменклатуры");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_ВремРезервы",			"И (ТЧ.СерияНоменклатуры = ВремРезервы.СерияНоменклатуры ИЛИ НЕ ЕстьNull(ТЧ.ЗаказПокупателя.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей,ложь))");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу",	"И Док.СерияНоменклатуры = РезервыПоДокументу.СерияНоменклатуры");
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументуКоличество%",		"ЕстьNull(Максимум(РезервыПоДокументу.КоличествоОстаток),0)");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_ДокументРезерва_Резервы",		" И (Док.ЗаказПокупателя = &ПустойЗаказПокупателя ИЛИ Док.СпособСписанияОстаткаТоваров <> &ИзРезерва)");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_ЗаказПокупателя%",					"ЗаказПокупателя");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_ЗаказПокупателя%",				"ДокТов.ЗаказПокупателя");

	

	Иначе //Если ИмяДокумента = "РеализацияТоваровУслуг" Тогда
		//документ не может списывать товар из резерва - не надо определять резервы по документу
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументуКоличество%",	"0");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_ЗаказПокупателя%",				"NULL");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_ЗаказПокупателя%",			"NULL");
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументуБезСерии_Количество%",		?(ТекстЗапросаРезервыПоДокументуБезСерии<>"",	"ЕстьNull(МАКСИМУМ(РезервыПоДокументуБезСерии.КоличествоОстаток),0)",	"0")); 
	Если ЕстьКачество Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Качество%",						"Качество");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_Качество%",				"ВЫБОР КОГДА ДокНаб.Качество = &ПустоеКачество ТОГДА ДокТов.Качество
																						|ИНАЧЕ ДокНаб.Качество КОНЕЦ");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Качество%",		" &Новый");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_Качество%", "&Новый");
	КонецЕсли;
	
	Если ЕстьСпособСписания Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_СпособСписанияОстаткаТоваров%",	"ВЫБОР КОГДА ДокНаб.СпособСписанияОстаткаТоваров = &ПустойСпособСписания ТОГДА ДокТов.СпособСписанияОстаткаТоваров
																								|ИНАЧЕ ДокНаб.СпособСписанияОстаткаТоваров КОНЕЦ");

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_СпособСписанияОстаткаТоваров%",			" СпособСписанияОстаткаТоваров");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_СпособСписанияОстаткаТоваров%",			" &ПустойСпособСписания");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_СпособСписанияОстаткаТоваров%",	"&ПустойСпособСписания");
	КонецЕсли;

	ДополнитьТекстЗапроса(ТекстЗапроса,ложь,,ложь,,ложь,ЕстьКачество,ЕстьСкладВТабЧасти,ТекстЗапросаСписокНоменклатуры);

	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ОбработатьРезультатЗапроса(Выборка,истина,ТекстЗапросаРезервыПоДокументуБезСерии<>"");

КонецПроцедуры //КонтрольОстатков_Реализация_ОтчетОРознПродажах()

//Процедура контролирует остаток по данному регистру для документов, у которых документ резерва находится в табличной части 
//	Особенность документа: 
//	Наличие документа резерва в табличной части.
//	В связи с этим документ резерва извлекается из таб части документа и используется соединение 
//	с виртуальными таблицами по документу резерва
Процедура КонтрольОстатков_ЗаказВТабЧасти()
	ТекстСерия 			= "СерияНоменклатуры";
	ТекстХарактеристика = "ХарактеристикаНоменклатуры";
	ЕстьСерия           = МетаданныеТабЧасти.Реквизиты.Найти(ТекстСерия)                  		<> Неопределено;
	ЕстьХарактеристика  = МетаданныеТабЧасти.Реквизиты.Найти(ТекстХарактеристика)               <> Неопределено;
	ЕстьКоэффициент     = МетаданныеТабЧасти.Реквизиты.Найти("Коэффициент")                  	<> Неопределено;
	ЕстьКачество        = МетаданныеТабЧасти.Реквизиты.Найти("Качество")                  		<> Неопределено;
	ЕстьСкладВТабличнойЧасти = МетаданныеТабЧасти.Реквизиты.Найти("Склад")                  	<> Неопределено;
	ЕстьСпособСписания 	=  МетаданныеТабЧасти.Реквизиты.Найти("СпособСписанияОстаткаТоваров")	<> Неопределено;

	ИмяРеквизитаЗаказ = "ЗаказПокупателя";
	Если ИмяДокумента = 			"ТребованиеНакладная" Тогда
		
		ИмяРеквизитаЗаказ = "ЗаказРезерв";
	ИначеЕсли ИмяДокумента = 		"ВозвратТоваровПоставщику" Тогда
		
		ИмяРеквизитаЗаказ = "Заказ";
	ИначеЕсли ИмяДокумента = 		"РасходныйОрдерНаТовары" 
			ИЛИ ИмяДокумента = 		"СписаниеТоваров" Тогда
			
			ИмяРеквизитаЗаказ = "ДокументРезерва";
    КонецЕсли;

	
	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ  Ссылка = &ДокументСсылка
	|";

	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	ЗаполнитьОбщиеПараметрыЗапроса(Запрос);
	Если ЕстьСкладВТабличнойЧасти Тогда
		ЗапросСклады = новый Запрос;
		ЗапросСклады.Текст = "Выбрать различные Склад ИЗ Документ."+ИмяТаблицы+"
		|ГДЕ Ссылка=&ДокументСсылка";
		ЗапросСклады.УстановитьПараметр("ДокументСсылка",СтруктураШапкиДокумента.Ссылка);
		СписокСкладов = ЗапросСклады.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
	Иначе
		СписокСкладов = новый Массив;
		СписокСкладов.Добавить(СтруктураШапкиДокумента.Склад);
		Запрос.УстановитьПараметр("Склад",  СтруктураШапкиДокумента.Склад);
	КонецЕсли;	

	Запрос.УстановитьПараметр("СписокСкладов",         СписокСкладов);


	МассивПустыхСсылок = ОбщегоНазначения.МассивПустыхЗначений(МетаданныеТабЧасти.Реквизиты[ИмяРеквизитаЗаказ].Тип);
	Запрос.УстановитьПараметр("МассивПустыхЗаказов",МассивПустыхСсылок);
	
	ДокументПередачи = "";
	Если ИмяДокумента = "РасходныйОрдерНаТовары" Тогда
		ДокументПередачи = СтруктураШапкиДокумента.ДокументПередачи;
		Запрос.УстановитьПараметр("ДокументПередачи",  ДокументПередачи);
	КонецЕсли;


	ТекстЗапроса = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура                                       		КАК Номенклатура,
	|	Док.Номенклатура.Представление                         		КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление 		КАК ЕдиницаХраненияОстатковПредставление,
	|   %ПОЛЕ_Док_Характеристика% 									КАК ХарактеристикаНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Характеристика%) 					КАК ХарактеристикаНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Серия% 											КАК СерияНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Серия%) 							КАК СерияНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Склад%                                            КАК Склад, 
	|   %ПОЛЕ_Док_Качество% 										КАК Качество,
	|   %ПОЛЕ_Док_Количество%										КАК ДокументКоличество,
	|	%ПОЛЕ_ОстаткиБезСерии_Количество%     						КАК ОстатокБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(Остатки.КоличествоОстаток),0)             КАК ОстатокКоличество,
	|	ЕстьNull(МАКСИМУМ(Резервы.КоличествоОстаток),0)             КАК РезервыКоличество,
	|	%ПОЛЕ_РезервыПоСерии_Количество%      						КАК РезервыПоСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПередаче.КоличествоОстаток),0)     КАК КПередачеКоличество,
	|	%ПОЛЕ_КПередачеБезСерии_Количество% 						КАК КПередачеБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(РезервыПоДокументу.КоличествоОстаток),0) 	КАК РезервыПоДокументуКоличество,
	|	%ПОЛЕ_РезервыПоДокументуБезСерии_Количество% 				КАК РезервыПоДокументуБезСерииКоличество,
	|	%ПОЛЕ_КПередачеПоДокументу_Количество% 						КАК КПередачеПоДокументуКоличество,
	|	%ПОЛЕ_КПередачеПоДокументуБезСерии_Количество%              КАК КПередачеПоДокументуБезСерииКоличество,
	|	0 	 														КАК КПолучению,
	|	0 															КАК КПолучениюПоДокументуКоличество
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	
	//таблица остатков товаров с учетом серий
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%  
	|		//УСЛОВИЕ_Качество
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_Остатки
	|   //СОЕДИНЕНИЕ_Серия_Остатки 
	| 	//СОЕДИНЕНИЕ_Качество_Остатки
	| 	//СОЕДИНЕНИЕ_Склад_Остатки
	
	//таблица остатков товаров без учета серий
	|//ЗАПРОС_ОстаткиБезСерии
	|
	
	//таблица товаров в резерве на складе без учета серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
    |ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%) КАК Резервы
	|ПО 
	|	Док.Номенклатура                = Резервы.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_Резервы 
	| И (Док.%ПОЛЕ_Заказ% в (&МассивПустыхЗаказов)
	|	//УСЛОВИЕ_Док_СпособСписанияНеРезерв
	|	)
	| 	//СОЕДИНЕНИЕ_Склад_Резервы
	|   //СОЕДИНЕНИЕ_Качество_Резервы
	
	//таблица товаров в резерве на складе с учетом серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
	|//ЗАПРОС_РезервыПоСерии

	//таблица резервов по документу: количество, которое списывается за счет резерва 
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	(ВЫБРАТЬ ТЧ.Номенклатура
	|	//ПОЛЕ_ТЧ_Склад
	|	//ПОЛЕ_ТЧ_Характеристика
	|	//ПОЛЕ_ТЧ_Серия
	|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
	|	,ВЫБОР КОГДА Сумма(ТЧ.ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
	|   	Сумма(ТЧ.ДокументКоличество)
	|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
	|	КОНЕЦ КАК КоличествоОстаток
	|	ИЗ
	|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
	|		(ВЫБРАТЬ  
	|			Номенклатура,
	|			%ПОЛЕ_Заказ%
	|			//ПОЛЕ_Характеристика
	|			//ПОЛЕ_Серия
	|			//ПОЛЕ_Склад
	|			,%ПОЛЕ_Количество% КАК ДокументКоличество
	|		ИЗ 
	|			Документ." + ИмяТаблицы +" КАК ВремДок
	|		ГДЕ  ВремДок.Ссылка = &ДокументСсылка
	|			И ВремДок.%ПОЛЕ_Заказ% НЕ в (&МассивПустыхЗаказов)
	|	//УСЛОВИЕ_ВремДок_СпособСписанияРезерв
	|		СГРУППИРОВАТЬ ПО
	|			Номенклатура,
	|			%ПОЛЕ_Заказ%
	|			//ПОЛЕ_Характеристика
	|			//ПОЛЕ_Серия
	|			//ПОЛЕ_Склад
	|		) КАК ТЧ
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|			%ВыборкаПоНоменклатуре%
	|			) КАК ВремРезервы
	|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
	|		И ТЧ.%ПОЛЕ_Заказ% = ВремРезервы.ДокументРезерва
	|		//СОЕДИНЕНИЕ_Характеристика_ВремРезервы
	|		//СОЕДИНЕНИЕ_Серия_ВремРезервы
	|		//СОЕДИНЕНИЕ_Склад_ВремРезервы
	|	СГРУППИРОВАТЬ ПО 
	|		ТЧ.Номенклатура
	|		//ПОЛЕ_ТЧ_Характеристика
	|		//ПОЛЕ_ТЧ_Серия
	|		//ПОЛЕ_ТЧ_Склад
	|) КАК РезервыПоДокументу
	|ПО 
	|Док.Номенклатура                 = РезервыПоДокументу.Номенклатура
	|И Док.%ПОЛЕ_Заказ% НЕ в (&МассивПустыхЗаказов)
	|	//УСЛОВИЕ_Док_СпособСписанияРезерв
	|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументу
	|//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу
	|//СОЕДИНЕНИЕ_Склад_РезервыПоДокументу

	//таблица резервов по документу без учета серий: количество, которое списывается за счет резерва 
    |//ЗАПРОС_РезервыПоДокументуБезСерии

	//товары к передаче
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%
	|		//УСЛОВИЕ_Качество
	|		) КАК ТоварыКПередаче
	|ПО 
	|	Док.Номенклатура                = ТоварыКПередаче.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередаче
	|   //СОЕДИНЕНИЕ_Серия_ТоварыКПередаче
	| 	//СОЕДИНЕНИЕ_Качество_ТоварыКПередаче
	| 	//СОЕДИНЕНИЕ_Склад_ТоварыКПередаче
	
	//товары к передаче со складов без учета серий
	|//ЗАПРОС_ТоварыКПередачеБезСерии

	//товары к передаче по документу
	|//ЗАПРОС_КПередачеПоДокументу

	//товары к передаче по документу без учета серий
	|//ЗАПРОС_КПередачеПоДокументуБезСерии
	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И Не Док.Номенклатура.Комплект
	|	И Не Док.Номенклатура.Услуга 
	// остатки по услугам контролировать не надо.
	|   //УСЛОВИЕ_Склад
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|   %ПОЛЕ_Док_Характеристика%,
	|   %ПОЛЕ_Док_Серия%,
	|   %ПОЛЕ_Док_Качество%, 									
	|   %ПОЛЕ_Док_Склад% 
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыНаСкладах.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|
	|ИТОГИ СУММА (ДокументКоличество), МАКСИМУМ(ОстатокБезСерииКоличество), МАКСИМУМ(РезервыКоличество),
	|         МАКСИМУМ(КПередачеКоличество), МАКСИМУМ(РезервыПоДокументуКоличество), Максимум(РезервыПоСерииКоличество), 
	|	МАКСИМУМ(КПередачеПоДокументуКоличество), Максимум(КПередачеБезСерииКоличество), МАКСИМУМ(РезервыПоДокументуБезСерииКоличество), 
	|	Максимум(КПолучению), Максимум(КПередачеПоДокументуБезСерииКоличество), Максимум(КПолучениюПоДокументуКоличество)
	|ПО Номенклатура,
	|   ХарактеристикаНоменклатуры
	|   //ПОЛЕ_Склад
	|	//ПОЛЕ_Качество
	|";
	
	ТекстЗапросаРезервыПоСерии = "";
	ТекстЗапросаОстаткиБезСерии = "";
	ТекстЗапросаКПередачеБезСерии = "";
	ТекстЗапросаКПередачеПоДокументу = "";
	ТекстЗапросаКПередачеПоДокументуБезСерии = "";
	ТекстЗапросаРезервыПоДокументуБезСерии = "";

	Если ЕстьСерия Тогда
		ТекстЗапросаОстаткиБезСерии = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад в (&СписокСкладов) И
		|		%ВыборкаПоНоменклатуре%
		|		//УСЛОВИЕ_Качество
		|	) КАК ОстаткиБезСерии
		|ПО 
		|	Док.Номенклатура                = ОстаткиБезСерии.Номенклатура
		| //СОЕДИНЕНИЕ_Характеристика_ОстаткиБезСерии
		| //СОЕДИНЕНИЕ_Качество_ОстаткиБезСерии
		| //СОЕДИНЕНИЕ_Склад_ОстаткиБезСерии";
		
		ТекстЗапросаКПередачеБезСерии = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
		|		%ВыборкаПоНоменклатуре%
		|		//УСЛОВИЕ_Качество
		|		) КАК ТоварыКПередачеБезСерии
		|ПО 
		|	Док.Номенклатура                = ТоварыКПередачеБезСерии.Номенклатура
		|   //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередачеБезСерии
		| 	//СОЕДИНЕНИЕ_Качество_ТоварыКПередачеБезСерии
		| 	//СОЕДИНЕНИЕ_Склад_ТоварыКПередачеБезСерии";

		Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
			ТекстЗапросаРезервыПоСерии="
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
			|		%ВыборкаПоНоменклатуре%) КАК РезервыПоСерии
			|ПО 
			|	Док.Номенклатура                = РезервыПоСерии.Номенклатура
			|   //СОЕДИНЕНИЕ_Характеристика_РезервыПоСерии 
			|   //СОЕДИНЕНИЕ_Серия_РезервыПоСерии 
			|   //СОЕДИНЕНИЕ_Склад_РезервыПоСерии
			| И (Док.%ПОЛЕ_Заказ% в (&МассивПустыхЗаказов)
			|	//УСЛОВИЕ_Док_СпособСписанияНеРезерв
			|	)
			|   //СОЕДИНЕНИЕ_Качество_Резервы
			|";
			
			ТекстЗапросаРезервыПоДокументуБезСерии = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	(ВЫБРАТЬ ТЧ.Номенклатура
			|	//ПОЛЕ_ТЧ_Склад
			|	//ПОЛЕ_ТЧ_Характеристика
			|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
			|	,ВЫБОР КОГДА Сумма(ТЧ.ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
			|   	Сумма(ТЧ.ДокументКоличество)
			|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
			|	КОНЕЦ КАК КоличествоОстаток
			|	ИЗ
			|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
			|		(ВЫБРАТЬ  
			|			Номенклатура,
			|			%ПОЛЕ_Заказ%
			|			//ПОЛЕ_Характеристика
			|			//ПОЛЕ_Склад
			|			,%ПОЛЕ_Количество% КАК ДокументКоличество
			|		ИЗ 
			|			Документ." + ИмяТаблицы +" КАК ВремДок
			|		ГДЕ  ВремДок.Ссылка = &ДокументСсылка
			|			И ВремДок.%ПОЛЕ_Заказ% НЕ в (&МассивПустыхЗаказов)
			|	//УСЛОВИЕ_ВремДок_СпособСписанияРезерв
			|		СГРУППИРОВАТЬ ПО
			|			Номенклатура,
			|			%ПОЛЕ_Заказ%
			|			//ПОЛЕ_Характеристика
			|			//ПОЛЕ_Склад
			|		) КАК ТЧ
			|	ЛЕВОЕ СОЕДИНЕНИЕ 
			|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
			|			%ВыборкаПоНоменклатуре%
			|			) КАК ВремРезервы
			|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
			|		И ТЧ.%ПОЛЕ_Заказ% = ВремРезервы.ДокументРезерва
			|		//СОЕДИНЕНИЕ_Характеристика_ВремРезервы
			|		//СОЕДИНЕНИЕ_Склад_ВремРезервы
			|	СГРУППИРОВАТЬ ПО 
			|		ТЧ.Номенклатура
			|		//ПОЛЕ_ТЧ_Характеристика
			|		//ПОЛЕ_ТЧ_Склад
			|) КАК РезервыПоДокументуБезСерии
			|ПО 
			|Док.Номенклатура                 = РезервыПоДокументуБезСерии.Номенклатура
			|И Док.%ПОЛЕ_Заказ% НЕ в (&МассивПустыхЗаказов)
			|	//УСЛОВИЕ_Док_СпособСписанияРезерв
			|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументуБезСерии
			|//СОЕДИНЕНИЕ_Склад_РезервыПоДокументуБезСерии";

		КонецЕсли; //Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
	КонецЕсли; //Если ЕстьСерия Тогда
	Если ИмяДокумента = "РасходныйОрдерНаТовары" и ЗначениеЗаполнено(ДокументПередачи) Тогда
		ТекстЗапросаКПередачеПоДокументу = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
		|		%ВыборкаПоНоменклатуре% 
		|	И ДокументПередачи = &ДокументПередачи ) КАК ТоварыКПередачеПоДокументу
		|ПО 
		|	Док.Номенклатура                = ТоварыКПередачеПоДокументу.Номенклатура
        |//СОЕДИНЕНИЕ_Характеристика_КПередачеПоДокументу 
		|//СОЕДИНЕНИЕ_Серия_КПередачеПоДокументу 
		|//СОЕДИНЕНИЕ_Склад_КПередачеПоДокументу 
		|";
		Если ЕстьСерия Тогда
			ТекстЗапросаКПередачеПоДокументуБезСерии = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
			|		%ВыборкаПоНоменклатуре% 
			|	И ДокументПередачи = &ДокументПередачи ) КАК ТоварыКПередачеПоДокументуБезСерии
			|ПО 
			|	Док.Номенклатура                = ТоварыКПередачеПоДокументуБезСерии.Номенклатура
	        |//СОЕДИНЕНИЕ_Характеристика_КПередачеПоДокументуБезСерии 
			|//СОЕДИНЕНИЕ_Склад_КПередачеПоДокументуБезСерии 
			|";

		КонецЕсли;
	ИначеЕсли ИмяДокумента = "СписаниеТоваров"	Тогда
		ТекстЗапросаКПередачеПоДокументу = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
		|		%ВыборкаПоНоменклатуре% 
		|	И ДокументПередачи в (ВЫБРАТЬ РАЗЛИЧНЫЕ ДокументРезерва ИЗ Документ." + ИмяТаблицы + ") ) КАК ТоварыКПередачеПоДокументу
		|ПО 
		|	Док.Номенклатура                = ТоварыКПередачеПоДокументу.Номенклатура
		|	И Док.ДокументРезерва = ТоварыКПередачеПоДокументу.ДокументПередачи
        |//СОЕДИНЕНИЕ_Характеристика_КПередачеПоДокументу 
		|//СОЕДИНЕНИЕ_Серия_КПередачеПоДокументу 
		|//СОЕДИНЕНИЕ_Склад_КПередачеПоДокументу 
		|";
		Если ЕстьСерия Тогда
			ТекстЗапросаКПередачеПоДокументуБезСерии = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
			|		%ВыборкаПоНоменклатуре% 
			|	И ДокументПередачи в (ВЫБРАТЬ РАЗЛИЧНЫЕ ДокументРезерва ИЗ Документ." + ИмяТаблицы + ") ) КАК ТоварыКПередачеПоДокументуБезСерии
			|ПО 
			|	Док.Номенклатура                = ТоварыКПередачеПоДокументуБезСерии.Номенклатура
			|	И Док.ДокументРезерва = ТоварыКПередачеПоДокументу.ДокументПередачи
	        |//СОЕДИНЕНИЕ_Характеристика_КПередачеПоДокументуБезСерии 
			|//СОЕДИНЕНИЕ_Склад_КПередачеПоДокументуБезСерии 
			|";

		КонецЕсли;

	КонецЕсли;

	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоСерии",				ТекстЗапросаРезервыПоСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ОстаткиБезСерии",				ТекстЗапросаОстаткиБезСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_КПередачеПоДокументуБезСерии",ТекстЗапросаКПередачеПоДокументуБезСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_КПередачеПоДокументу",		ТекстЗапросаКПередачеПоДокументу);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ТоварыКПередачеБезСерии",		ТекстЗапросаКПередачеБезСерии);
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоДокументуБезСерии",	ТекстЗапросаРезервыПоДокументуБезСерии);

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоСерии_Количество%",				?(ТекстЗапросаРезервыПоСерии<>"",				"ЕстьNull(МАКСИМУМ(РезервыПоСерии.КоличествоОстаток),0)",			"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_ОстаткиБезСерии_Количество%",			?(ТекстЗапросаОстаткиБезСерии<>"",				"ЕстьNull(МАКСИМУМ(ОстаткиБезСерии.КоличествоОстаток),0)",			"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_КПередачеПоДокументуБезСерии_Количество%",?(ТекстЗапросаКПередачеПоДокументуБезСерии<>"","ЕстьNull(МАКСИМУМ(ТоварыКПередачеПоДокументуБезСерии.КоличествоОстаток),0)","0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_КПередачеПоДокументу_Количество%",		?(ТекстЗапросаКПередачеПоДокументу<>"",			"ЕстьNull(МАКСИМУМ(ТоварыКПередачеПоДокументу.КоличествоОстаток),0)",	"0"));
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_КПередачеБезСерии_Количество%",			?(ТекстЗапросаКПередачеБезСерии<>"",			"ЕстьNull(МАКСИМУМ(ТоварыКПередачеБезСерии.КоличествоОстаток),0)",		"0")); 
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументуБезСерии_Количество%",	?(ТекстЗапросаРезервыПоДокументуБезСерии<>"",	"ЕстьNull(МАКСИМУМ(РезервыПоДокументуБезСерии.КоличествоОстаток),0)",	"0")); 


	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Заказ%",				ИмяРеквизитаЗаказ);
	
	
	Если ЕстьСерия Тогда
		Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_РезервыПоСерии",	"И Док."+ТекстСерия+" = РезервыПоСерии.СерияНоменклатуры");
			//резервы по документу имеет смысл определять с точностью до серии
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_ТЧ_Серия",						",ТЧ."+ТекстСерия);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Серия",							","+ТекстСерия);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_ВремРезервы",		"И (ТЧ."+ТекстСерия+" = ВремРезервы.СерияНоменклатуры ИЛИ НЕ ЕстьNull(ВЫРАЗИТЬ(ТЧ."+ИмяРеквизитаЗаказ+" КАК Документ.ЗаказПокупателя).ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей,ложь))");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу","И Док."+ТекстСерия+" = РезервыПоДокументу."+ТекстСерия);
		КонецЕсли;
	КонецЕсли;  //Если ЕстьСерия Тогда
	
	Если ЕстьСпособСписания Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//УСЛОВИЕ_Док_СпособСписанияНеРезерв",		"ИЛИ Док.СпособСписанияОстаткаТоваров <> &ИзРезерва");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//УСЛОВИЕ_Док_СпособСписанияРезерв",		"И Док.СпособСписанияОстаткаТоваров = &ИзРезерва");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//УСЛОВИЕ_ВремДок_СпособСписанияРезерв",	"И ВремДок.СпособСписанияОстаткаТоваров = &ИзРезерва");
	КонецЕсли;
	ДополнитьТекстЗапроса(ТекстЗапроса,ЕстьХарактеристика,ТекстХарактеристика,ЕстьСерия,ТекстСерия,ЕстьКоэффициент,ЕстьКачество,ЕстьСкладВТабличнойЧасти,ТекстЗапросаСписокНоменклатуры);
	
	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ОбработатьРезультатЗапроса(Выборка,ЕстьСерия,ТекстЗапросаРезервыПоДокументуБезСерии<>"");

КонецПроцедуры //КонтрольОстатков_ЗаказВТабЧасти

//Процедура контролирует остаток по данному регистру для документов, у которых склад и документ резерва находятся в шапке
//	В связи с этим используется отбор в виртуальных таблицах по складу и документу резерва, указанным в шапке документа 
Процедура КонтрольОстатков_СкладВШапке_ЗаказВШапке()
	
	Если  ИмяТабличнойЧасти<>"" Тогда
		ЕстьСпособСписания  = МетаданныеТабЧасти.Реквизиты.Найти("СпособСписанияОстаткаТоваров")	<> Неопределено;
		ЕстьСерия           = МетаданныеТабЧасти.Реквизиты.Найти("СерияНоменклатуры")               <> Неопределено;
		ЕстьХарактеристика  = МетаданныеТабЧасти.Реквизиты.Найти("ХарактеристикаНоменклатуры")      <> Неопределено;
		ЕстьКоэффициент     = МетаданныеТабЧасти.Реквизиты.Найти("Коэффициент")                  	<> Неопределено;
	Иначе
		//документ Комплектация, вид операции - разукомплектация
		ЕстьСпособСписания  = истина;
		ЕстьСерия           = истина;
		ЕстьХарактеристика  = истина;
		ЕстьКоэффициент     = истина;
	КонецЕсли;
	
	ИмяРеквизитаЗаказ = "Заказ";

	Заказ = СтруктураШапкиДОкумента[ИмяРеквизитаЗаказ];

	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ  Ссылка = &ДокументСсылка
	|";

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	ЗаполнитьОбщиеПараметрыЗапроса(Запрос);
	Запрос.УстановитьПараметр("Склад",         	СтруктураШапкиДокумента.Склад);
	Запрос.УстановитьПараметр("Заказ",          Заказ);

	
	МассивПустыхСсылок = ОбщегоНазначения.МассивПустыхЗначений(МетаданныеДокумента.Реквизиты[ИмяРеквизитаЗаказ].Тип);
	Запрос.УстановитьПараметр("МассивПустыхЗаказов",МассивПустыхСсылок);

	ТекстЗапроса = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура                                       	КАК Номенклатура,
	|	Док.Номенклатура.Представление                         	КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление 	КАК ЕдиницаХраненияОстатковПредставление,
	|	&Новый 													КАК Качество,
	|   %ПОЛЕ_Док_Характеристика% 								КАК ХарактеристикаНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Характеристика%) 				КАК ХарактеристикаНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Серия% 										КАК СерияНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Серия%) 						КАК СерияНоменклатурыПредставление,
	|   &Склад                                              	КАК Склад, 
	|   %ПОЛЕ_Док_Количество% 									КАК ДокументКоличество,
	|	%ПОЛЕ_ОстаткиБезСерии_Количество%            			КАК ОстатокБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(Остатки.КоличествоОстаток),0)         КАК ОстатокКоличество,
	|	%ПОЛЕ_Резервы_Количество%                    			КАК РезервыКоличество,
	|	%ПОЛЕ_РезервыПоСерии_Количество%                    	КАК РезервыПоСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПередаче.КоличествоОстаток),0) КАК КПередачеКоличество,
	|	%ПОЛЕ_КПередачеБезСерии_Количество% 					КАК КПередачеБезСерииКоличество,
	|	%ПОЛЕ_РезервыПоДокументу_Количество% 					КАК РезервыПоДокументуКоличество,
	|	%ПОЛЕ_РезервыПоДокументуБезСерии_Количество% 			КАК РезервыПоДокументуБезСерииКоличество,
	|	0 														КАК КПолучению,
	|	0                 										КАК КПолучениюПоДокументуКоличество,
	|	0 														КАК КПередачеПоДокументуКоличество,
	|	0              											КАК КПередачеПоДокументуБезСерииКоличество

	
	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	//таблица остатков товаров с учетом серий
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад И
	|		%ВыборкаПоНоменклатуре%  И Качество = &Новый
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_Остатки
	|   //СОЕДИНЕНИЕ_Серия_Остатки 
	|
	//таблица остатков товаров без учета серий
	|//ЗАПРОС_ОстаткиБезСерии
	|
	//таблица товаров в резерве на складе без учета серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
	|//ЗАПРОС_Резервы
	
	//таблица товаров в резерве на складе с учетом серий номенклатуры (определяется для строк, которые списываются за счет свободного остатка)
	|//ЗАПРОС_РезервыПоСерии
	
	//таблица резервов по документу без учета серий: количество, которое списывается за счет резерва 
    |//ЗАПРОС_РезервыПоДокументуБезСерии
	
	//таблица резервов по документу: количество, которое списывается за счет резерва 
	|//ЗАПРОС_РезервыПоДокументу
	
	//товары к передаче со складов
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад = &Склад И
	|		%ВыборкаПоНоменклатуре% И Качество = &Новый) КАК ТоварыКПередаче
	|ПО 
	|	Док.Номенклатура                = ТоварыКПередаче.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередаче
	|   //СОЕДИНЕНИЕ_Серия_ТоварыКПередаче

	//товары к передаче со складов без учета серий
	|//ЗАПРОС_ТоварыКПередачеБезСерии

	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И Не Док.Номенклатура.Комплект
	|	И Не Док.Номенклатура.Услуга 
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|   %ПОЛЕ_Док_Характеристика%,
	|   %ПОЛЕ_Док_Серия%
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыНаСкладах.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|
	|ИТОГИ СУММА (ДокументКоличество), МАКСИМУМ(ОстатокБезСерииКоличество), МАКСИМУМ(РезервыКоличество),
	|         МАКСИМУМ(КПередачеКоличество), МАКСИМУМ(РезервыПоДокументуКоличество), Максимум(РезервыПоСерииКоличество), 
	|			Максимум(КПередачеБезСерииКоличество), МАКСИМУМ(РезервыПоДокументуБезСерииКоличество), Максимум(КПолучению), 
	|			Максимум(КПередачеПоДокументуКоличество), Максимум(КПередачеПоДокументуБезСерииКоличество), Максимум(КПолучениюПоДокументуКоличество)
	|ПО Номенклатура,
	|   %ПОЛЕ_Док_Характеристика%
	|";
	
	ТекстЗапросаРезервы = "";
	ТекстЗапросаРезервыПоСерии = "";
	ТекстЗапросаРезервыПоДокументу = "";
	ТекстЗапросаОстаткиБезСерии = "";
	ТекстЗапросаКПередачеБезСерии = "";
    ТекстЗапросаРезервыПоДокументуБезСерии = "";

	Если Не ЗначениеЗаполнено(Заказ) 
		ИЛИ ТипЗнч(Заказ)=Тип("ДокументСсылка.ЗаказПоставщику") Тогда 
		//За счет резерва остатки не списываются
		ТекстЗапросаРезервы = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
		|		%ВыборкаПоНоменклатуре%) КАК Резервы
		|ПО 
		|	Док.Номенклатура                = Резервы.Номенклатура
		|   //СОЕДИНЕНИЕ_Характеристика_Резервы ";

		Если ЕстьСерия и ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
				ТекстЗапросаРезервыПоСерии =" 
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
				|		%ВыборкаПоНоменклатуре%) КАК РезервыПоСерии
				|ПО 
				|	Док.Номенклатура                = РезервыПоСерии.Номенклатура
				|   //СОЕДИНЕНИЕ_Характеристика_РезервыПоСерии 
				|   //СОЕДИНЕНИЕ_Серия_РезервыПоСерии ";
        КонецЕсли;
	Иначе  //Если ЗначениеНеЗаполнено(Заказ) Тогда
		Если ЕстьСпособСписания Тогда
			//возможно списание за счет свободного остатка - необходим контроль резервов
			ТекстЗапросаРезервы = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
			|		%ВыборкаПоНоменклатуре%) КАК Резервы
			|ПО 
			|	Док.Номенклатура                = Резервы.Номенклатура
			|	И Док.СпособСписанияОстаткаТоваров <> &ИзРезерва
			|   //СОЕДИНЕНИЕ_Характеристика_Резервы ";
			
			Если ЕстьСерия и ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
				ТекстЗапросаРезервыПоСерии =" 
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И
				|		%ВыборкаПоНоменклатуре%) КАК РезервыПоСерии
				|ПО 
				|	Док.Номенклатура                = РезервыПоСерии.Номенклатура
				|	И Док.СпособСписанияОстаткаТоваров <> &ИзРезерва
				|   //СОЕДИНЕНИЕ_Характеристика_РезервыПоСерии 
				|   //СОЕДИНЕНИЕ_Серия_РезервыПоСерии ";
				
				ТекстЗапросаРезервыПоДокументуБезСерии = "
				|ЛЕВОЕ СОЕДИНЕНИЕ
				|	(ВЫБРАТЬ ТЧ.Номенклатура
				|	//ПОЛЕ_ТЧ_Характеристика
				|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
				|	,ВЫБОР КОГДА Сумма(ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
				|   	Сумма(ДокументКоличество)
				|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
				|	КОНЕЦ КАК КоличествоОстаток
				|	ИЗ
				|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
				|		(ВЫБРАТЬ  
				|			Номенклатура
				|			//ПОЛЕ_Характеристика
				|			,%ПОЛЕ_Количество% КАК ДокументКоличество
				|		ИЗ 
				|			Документ." + ИмяТаблицы +" КАК ВремДок
				|		ГДЕ  ВремДок.Ссылка = &ДокументСсылка
				|			И ВремДок.СпособСписанияОстаткаТоваров = &ИзРезерва 
				|		СГРУППИРОВАТЬ ПО
				|			Номенклатура
				|			//ПОЛЕ_Характеристика
				|		) КАК ТЧ
				|	ЛЕВОЕ СОЕДИНЕНИЕ 
				|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И ДокументРезерва = &Заказ  И
				|			%ВыборкаПоНоменклатуре%
				|			) КАК ВремРезервы
				|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
				|		//СОЕДИНЕНИЕ_Характеристика_ВремРезервы
				|	СГРУППИРОВАТЬ ПО 
				|		ТЧ.Номенклатура
				|		//ПОЛЕ_ТЧ_Характеристика
				|) КАК РезервыПоДокументуБезСерии
				|ПО 
				|Док.Номенклатура                 = РезервыПоДокументуБезСерии.Номенклатура
				|И Док.СпособСписанияОстаткаТоваров = &ИзРезерва
				|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументуБезСерии ";
            КонецЕсли;
			
			ТекстЗапросаРезервыПоДокументу = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	(ВЫБРАТЬ ТЧ.Номенклатура
			|	//ПОЛЕ_ТЧ_Характеристика
			|	//ПОЛЕ_ТЧ_Серия
			|	//количество, списываемое за счет резерва, не может превышать количество указанное в документе
			|	,ВЫБОР КОГДА Сумма(ДокументКоличество)<Сумма(ВремРезервы.КоличествоОстаток) ТОГДА
			|   	Сумма(ДокументКоличество)
			|	ИНАЧЕ Сумма(ВремРезервы.КоличествоОстаток)
			|	КОНЕЦ КАК КоличествоОстаток
			|	ИЗ
			|		//сгруппированная табличная часть документа с отбором строк которые списываются из резерва
			|		(ВЫБРАТЬ  
			|			Номенклатура
			|			//ПОЛЕ_Характеристика
			|			//ПОЛЕ_Серия
			|			,%ПОЛЕ_Количество% КАК ДокументКоличество
			|		ИЗ 
			|			Документ." + ИмяТаблицы +" КАК ВремДок
			|		ГДЕ  ВремДок.Ссылка = &ДокументСсылка
			|			И ВремДок.СпособСписанияОстаткаТоваров = &ИзРезерва 
			|		СГРУППИРОВАТЬ ПО
			|			Номенклатура
			|			//ПОЛЕ_Характеристика
			|			//ПОЛЕ_Серия
			|		) КАК ТЧ
			|	ЛЕВОЕ СОЕДИНЕНИЕ 
			|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И ДокументРезерва = &Заказ  И
			|			%ВыборкаПоНоменклатуре%
			|			) КАК ВремРезервы
			|	ПО ТЧ.Номенклатура = ВремРезервы.Номенклатура 
			|		//СОЕДИНЕНИЕ_Характеристика_ВремРезервы
			|		//СОЕДИНЕНИЕ_Серия_ВремРезервы
			|	СГРУППИРОВАТЬ ПО 
			|		ТЧ.Номенклатура
			|		//ПОЛЕ_ТЧ_Характеристика
			|		//ПОЛЕ_ТЧ_Серия
			|) КАК РезервыПоДокументу
			|ПО 
			|Док.Номенклатура                 = РезервыПоДокументу.Номенклатура
			|И Док.СпособСписанияОстаткаТоваров = &ИзРезерва
			|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументу
			|//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу ";

		Иначе //Если ЕстьСпособСписания Тогда
			//все списывается из резерва
			ТекстЗапросаРезервыПоДокументу = "
            |	ЛЕВОЕ СОЕДИНЕНИЕ 
			|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И ДокументРезерва = &Заказ
			|			И %ВыборкаПоНоменклатуре%
			|	) КАК РезервыПоДокументу
            |ПО 
			|Док.Номенклатура                 = РезервыПоДокументу.Номенклатура
			|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументу
			|//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу ";
			
			Если ЕстьСерия и ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
				ТекстЗапросаРезервыПоДокументуБезСерии = "
	            |	ЛЕВОЕ СОЕДИНЕНИЕ 
				|		РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад = &Склад И ДокументРезерва = &Заказ
				|			И %ВыборкаПоНоменклатуре%
				|	) КАК РезервыПоДокументуБезСерии
	            |ПО 
				|Док.Номенклатура                 = РезервыПоДокументуБезСерии.Номенклатура
				|//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументуБезСерии";

            КонецЕсли;
		КонецЕсли;  //Если ЕстьСпособСписания Тогда
	КонецЕсли;
	
	Если ЕстьСерия Тогда
		ТекстЗапросаОстаткиБезСерии = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад И
		|		%ВыборкаПоНоменклатуре% И Качество = &Новый
		|	) КАК ОстаткиБезСерии
		|ПО 
		|	Док.Номенклатура                = ОстаткиБезСерии.Номенклатура
		| //СОЕДИНЕНИЕ_Характеристика_ОстаткиБезСерии";
		
		ТекстЗапросаКПередачеБезСерии = "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад = &Склад И
		|		%ВыборкаПоНоменклатуре% И Качество = &Новый) КАК ТоварыКПередачеБезСерии
		|ПО 
		|	Док.Номенклатура                = ТоварыКПередачеБезСерии.Номенклатура
		|   //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередачеБезСерии";

	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоСерии",		ТекстЗапросаРезервыПоСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоДокументуБезСерии",	ТекстЗапросаРезервыПоДокументуБезСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоДокументу",	ТекстЗапросаРезервыПоДокументу);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_Резервы",				ТекстЗапросаРезервы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ОстаткиБезСерии",		ТекстЗапросаОстаткиБезСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ТоварыКПередачеБезСерии",	ТекстЗапросаКПередачеБезСерии);

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоСерии_Количество%",				?(ТекстЗапросаРезервыПоСерии <> "",				"ЕстьNull(МАКСИМУМ(РезервыПоСерии.КоличествоОстаток),0)",		"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументу_Количество%",			?(ТекстЗапросаРезервыПоДокументу <> "",			"ЕстьNull(МАКСИМУМ(РезервыПоДокументу.КоличествоОстаток),0)",	"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Резервы_Количество%",					?(ТекстЗапросаРезервы <> "",					"ЕстьNull(МАКСИМУМ(Резервы.КоличествоОстаток),0)",				"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_ОстаткиБезСерии_Количество%",			?(ТекстЗапросаОстаткиБезСерии <> "",			"ЕстьNull(МАКСИМУМ(ОстаткиБезСерии.КоличествоОстаток),0)",		"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_КПередачеБезСерии_Количество%",			?(ТекстЗапросаКПередачеБезСерии<>"",			"ЕстьNull(МАКСИМУМ(ТоварыКПередачеБезСерии.КоличествоОстаток),0)",	"0")); 
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоДокументуБезСерии_Количество%",	?(ТекстЗапросаРезервыПоДокументуБезСерии<>"",	"ЕстьNull(МАКСИМУМ(РезервыПоДокументуБезСерии.КоличествоОстаток),0)","0")); 

	Если ЕстьСерия И ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
		Если ЗначениеЗаполнено(Заказ) И ТипЗнч(Заказ)=Тип("ДокументСсылка.ЗаказПокупателя") Тогда
			Если Заказ.ДоговорКонтрагента.ОбособленныйУчетТоваровПоЗаказамПокупателей Тогда
				//резервы по документу имеет смысл определять с точностью до серии
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_ТЧ_Серия",							",ТЧ.СерияНоменклатуры");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Серия",								",СерияНоменклатуры");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_ВремРезервы",			"И ТЧ.СерияНоменклатуры = ВремРезервы.СерияНоменклатуры ");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_РезервыПоДокументу",	"И Док.СерияНоменклатуры = РезервыПоДокументу.СерияНоменклатуры");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДополнитьТекстЗапроса(ТекстЗапроса,ЕстьХарактеристика,,ЕстьСерия,,ЕстьКоэффициент,ложь,ложь,ТекстЗапросаСписокНоменклатуры);
	
	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ОбработатьРезультатЗапроса(Выборка,ЕстьСерия,ТекстЗапросаРезервыПоДокументуБезСерии<>"");

КонецПроцедуры //КонтрольОстатков_СкладВШапке_ЗаказВШапке()

//Процедура контролирует остаток по данному регистру для документов, у которых склад в шапке, документ резерва не указывается
//	В связи с этим:
//	1) Используется отбор в виртуальных таблицах по складу, указанному в шапке документа 
//	2) Не требуется определять количество, которое списывается за счет резерва
Процедура КонтрольОстатков_БезЗаказа()
	ЕстьСерия           = МетаданныеТабЧасти.Реквизиты.Найти("СерияНоменклатуры")           <> Неопределено;
	ЕстьХарактеристика  = МетаданныеТабЧасти.Реквизиты.Найти("ХарактеристикаНоменклатуры")	<> Неопределено;
	ЕстьКоэффициент     = МетаданныеТабЧасти.Реквизиты.Найти("Коэффициент")                 <> Неопределено;
    ЕстьКачество        = МетаданныеТабЧасти.Реквизиты.Найти("Качество")                  	<> Неопределено;
    ЕстьСкладВТабличнойЧасти = МетаданныеТабЧасти.Реквизиты.Найти("Склад")                  	<> Неопределено;

	ТекстСерия 			= "СерияНоменклатуры";
	ТекстХарактеристика = "ХарактеристикаНоменклатуры";

	ТекстЗапросаСписокНоменклатуры = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура 
	|ИЗ
	|	Документ." + ИмяТаблицы +"
	|ГДЕ  Ссылка = &ДокументСсылка
	|";

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	ЗаполнитьОбщиеПараметрыЗапроса(Запрос);
		Если ЕстьСкладВТабличнойЧасти Тогда
		ЗапросСклады = новый Запрос;
		ЗапросСклады.Текст = "Выбрать различные Склад ИЗ Документ."+ИмяТаблицы+"
		|ГДЕ Ссылка=&ДокументСсылка";
		ЗапросСклады.УстановитьПараметр("ДокументСсылка",СтруктураШапкиДокумента.Ссылка);
		СписокСкладов = ЗапросСклады.Выполнить().Выгрузить().ВыгрузитьКолонку("Склад");
	Иначе
		СписокСкладов = новый Массив;
		СписокСкладов.Добавить(СтруктураШапкиДокумента.Склад);
		Запрос.УстановитьПараметр("Склад",  СтруктураШапкиДокумента.Склад);
	КонецЕсли;	

	Запрос.УстановитьПараметр("СписокСкладов",         СписокСкладов);
	Запрос.УстановитьПараметр("ПустойОрдер",         Документы.ПриходныйОрдерНаТовары.ПустаяСсылка());


	ТекстЗапроса = "
	|ВЫБРАТЬ // Запрос, контролирующий остатки на складах
	|	Док.Номенклатура                                       	КАК Номенклатура,
	|	Док.Номенклатура.Представление                         	КАК НоменклатураПредставление,
	|	Док.Номенклатура.ЕдиницаХраненияОстатков.Представление 	КАК ЕдиницаХраненияОстатковПредставление,
	|	%ПОЛЕ_Док_Качество% 									КАК Качество,
	|   %ПОЛЕ_Док_Характеристика% 								КАК ХарактеристикаНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Характеристика%) 				КАК ХарактеристикаНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Серия% 										КАК СерияНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(%ПОЛЕ_Док_Серия%) 						КАК СерияНоменклатурыПредставление,
	|   %ПОЛЕ_Док_Склад%                                        КАК Склад, 
	|   %ПОЛЕ_Док_Количество% 									КАК ДокументКоличество,
	|	%ПОЛЕ_ОстаткиБезСерии_Количество%           	 		КАК ОстатокБезСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(Остатки.КоличествоОстаток),0)         КАК ОстатокКоличество,
	|	ЕстьNull(МАКСИМУМ(Резервы.КоличествоОстаток),0)         КАК РезервыКоличество,
	|	%ПОЛЕ_РезервыПоСерии_Количество%                    	КАК РезервыПоСерииКоличество,
	|	ЕстьNull(МАКСИМУМ(ТоварыКПередаче.КоличествоОстаток),0)	КАК КПередачеКоличество,
	|	%ПОЛЕ_КПередачеБезСерии_Количество% 					КАК КПередачеБезСерииКоличество,
	|	0                                                       КАК КПолучению,
	|	0                                                       КАК КПолучениюПоДокументуКоличество,
	|	0 														КАК РезервыПоДокументуКоличество,
	|	0 														КАК РезервыПоДокументуБезСерииКоличество,
	|	0 														КАК КПередачеПоДокументуКоличество,
	|	0              											КАК КПередачеПоДокументуБезСерииКоличество

	|ИЗ 
	|	Документ." + ИмяТаблицы + " КАК Док
	|
	//таблица остатков товаров с учетом серий
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%  
	|	//УСЛОВИЕ_Качество
	|	) КАК Остатки
	|ПО 
	|	Док.Номенклатура                = Остатки.Номенклатура
	| 	//СОЕДИНЕНИЕ_Качество_Остатки
	|   //СОЕДИНЕНИЕ_Характеристика_Остатки
	|   //СОЕДИНЕНИЕ_Серия_Остатки 
	| 	//СОЕДИНЕНИЕ_Склад_Остатки
	|
	//таблица остатков товаров без учета серий
	|//ЗАПРОС_ОстаткиБезСерии
	|
	//таблица товаров в резерве на складе без учета серий номенклатуры 
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%) КАК Резервы
	|ПО 
	|	Док.Номенклатура                = Резервы.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_Резервы 
	| 	//СОЕДИНЕНИЕ_Склад_Резервы
	|   //СОЕДИНЕНИЕ_Качество_Резервы
	
	//таблица товаров в резерве на складе с учетом серий номенклатуры 
	|//ЗАПРОС_РезервыПоСерии
	
	//товары к передаче со складов
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
	|		%ВыборкаПоНоменклатуре%
	|	//УСЛОВИЕ_Качество
	|	) КАК ТоварыКПередаче
	|ПО 
	|	Док.Номенклатура                = ТоварыКПередаче.Номенклатура
	|   //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередаче
	| 	//СОЕДИНЕНИЕ_Качество_ТоварыКПередаче
	|   //СОЕДИНЕНИЕ_Серия_ТоварыКПередаче
	| 	//СОЕДИНЕНИЕ_Склад_ТоварыКПередаче

	//товары к передаче со складов без учета серий
	|//ЗАПРОС_ТоварыКПередачеБезСерии

	|
	|ГДЕ
	|	Док.Ссылка  =  &ДокументСсылка
	|	И Не Док.Номенклатура.Комплект
	|	И Не Док.Номенклатура.Услуга 
	|СГРУППИРОВАТЬ ПО
	|
	|	Док.Номенклатура,
	|   %ПОЛЕ_Док_Характеристика%,
	|   %ПОЛЕ_Док_Серия%,
	|	%ПОЛЕ_Док_Качество%,
	|	%ПОЛЕ_Док_Склад%
	|
	|ДЛЯ ИЗМЕНЕНИЯ РегистрНакопления.ТоварыНаСкладах.Остатки // Блокирующие чтение таблицы остатков регистра для разрешения коллизий многопользовательской работы
	|
	|ИТОГИ СУММА (ДокументКоличество), МАКСИМУМ(ОстатокБезСерииКоличество), МАКСИМУМ(РезервыКоличество),
	|         МАКСИМУМ(КПередачеКоличество), Максимум(РезервыПоСерииКоличество), Максимум(КПередачеБезСерииКоличество),
	|         МАКСИМУМ(КПолучению), МАКСИМУМ(РезервыПоДокументуКоличество), МАКСИМУМ(РезервыПоДокументуБезСерииКоличество), 
	|		Максимум(КПередачеПоДокументуКоличество), Максимум(КПередачеПоДокументуБезСерииКоличество), Максимум(КПолучениюПоДокументуКоличество)

	|ПО Номенклатура,
	|   %ПОЛЕ_Док_Характеристика%, %ПОЛЕ_Док_Склад%
	|	//ПОЛЕ_Качество
	|";
	
	ТекстЗапросаРезервыПоСерии = "";
	ТекстЗапросаОстаткиБезСерии = "";
	ТекстЗапросаКПередачеБезСерии = "";

	Если ЕстьСерия Тогда
		Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
			ТекстЗапросаРезервыПоСерии =" 
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрНакопления.ТоварыВРезервеНаСкладах.Остатки(, Склад в (&СписокСкладов) И
			|		%ВыборкаПоНоменклатуре%) КАК РезервыПоСерии
			|ПО 
			|	Док.Номенклатура                = РезервыПоСерии.Номенклатура
			|   //СОЕДИНЕНИЕ_Характеристика_РезервыПоСерии
			|   //СОЕДИНЕНИЕ_Склад_РезервыПоСерии
			|   //СОЕДИНЕНИЕ_Качество_Резервы
			|   //СОЕДИНЕНИЕ_Серия_РезервыПоСерии ";
		КонецЕсли;	
		ТекстЗапросаОстаткиБезСерии = "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад в (&СписокСкладов) И
			|		%ВыборкаПоНоменклатуре% 
			|	//УСЛОВИЕ_Качество
			|	) КАК ОстаткиБезСерии
			|ПО 
			|	Док.Номенклатура                = ОстаткиБезСерии.Номенклатура
			| 	//СОЕДИНЕНИЕ_Качество_ОстаткиБезСерии
			| 	//СОЕДИНЕНИЕ_Склад_ОстаткиБезСерии
			| //СОЕДИНЕНИЕ_Характеристика_ОстаткиБезСерии";
		ТекстЗапросаКПередачеБезСерии = "	
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрНакопления.ТоварыКПередачеСоСкладов.Остатки(, Склад в (&СписокСкладов) И
		|		%ВыборкаПоНоменклатуре%
		|	//УСЛОВИЕ_Качество
		|	) КАК ТоварыКПередачеБезСерии
		|ПО 
		|	Док.Номенклатура                = ТоварыКПередачеБезСерии.Номенклатура
		|   //СОЕДИНЕНИЕ_Характеристика_ТоварыКПередачеБезСерии
		|   //СОЕДИНЕНИЕ_Склад_ТоварыКПередачеБезСерии
		| 	//СОЕДИНЕНИЕ_Качество_ТоварыКПередачеБезСерии";
	
	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_РезервыПоСерии",	ТекстЗапросаРезервыПоСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ОстаткиБезСерии",	ТекстЗапросаОстаткиБезСерии);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ЗАПРОС_ТоварыКПередачеБезСерии",	ТекстЗапросаКПередачеБезСерии);

	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_РезервыПоСерии_Количество%",	?(ТекстЗапросаРезервыПоСерии<>"",	"ЕстьNull(МАКСИМУМ(РезервыПоСерии.КоличествоОстаток),0)",	"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_ОстаткиБезСерии_Количество%",?(ТекстЗапросаОстаткиБезСерии<>"",	"ЕстьNull(МАКСИМУМ(ОстаткиБезСерии.КоличествоОстаток),0)",	"0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_КПередачеБезСерии_Количество%",	?(ТекстЗапросаКПередачеБезСерии<>"",	"ЕстьNull(МАКСИМУМ(ТоварыКПередачеБезСерии.КоличествоОстаток),0)",	"0")); 
	ДополнитьТекстЗапроса(ТекстЗапроса,ЕстьХарактеристика,ТекстХарактеристика,ЕстьСерия,ТекстСерия,ЕстьКоэффициент,ЕстьКачество,ЕстьСкладВТабличнойЧасти,ТекстЗапросаСписокНоменклатуры);

	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ОбработатьРезультатЗапроса(Выборка,ЕстьСерия,ложь);

КонецПроцедуры

//Вспомогательная процедура для установки некоторых общих для многих процедур параметров запроса
Процедура ЗаполнитьОбщиеПараметрыЗапроса(Запрос)
	Запрос.УстановитьПараметр("ДокументСсылка",			СтруктураШапкиДокумента.Ссылка);
	Запрос.УстановитьПараметр("Новый",          		Справочники.Качество.Новый);
    Запрос.УстановитьПараметр("ПустаяХарактеристика",  	Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСерия",  			Справочники.СерииНоменклатуры.ПустаяСсылка());
    Запрос.УстановитьПараметр("ИзРезерва",      		Перечисления.СпособыСписанияОстаткаТоваров.ИзРезерва);
    Запрос.УстановитьПараметр("НТТ",                   	Перечисления.ВидыСкладов.НТТ);
	Запрос.УстановитьПараметр("Розничный",              Перечисления.ВидыСкладов.Розничный);

КонецПроцедуры

//Вспомогательная процедура для дополнения текста запроса стандартными конструкциями
Процедура ДополнитьТекстЗапроса(ТекстЗапроса,ЕстьХарактеристика,ТекстХарактеристика = "ХарактеристикаНоменклатуры",ЕстьСерия,ТекстСерия="СерияНоменклатуры",ЕстьКоэффициент,ЕстьКачество, ЕстьСкладВТабЧасти=ложь,ТекстЗапросаСписокНоменклатуры)
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ВыборкаПоНоменклатуре%","Номенклатура В ("+ТекстЗапросаСписокНоменклатуры+")");

	Если ЕстьХарактеристика Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Характеристика%",							"Док."+ТекстХарактеристика);
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_ОстаткиБезСерии",			"И Док."+ТекстХарактеристика+" = ОстаткиБезСерии.ХарактеристикаНоменклатуры"); 
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_Остатки",					"И Док."+ТекстХарактеристика+" = Остатки.ХарактеристикаНоменклатуры");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_РезервыПоСерии",			"И Док."+ТекстХарактеристика+" = РезервыПоСерии.ХарактеристикаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументуБезСерии","И Док."+ТекстХарактеристика+" = РезервыПоДокументуБезСерии."+ТекстХарактеристика);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_РезервыПоДокументу",		"И Док."+ТекстХарактеристика+" = РезервыПоДокументу."+ТекстХарактеристика);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_Резервы",					"И Док."+ТекстХарактеристика+" = Резервы.ХарактеристикаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_ТоварыКПередачеБезСерии",	"И Док."+ТекстХарактеристика+" = ТоварыКПередачеБезСерии.ХарактеристикаНоменклатуры");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_ТоварыКПередаче",			"И Док."+ТекстХарактеристика+" = ТОварыКПередаче.ХарактеристикаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_ТоварыКПолучению",			"И Док."+ТекстХарактеристика+" = ТоварыКПолучению.ХарактеристикаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_КПередачеПоДокументуБезСерии",		"И Док."+ТекстХарактеристика+" = ТоварыКПередачеПоДокументуБезСерии.ХарактеристикаНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_КПередачеПоДокументу",		"И Док."+ТекстХарактеристика+" = ТоварыКПередачеПоДокументу.ХарактеристикаНоменклатуры");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_ТЧ_Характеристика",								",ТЧ."+ТекстХарактеристика);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Характеристика",								","+ТекстХарактеристика);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Характеристика_ВремРезервы",				"И ТЧ."+ТекстХарактеристика+" = ВремРезервы.ХарактеристикаНоменклатуры");

	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Характеристика%",					"&ПустаяХарактеристика");
	КонецЕсли;

	Если ЕстьКоэффициент Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Количество%",	"СУММА(ВЫРАЗИТЬ(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3)))");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Количество_Зап%",	"ВЫРАЗИТЬ(Док.Количество * Док.Коэффициент /Док.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3))");

        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Количество%",		"СУММА(ВЫРАЗИТЬ(Количество * Коэффициент /Номенклатура.ЕдиницаХраненияОстатков.Коэффициент КАК Число(15,3)))");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Количество%",		"СУММА(ВЫРАЗИТЬ(Количество КАК Число(15,3)))");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Количество_Зап%",	"ВЫРАЗИТЬ(Док.Количество КАК Число(15,3))");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Количество%",	"СУММА(ВЫРАЗИТЬ(Док.Количество КАК Число(15,3)))");
	КонецЕсли;

	Если ЕстьКачество Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Качество%",								"Док.Качество");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Качество_ОстаткиБезСерии",			"И Док.Качество = ОстаткиБезСерии.Качество");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Качество_Остатки",					"И Док.Качество = Остатки.Качество");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Качество_ТоварыКПередачеБезСерии",	"И Док.Качество = ТоварыКПередачеБезСерии.Качество");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Качество_ТоварыКПередаче",			"И Док.Качество = ТоварыКПередаче.Качество");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Качество_ТоварыКПолучению",			"И Док.Качество = ТоварыКПолучению.Качество");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Качество_Резервы",					"И Док.Качество = &Новый");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Качество",									",Качество");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Качество%",						"&Новый");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//УСЛОВИЕ_Качество",						" И Качество = &Новый");
	КонецЕсли;

	Если ЕстьСерия Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Серия%",							"Док."+ТекстСерия);
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_Остатки",				"И Док."+ТекстСерия+" = Остатки.СерияНоменклатуры");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_РезервыПоСерии",		"И Док."+ТекстСерия+" = РезервыПоСерии.СерияНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_КПередачеПоДокументу",	"И Док."+ТекстСерия+" = ТоварыКПередачеПоДокументу.СерияНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_ТоварыКПередаче",		"И Док."+ТекстСерия+" = ТоварыКПередаче.СерияНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Серия_ТоварыКПолучению",		"И Док."+ТекстСерия+" = ТоварыКПолучению.СерияНоменклатуры");

	Иначе
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Серия%",									"&ПустаяСерия");
	КонецЕсли;

	Если ЕстьСкладВТабЧасти Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Склад%",					"Док.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Склад%",						"Склад");

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//УСЛОВИЕ_Склад",	"И Док.Склад.ВидСклада<>&НТТ И Док.Склад.ВидСклада<>&Розничный");

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_ОстаткиБезСерии",				"И Док.Склад = ОстаткиБезСерии.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_ТоварыКПередачеБезСерии",		"И Док.Склад = ТоварыКПередачеБезСерии.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_ТоварыКПередаче",				"И Док.Склад = ТоварыКПередаче.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_КПередачеПоДокументуБезСерии",	"И Док.Склад = КПередачеПоДокументуБезСерии.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_КПередачеПоДокументу",			"И Док.Склад = КПередачеПоДокументу.Склад");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_Остатки",						"И Док.Склад = Остатки.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_РезервыПоДокументуБезСерии",	"И Док.Склад = РезервыПоДокументуБезСерии.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_РезервыПоДокументу",			"И Док.Склад = РезервыПоДокументу.Склад");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_РезервыПоСерии",				"И Док.Склад = РезервыПоСерии.Склад");
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//СОЕДИНЕНИЕ_Склад_Резервы",						"И Док.Склад = Резервы.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_ТЧ_Склад",									",ТЧ.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОЛЕ_Склад",										",Склад");

        ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_Склад%",				"ВЫБОР КОГДА ДокНаб.Склад = &ПустойСклад ТОГДА ДокТов.Склад
																						|ИНАЧЕ ДокНаб.Склад КОНЕЦ");
																						
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Док_Склад%",					"Док.Ссылка.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Склад%",						"Ссылка.Склад");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"%ПОЛЕ_Набор_Склад%", 				"ДокТов.Ссылка.Склад");
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьРезультатЗапроса(Выборка,ЕстьСерия,ЕстьРезервыПоДокументуБезСерии)
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда
			ДокументКоличествоБезСерии = Выборка.ДокументКоличество;
			РезервыПоДокументуБезСерииКоличество   = ?(ЕстьРезервыПоДокументуБезСерии,Выборка.РезервыПоДокументуБезСерииКоличество,Выборка.РезервыПоДокументуКоличество);
			Продолжить;
		КонецЕсли;
		
		ДокументКоличество             = Выборка.ДокументКоличество;
		КоличествоНаСкладе             = Выборка.ОстатокКоличество;
		КоличествоВРезерве             = Выборка.РезервыКоличество; 
		КоличествоВРезервеПоСерии      = Выборка.РезервыПоСерииКоличество; 
		РезервыПоДокументуКоличество   = Выборка.РезервыПоДокументуКоличество;
		КПередачеКоличество            = Выборка.КПередачеКоличество;
		КПередачеБезСерииКоличество    = ?(ЕстьСерия,Выборка.КПередачеБезСерииКоличество,КПередачеКоличество);
		КПередачеПоДокументуКоличество = Выборка.КПередачеПоДокументуКоличество;
		КПередачеПоДокументуБезСерииКоличество = ?(ЕстьСерия,Выборка.КПередачеПоДокументуБезСерииКоличество,Выборка.КПередачеПоДокументуКоличество);
		КПолучениюКоличество           = Выборка.КПолучению;
		КПолучениюПоДокументуКоличество = Выборка.КПолучениюПоДокументуКоличество;


		НедоступноНаСкладе = Макс(КоличествоВРезерве - РезервыПоДокументуБезСерииКоличество,0) + Макс(КПередачеБезСерииКоличество - КПередачеПоДокументуБезСерииКоличество,0) + Макс(КПолучениюКоличество-КПолучениюПоДокументуКоличество,0);
		
		// Сначала проверяем остаток товара на складе с учетом серий,
		// потом свободный остаток товара (без учета серий)
		Если ЕстьСерия Тогда
			КоличествоБезРезерва = КоличествоНаСкладе;
			мКоличествоВРезерве = 0;
			//если серия не заполнена - уменьшать свободный остаток на количество резерва и товаров к передаче нельзя, 
			//	т.к. может попасть резервирование или товары к передаче, выполненные без учета серии 
			Если  ЗначениеЗаполнено(Выборка.СерияНоменклатуры) Тогда
				Если ИспользоватьУказаниеСерийНоменклатурыПриРезервировании Тогда
					//может иметься резерв в разрезе серий - его надо убрать из свободного остатка
					мКоличествоВРезерве = Макс(КоличествоВРезервеПоСерии - РезервыПоДокументуКоличество,0);
					КоличествоБезРезерва = КоличествоНаСкладе - мКоличествоВРезерве;
				КонецЕсли;
				КоличествоБезРезерва = КоличествоБезРезерва - КПередачеКОличество + КПередачеПоДокументуКоличество;
			КонецЕсли;
	
			Если КоличествоБезРезерва < ДокументКоличество Тогда
				УправлениеЗапасами.СообщитьОНедостаткеТовара(ДокументКоличество                  // Требуемое количество товара
				, КоличествоБезРезерва                                      // Свободное количество товара
				, мКоличествоВРезерве                                        // Количество в резерве
				, Макс(КПередачеКоличество - КПередачеПоДокументуКоличество,0)
				, Макс(КПолучениюКоличество-КПолучениюПоДокументуКоличество,0)
				, Отказ, Заголовок
				, СокрЛП(Выборка.Склад)                                   // Склад (представление)
				,                                                         // Заказ поставщику (представление)
				, Выборка.НоменклатураПредставление                       // Номенклатура (представление)
				, " "+Выборка.ХарактеристикаНоменклатурыПредставление 		// Характеристика (представление)
				, " "+Выборка.СерияНоменклатурыПредставление 				// Серия номенклатуры (представление)
				, СокрЛП(Выборка.Качество)                                // Качество товара (представление)
				, Выборка.ЕдиницаХраненияОстатковПредставление            // Единица хранения остатков (представление)
				);
					
				Продолжить;
			КонецЕсли;
			КоличествоНаСкладе = Выборка.ОстатокБезСерииКоличество;
			ДокументКоличество = ДокументКоличествоБезСерии;
		КонецЕсли;
		Если КоличествоНаСкладе - НедоступноНаСкладе < ДокументКоличество Тогда
			УправлениеЗапасами.СообщитьОНедостаткеТовара(ДокументКоличество                  // Требуемое количество товара
			, КоличествоНаСкладе - НедоступноНаСкладе                 // Свободное количество товара
			, КоличествоВРезерве - РезервыПоДокументуБезСерииКоличество       // Количество товара в резерве
			, Макс(КПередачеБезСерииКоличество - КПередачеПоДокументуБезСерииКоличество,0)     // Количество товара к передаче
			, Макс(КПолучениюКоличество-КПолучениюПоДокументуКоличество,0)
			, Отказ, Заголовок 
			, СокрЛП(Выборка.Склад)                         // Склад (представление)
			,                                               // Заказ поставщику (представление)
			, Выборка.НоменклатураПредставление             // Номенклатура (представление)
			, Выборка.ХарактеристикаНоменклатурыПредставление // Характеристика (представление)
			,                                               // Серия номенклатуры (представление)
			,                       // Качество товара (представление)
			, Выборка.ЕдиницаХраненияОстатковПредставление  // Единица хранения остатков (представление)
			);
			
			ДокументКоличествоБезСерии = 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

