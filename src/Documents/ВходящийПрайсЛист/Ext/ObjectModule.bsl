
Функция ЗагрузитьЭлементХМЛ(ОбъектXML, ИмяУзла, Параметры) Экспорт
	Перем СтрокаТовара, ТипНалога, ВалютаСтрок;
	
	Параметры.Свойство("СтрокаТовара", СтрокаТовара);
	Параметры.Свойство("ТипНалога", ТипНалога);
	
	Если ИмяУзла = "ПрайсЛист" Тогда
		
	ИначеЕсли ИмяУзла = "НомерИсходногоДокумента" Тогда
		ИсходныйДокумент = ЭлектронныеДокументы.ПолучитьСсылкуНаДокументПоДаннымИзXML(ОбъектXML, Документы.ИсходящийЗапросПрайсЛиста.ПустаяСсылка(), Параметры);
		
	ИначеЕсли ИмяУзла = "ДлительностьОжиданияОтвета" Тогда
		ДлительностьОжиданияОтвета = ЭлектронныеДокументы.ПолучитьДлительностьЭлемента(ОбъектXML);
		
	ИначеЕсли ИмяУзла = "ПолныйПрайсЛист" Тогда
		ЭтоПолныйПрайсЛист = ЭлектронныеДокументы.ПолучитьБулевоЭлемента(ОбъектXML);
		
	ИначеЕсли ИмяУзла = "НачалоДействия" Тогда
		НачалоДействия = ЭлектронныеДокументы.ПолучитьДатуЭлемента(ОбъектXML);
		
	ИначеЕсли ИмяУзла = "ОкончаниеДействия" Тогда
		ОкончаниеДействия = ЭлектронныеДокументы.ПолучитьДатуЭлемента(ОбъектXML);
		
	ИначеЕсли ИмяУзла = "ТоварПрайсЛист" Тогда
			
		СтрокаТовара = Товары.Добавить();
		Параметры.Вставить("СтрокаТовара",СтрокаТовара);
			
	ИначеЕсли ИмяУзла = "ИдТовараКлиента" Тогда
			
		СтрокаТовара.ИдТовараКонтрагента = ЭлектронныеДокументы.ПолучитьТекстЭлементаХМЛ(ОбъектXML);
			
	ИначеЕсли   ИмяУзла = "ИдТовара" Тогда
			
		Значение = ЭлектронныеДокументы.ПолучитьСсылкуЭлемента(ОбъектXML);
		
		ЭлектронныеДокументы.ЗаполнитьНоменклатуруИХарактеристикуПоИдентификатору(СтрокаТовара, Значение, Параметры);				
		
	ИначеЕсли ИмяУзла = "Цена" Тогда
		
	ИначеЕсли ИмяУзла = "Сумма" и ТипНалога = Неопределено Тогда
		
		ЭлектронныеДокументы.ПрочитатьВалютуДокумента(ЭтотОбъект, ОбъектXML, Параметры);
		
		СтрокаТовара.Цена = ЭлектронныеДокументы.ПолучитьЧислоЭлемента(ОбъектXML);
	
	ИначеЕсли ИмяУзла = "Налог" Тогда
		
	ИначеЕсли ИмяУзла = "ТипНалога" Тогда
		
		Параметры.Вставить("ТипНалога", вРег(ЭлектронныеДокументы.ПолучитьТекстЭлементаХМЛ(ОбъектXML)));
		
	ИначеЕсли ИмяУзла = "ВеличинаСтавкиНалога" Тогда
		
		Если ТипНалога = "НДС" Тогда
			СтрокаТовара.СтавкаНДС = Перечисления.СтавкиНДС["НДС" + ЭлектронныеДокументы.ПолучитьТекстЭлементаХМЛ(ОбъектXML)];
		КонецЕсли;
		
	ИначеЕсли ИмяУзла = "Сумма" и Не ТипНалога = Неопределено Тогда
		
		ЭлектронныеДокументы.ПрочитатьВалютуДокумента(ЭтотОбъект, ОбъектXML, Параметры);
		
		Значение = ЭлектронныеДокументы.ПолучитьЧислоЭлемента(ОбъектXML);
		Если ТипНалога = "НДС" Тогда
			СтрокаТовара.СуммаНДС = Значение;
		Иначе
			СтрокаТовара.СуммаИныхНалогов = СтрокаТовара.СуммаИныхНалогов + Значение;
		КонецЕсли;
		
	ИначеЕсли ИмяУзла = "ВключеноВСтоимость" Тогда
		
		Если ТипНалога = "НДС" Тогда
			
			ВключеноВСтоимостьСтроки = ЭлектронныеДокументы.ПолучитьБулевоЭлемента(ОбъектXML);
			ВключеноВСтоимостьСтрок  = Неопределено;
			
			Если Параметры.Свойство("ВключеноВСтоимостьСтрок", ВключеноВСтоимостьСтрок) Тогда
				
				Если Не ВключеноВСтоимостьСтрок = ВключеноВСтоимостьСтроки Тогда
					ЭлектронныеДокументы.ДополинтьИнформациюДляСообщения(Параметры.ТекстОшибки, "Торговая система не поддерживает документы с различным способом включения НДС в цену товара в одном документе!"); 
				КонецЕсли;
			Иначе
				ЦенаВключаетНДС = ВключеноВСтоимостьСтроки;
				Параметры.Вставить("ВключеноВСтоимостьСтрок", ВключеноВСтоимостьСтроки);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Параметры.Удалить("ТипНалога")
		
	ИначеЕсли ИмяУзла = "Примечание" Тогда
		
		СтрокаТовара.Примечание = ЭлектронныеДокументы.ПолучитьТекстЭлементаХМЛ(ОбъектXML);
		
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ТекстОшибкиПоДоговору = ПолучитьТекстОшибкиПоТипуЦен(Отказ);
	Если ЗначениеЗаполнено(ТекстОшибкиПоДоговору) Тогда
		Сообщить(""+ТекстОшибкиПоДоговору);
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.НоменклатураКонтрагентов.СоздатьМенеджерЗаписи();
	
	Для каждого СтрокаТовара Из Товары Цикл
		
		Запись = Движения.ЦеныНоменклатурыКонтрагентов.Добавить();
		Запись.Период = ДатаВходящегоДокумента;
		Запись.Номенклатура = СтрокаТовара.Номенклатура;
		Запись.ХарактеристикаНоменклатуры = СтрокаТовара.ХарактеристикаНоменклатуры;
		Запись.Цена = СтрокаТовара.Цена;
		Запись.ТипЦен = ТипЦен;
		Запись.Валюта = ТипЦен.ВалютаЦены;
		
		МенеджерЗаписи.Контрагент                 = Контрагент;
		МенеджерЗаписи.Номенклатура               = СтрокаТовара.Номенклатура;
		МенеджерЗаписи.ХарактеристикаНоменклатуры = СтрокаТовара.ХарактеристикаНоменклатуры;
		МенеджерЗаписи.Прочитать();
		
		Запись.ЕдиницаИзмерения = ЭлектронныеДокументы.ПолучитьЕдиницуИзмеренияНоменклатурыПоКлассификатору(СтрокаТовара.Номенклатура, МенеджерЗаписи.ЕдиницаНоменклатурыКонтрагента);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТекстОшибкиПоТипуЦен(Отказ = Ложь) Экспорт
	
	ТекстОшибки = "";
	
	Если Не ЗначениеЗаполнено(ТипЦен) Тогда
		ТекстОшибки = "Не указан тип цен";
	ИначеЕсли Не ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ТекстОшибки = "Не заполнена валюта документа";
	ИначеЕсли ТипЦен.ВалютаЦены <> ВалютаДокумента Тогда
		ТекстОшибки = "Валюта документа не соответствует валюте типа цен";
	ИначеЕсли ТипЦен.ЦенаВключаетНДС <> ЦенаВключаетНДС Тогда
		ТекстОшибки = "Способ включения НДС в цену не соответствует типу цен";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Отказ = Истина;
	КонецЕсли; 
	
	Возврат ТекстОшибки
	
КонецФункции // () 