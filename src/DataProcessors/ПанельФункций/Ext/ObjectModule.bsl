Перем мИмяТекущейСтраницы    Экспорт; // имя текущей страницы для формы ПанельФункций
Перем мСоответствиеИстории   Экспорт; // счетчик истории переходов на страницы
Перем мТекущийУзел           Экспорт; // текущий узел XML документа макета
Перем мТекущаяСтраница       Экспорт; // имя текущей страницы формы Справка
Перем мСтраницыПанели        Экспорт; // страница панели

// Процедура записывает в СчетчикИстории имя текущей страницы макета.
//
// Параметры
//  Нет
//
Процедура ЗаписатьВИсторию() Экспорт

	Если ТипЗнч(мСоответствиеИстории) <> Тип("Соответствие") Тогда
		мСоответствиеИстории = Новый Соответствие;
		мСоответствиеИстории.Вставить("Счетчик", 0);
		мСоответствиеИстории.Вставить("Количество", 0);
	КонецЕсли;
	
	СчетчикИстории         = мСоответствиеИстории["Счетчик"];
	ТекущаяСтраницаИстории = мСоответствиеИстории[СчетчикИстории];

	Если ТекущаяСтраницаИстории <> мИмяТекущейСтраницы Тогда

		СчетчикИстории = мСоответствиеИстории.Количество() - 1;

		мСоответствиеИстории.Вставить("Счетчик",      СчетчикИстории);
		мСоответствиеИстории.Вставить("Количество",   СчетчикИстории);
		мСоответствиеИстории.Вставить(СчетчикИстории, мИмяТекущейСтраницы);

	КонецЕсли;

КонецПроцедуры // ЗаписатьВИсторию()

// Функция получает атрибуты элемента XML по его имени.
//
//  Параметры:
// ОбъектДоступаXML - объект ЧтениеXML, содержащий строку макета
//		Тип - ЧтениеXML
// ИмяЭлемента - имя узла
//		Тип - строка
//
//  Возвращаемое значение
//		Тип - Соответствие, содержащее атрибуты узла.
Функция ПрочитатьЭлементXMLМакета(ОбъектДоступаXML, ИмяЭлемента = Неопределено) Экспорт
	
	Пока ОбъектДоступаXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Цикл // тип узла = начало элемента
		Если НЕ ОбъектДоступаXML.Прочитать() Тогда // должен прочитаться узел
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если ИмяЭлемента <> Неопределено Тогда // если определили имя элемента, нужно чтобы совпадало с полученным
		Если ОбъектДоступаXML.Имя <> ИмяЭлемента Тогда
			Сообщить("Неправильное имя узла (" + ИмяЭлемента + ")!"); 
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	// Установим текущей узел макета
	мТекущийУзел = ОбъектДоступаXML.Имя;
	
	СоответствиеАтрибутов = Новый Соответствие();
	Пока ОбъектДоступаXML.ПрочитатьАтрибут() Цикл
		СоответствиеАтрибутов.Вставить(ОбъектДоступаXML.Имя, ОбъектДоступаXML.Значение);
	КонецЦикла;
	
	Возврат СоответствиеАтрибутов;

КонецФункции // ПрочитатьЭлементXMLМакета()

