
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ЭД") Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыДляОбработки = Новый Массив;
	Если ТипЗнч(Параметры.ЭД) <> Тип("Массив") Тогда
		ОбъектыДляОбработки.Добавить(Параметры.ЭД);
	Иначе
		ОбъектыДляОбработки = Параметры.ЭД;
	КонецЕсли;
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбъектыДляОбработки, УникальныйИдентификатор);
	
	Если ОбъектыДляОбработки.Количество() > 1 Тогда
		Элементы.ОбъектыДляОбработки.Заголовок = НСтр("ru = 'Список'");
	КонецЕсли;
	
	Если Параметры.Свойство("Ответственный") Тогда
		Пользователь = Параметры.Ответственный;
	КонецЕсли;
	Если ОбъектыДляОбработки.Количество() = 1 Тогда
		ЭлектронныйДокумент = ОбъектыДляОбработки[0];
		ТекстГиперссылки = ЭлектронныеДокументыСлужебный.ПолучитьПредставлениеЭД(ЭлектронныйДокумент);
	Иначе
		ТекстГиперссылки = НСтр("ru = 'Электронные документы (%1)'");
		ТекстГиперссылки = СтрЗаменить(ТекстГиперссылки, "%1", ОбъектыДляОбработки.Количество());
	КонецЕсли;
	
	ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
	НадписьОбъектыДляОбработки = ТекстГиперссылки;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Пользователи.Ссылка
	|ИЗ
	|	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиНастроекЭДО.СертификатыПодписейОрганизации КАК ПрофилиНастроекЭДОСертификатыПодписейОрганизации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СертификатыЭЦП КАК Сертификаты
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|				ПО Сертификаты.Пользователь = Пользователи.Ссылка
	|			ПО ПрофилиНастроекЭДОСертификатыПодписейОрганизации.Сертификат = Сертификаты.Ссылка
	|		ПО ЭДПрисоединенныеФайлы.ПрофильНастроекЭДО = ПрофилиНастроекЭДОСертификатыПодписейОрганизации.Ссылка
	|ГДЕ
	|	ЭДПрисоединенныеФайлы.Ссылка В(&МассивЭД)";
	
	Запрос.УстановитьПараметр("МассивЭД", ОбъектыДляОбработки);
	РезультатЗапроса = Запрос.Выполнить();
	ПользователиСертификатов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Если ЗначениеЗаполнено(ПользователиСертификатов) Тогда
		Элементы.Пользователь.СписокВыбора.ЗагрузитьЗначения(ПользователиСертификатов);
		Элементы.Пользователь.КнопкаСпискаВыбора = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ОбъектыДляОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивЭД = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если МассивЭД.Количество() > 1 Тогда
		ФормаПросмотраЭД = ОткрытьФорму("Обработка.ЭлектронныеДокументы.Форма.ФормаСпискаВыгружаемыхДокументов",
			Новый Структура("МассивСсылокЭД", МассивЭД), ЭтаФорма);
	Иначе
		ОткрытьЗначение(МассивЭД[0]);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан ответственный.'"),, "Пользователь");
		Возврат;
	КонецЕсли;
	ВсегоЭД = 0;
	Результат = ПеренаправитьЭД(ВсегоЭД);
	ЭлектронныеДокументыСлужебныйКлиент.ОповеститьПользователяОСменеОтветственного(Пользователь, ВсегоЭД, Результат);
	Оповестить("ОбновитьСостояниеЭД");
	Закрыть(Результат > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ПеренаправитьЭД(ВсегоЭД)
	
	МассивЭД = ПолучитьИзВременногоХранилища(АдресХранилища);
	ВсегоЭД = МассивЭД.Количество();
	КоличествоОбработанныхЭД = 0;
	ЭлектронныеДокументыСлужебныйВызовСервера.УстановитьОтветственногоЭД(МассивЭД,
		Пользователь, КоличествоОбработанныхЭД, Комментарий);
	
	Возврат КоличествоОбработанныхЭД;
	
КонецФункции
