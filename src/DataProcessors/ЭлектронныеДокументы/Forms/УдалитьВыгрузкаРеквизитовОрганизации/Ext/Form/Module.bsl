////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ВыгрузитьРеквизитыОрганизации(АдресХранилища, УникальныйИдентификатор)
	
	ТекстОшибки = "";
	
	Попытка
		
		КонтрагентXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Контрагент", "4.02");
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "ИД", ИНН + "_" + КПП, Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "Наименование", Наименование, Истина, ТекстОшибки);
		
		Если НЕ ЭлектронныеДокументыПереопределяемый.ЭтоФизЛицо(Организация) Тогда
			ЮрФизЛицоXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("РеквизитыЮрЛица", "4.02");
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
				ЮрФизЛицоXDTO, "ОфициальноеНаименование", НаименованиеПолное, Истина, ТекстОшибки);
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЮрФизЛицоXDTO, "КПП", КПП, , ТекстОшибки);
			
			Если ЗначениеЗаполнено(Руководитель) Тогда
				РуководительXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Контрагент", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(РуководительXDTO, "ИД", Руководитель, Истина, ТекстОшибки);
				ФизЛицоРуководительXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Контрагент.ФизЛицо", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					ФизЛицоРуководительXDTO, "ПолноеНаименование", Руководитель, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					ФизЛицоРуководительXDTO, "Должность", ДолжностьРуководителя, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					РуководительXDTO, "ФизЛицо", ФизЛицоРуководительXDTO, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					ЮрФизЛицоXDTO, "Руководитель", РуководительXDTO, Истина, ТекстОшибки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЮридическийАдрес) Тогда
				АдресXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Адрес", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(АдресXDTO, "Представление", ЮридическийАдрес, Истина, ТекстОшибки);
				Если ЗначениеЗаполнено(ЗначенияПолейЮрАдрес) Тогда
					РазобратьАдрес(АдресXDTO, ЗначенияПолейЮрАдрес, ТекстОшибки);
				КонецЕсли;
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЮрФизЛицоXDTO, "ЮридическийАдрес", АдресXDTO, Истина, ТекстОшибки);
			КонецЕсли;
			
			ИмяСвойства = "ЮрЛицо";
		Иначе
			ЮрФизЛицоXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("РеквизитыФизЛица", "4.02");
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЮрФизЛицоXDTO, "ПолноеНаименование", НаименованиеПолное, Истина, ТекстОшибки);
			
			Если ЗначениеЗаполнено(ЮридическийАдрес) Тогда
				АдресXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Адрес", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(АдресXDTO, "Представление", ЮридическийАдрес, Истина, ТекстОшибки);
				Если ЗначениеЗаполнено(ЗначенияПолейЮрАдрес) Тогда
					РазобратьАдрес(АдресXDTO, ЗначенияПолейЮрАдрес, ТекстОшибки);
				КонецЕсли;
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЮрФизЛицоXDTO, "АдресРегистрации", АдресXDTO, Истина, ТекстОшибки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СвидетельствоДата) И ЗначениеЗаполнено(СвидетельствоНомер) Тогда
				СвидетельствоXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("РеквизитыФизЛица.Свидетельство", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					СвидетельствоXDTO, "Номер", СвидетельствоНомер, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					СвидетельствоXDTO, "ДатаВыдачи", СвидетельствоДата, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					ЮрФизЛицоXDTO, "Свидетельство", СвидетельствоXDTO, Истина, ТекстОшибки);
			КонецЕсли;
			ИмяСвойства = "ФизЛицо";
		КонецЕсли;
		
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЮрФизЛицоXDTO, "ИНН", ИНН, Истина, ТекстОшибки);
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(ЮрФизЛицоXDTO, "ОКПО", ОКПО, , ТекстОшибки);
		
		Если ЗначениеЗаполнено(ФактическийАдрес) Тогда
			АдресXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Адрес", "4.02");
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(АдресXDTO, "Представление", ФактическийАдрес, Истина, ТекстОшибки);
			Если ЗначениеЗаполнено(ЗначенияПолейФактАдрес) Тогда
				РазобратьАдрес(АдресXDTO, ЗначенияПолейФактАдрес, ТекстОшибки);
			КонецЕсли;
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "Адрес", АдресXDTO, Истина, ТекстОшибки);
		КонецЕсли;
		
		ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, ИмяСвойства, ЮрФизЛицоXDTO, Истина, ТекстОшибки);
		
		ТЗБанковскиеСчета = БанковскиеСчета.Выгрузить(Новый Структура("Выбран", Истина));
		БанковскиеРеквизиты = ЭлектронныеДокументыПереопределяемый.ПолучитьБанковскиеРеквизиты(
			ТЗБанковскиеСчета.ВыгрузитьКолонку("БанковскийСчет"));
		
		Если БанковскиеРеквизиты.Количество() > 0 Тогда
			
			РасчетныеСчетаXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Контрагент.РасчетныеСчета", "4.02");
			
			Для Каждого Счет из БанковскиеРеквизиты Цикл
				РасчетныйСчетXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("РасчетныйСчет", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					РасчетныйСчетXDTO, "НомерСчета", Счет.РасчетныйСчет, Истина, ТекстОшибки);
				БанкXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Банк", "4.02");
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(БанкXDTO, "БИК", Счет.БИК, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
					БанкXDTO, "СчетКорреспондентский", Счет.КорреспондентскийСчет, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(БанкXDTO, "Наименование", Счет.Банк, Истина, ТекстОшибки);
				ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(РасчетныйСчетXDTO, "Банк",БанкXDTO, Истина, ТекстОшибки);
				
				Если ЗначениеЗаполнено(Счет.БанкДляРасчетов) Тогда
					БанкXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Банк", "4.02");
					ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(БанкXDTO, "БИК", Счет.БанкДляРасчетовБИК, Истина, ТекстОшибки);
					ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
						БанкXDTO, "СчетКорреспондентский", Счет.БанкДляРасчетовКоррСчет, Истина, ТекстОшибки);
					ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
						БанкXDTO, "Наименование", Счет.БанкДляРасчетов, Истина, ТекстОшибки);
					ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
						РасчетныйСчетXDTO, "БанкКорреспондент", БанкXDTO, Истина, ТекстОшибки);
				КонецЕсли;
					
				РасчетныеСчетаXDTO.РасчетныйСчет.Добавить(РасчетныйСчетXDTO);
			КонецЦикла;
			
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
				КонтрагентXDTO, "РасчетныеСчета", РасчетныеСчетаXDTO, Истина, ТекстОшибки);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Телефон) Тогда
			КонтактнаяИнформацияXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("КонтактнаяИнформация", "4.02");
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(
				КонтактнаяИнформацияXDTO, "Тип", "Телефон рабочий", Истина, ТекстОшибки);
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(КонтактнаяИнформацияXDTO, "Значение", Телефон, Истина, ТекстОшибки);
			
			КонтактыXDTO = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Контрагент.Контакты", "4.02");
			КонтактыXDTO.Контакт.Добавить(КонтактнаяИнформацияXDTO);
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(КонтрагентXDTO, "Контакты", КонтактыXDTO, Истина, ТекстОшибки);
		КонецЕсли;
		
		КонтрагентXDTO.Проверить();
		
		Если ТекстОшибки = "" Тогда
			ИмяФайла = ЭлектронныеДокументыСлужебный.ТекущееИмяВременногоФайла("xml");
			НоваяЗаписьXML = Новый ЗаписьXML;
			НоваяЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
			НоваяЗаписьXML.ЗаписатьОбъявлениеXML();
			ФабрикаXDTO.ЗаписатьXML(НоваяЗаписьXML, КонтрагентXDTO, , , , НазначениеТипаXML.Явное);
			НоваяЗаписьXML.Закрыть();
			ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
			УдалитьФайлы(ИмяФайла);
			
			АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		Иначе
			ШаблонСообщения = НСтр("ru = '%1 (подробности см. в Журнале регистрации).'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ТекстОшибки);
			ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Формирование ЭД'"),
																						ТекстОшибки,
																						ТекстОшибки);
		КонецЕсли;
		
	Исключение
		
		ШаблонСообщения = НСтр("ru = '%1 (подробности см. в Журнале регистрации).'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru = 'Формирование ЭД'"),
																					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
																					ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура РазобратьАдрес(ОбъектXDTO, Значение, ТекстОшибки)

	Значение = СтрЗаменить(Значение, "Индекс",         "Почтовый индекс");
	Значение = СтрЗаменить(Значение, "НаселенныйПункт","Населенный пункт");
	ДопустимыеТипы = "Почтовый индекс, Страна, Регион, Район, Населенный пункт, Город, Улица, Дом, Корпус, Квартира";
	
	Для Индекс=1 По СтрЧислоСтрок(Значение) Цикл
		ТекСтрока = СтрПолучитьСтроку(Значение, Индекс);
		Тип = Сред(ТекСтрока, 1, Найти(ТекСтрока, "=") - 1);
		Если Найти(ДопустимыеТипы, Тип) > 0 Тогда
			АдресноеПоле = ЭлектронныеДокументыВнутренний.ПолучитьОбъектТипаCML("Адрес.АдресноеПоле", "4.02");
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(АдресноеПоле, "Тип", Тип, Истина, ТекстОшибки);
			ЭлектронныеДокументыВнутренний.ЗаполнитьСвойствоXDTO(АдресноеПоле, "Значение", Сред(ТекСтрока,Найти(ТекСтрока, "=") + 1), Истина, ТекстОшибки);
			ОбъектXDTO.АдресноеПоле.Добавить(АдресноеПоле);
		КонецЕсли;
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Функция ПолучитьУчетнуюЗапись()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеЗаписиЭлектроннойПочты.Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
	|ГДЕ
	|	УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления = ЛОЖЬ
	|	И УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки = ИСТИНА";
		
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Количество() = 1 Тогда
		Результат.Следующий();
		Возврат Результат.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка();

КонецФункции

&НаСервере
Функция ПолучитьСтруктуруРеквизитов()
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Наименование");
	СтруктураВозврата.Вставить("НаименованиеПолное");
	СтруктураВозврата.Вставить("ИНН");
	СтруктураВозврата.Вставить("КПП");
	СтруктураВозврата.Вставить("ОКПО");
	СтруктураВозврата.Вставить("ДолжностьРуководителя");
	СтруктураВозврата.Вставить("Руководитель");
	СтруктураВозврата.Вставить("ЮрФизЛицо");
	СтруктураВозврата.Вставить("СвидетельствоДата");
	СтруктураВозврата.Вставить("СвидетельствоНомер");
	СтруктураВозврата.Вставить("ЮридическийАдрес");
	СтруктураВозврата.Вставить("ЗначенияПолейЮрАдрес");
	СтруктураВозврата.Вставить("ФактическийАдрес");
	СтруктураВозврата.Вставить("ЗначенияПолейФактАдрес");
	СтруктураВозврата.Вставить("Телефон");
	
	Возврат СтруктураВозврата;

КонецФункции

&НаКлиенте
Процедура ОбновитьФорму()
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Элементы.СтраницыПолучателей.ТекущаяСтраница = Элементы.СтраницаПисьмо;
	ИначеЕсли СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		Элементы.СтраницыПолучателей.ТекущаяСтраница = Элементы.СтраницаКаталог;
	КонецЕсли;
	Если ЮрФизЛицо = ИндивидуальныйПредприниматель Тогда
		Элементы.ГруппаРуководитель.Видимость = Ложь;
		Элементы.КПП.Видимость                = Ложь;
		
		НадписьСвидетельство = Нстр("ru ='Свидетельство №'") + СвидетельствоНомер + Нстр("ru =' от '") + Формат(СвидетельствоДата, "ДЛФ=D");
	Иначе
		Элементы.Свидетельство.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыгрузитьРеквизиты(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() ТОгда
		Возврат;
	КонецЕсли;
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		Если НЕ ЗначениеЗаполнено(АдресВыгрузки) Тогда
			ТекстСообщения = Нстр("ru = 'Не выбрана учетная запись электронной почты.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	АдресХранилища = Неопределено;
	ВыгрузитьРеквизитыОрганизации(АдресХранилища, УникальныйИдентификатор);
	Если АдресХранилища = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
		
		ИмяФайлаПоУмолчанию = Наименование;
		ИмяФайлаПоУмолчанию = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайлаПоУмолчанию, "");
		
		ДанныеФайлаДляСохранения = Новый Структура;
		ДанныеФайлаДляСохранения.Вставить("Расширение", "xml");
		ДанныеФайлаДляСохранения.Вставить("ПолноеНаименование", ИмяФайлаПоУмолчанию);
		ДанныеФайлаДляСохранения.Вставить("АдресХранилища", АдресХранилища);
		
		ЭлектронныеДокументыКлиент.СохранитьКак(ДанныеФайлаДляСохранения);
		
	ИначеЕсли СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		ИмяФайлаДанных = Наименование + ".xml";
		ИмяФайлаДанных = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайлаДанных, "");
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Тема", "Реквизиты " + Наименование);
		ПараметрыФормы.Вставить("УчетнаяЗапись", АдресВыгрузки);
		ИмяФайла = ИмяФайлаДанных;
		Пока Истина Цикл
			Поз = Макс(Найти(ИмяФайла, "\"), Найти(ИмяФайла, "/"));
			Если Поз = 0 Тогда
				Прервать;
			КонецЕсли;
			ИмяФайла = Сред(ИмяФайла, Поз + 1);
		КонецЦикла;
		Вложения = Новый СписокЗначений;
		НовыйЭлемент = Вложения.Добавить(АдресХранилища, ИмяФайла);
		ПараметрыФормы.Вставить("Вложения", Вложения);
		Форма = ОткрытьФорму("ОбщаяФорма.ОтправкаСообщения", ПараметрыФормы);
	КонецЕсли;
	
	Закрыть();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура СпособВыгрузкиПриИзменении(Элемент)
	
	АдресВыгрузки = "";
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
		АдресВыгрузки = ПолучитьУчетнуюЗапись();
	КонецЕсли;
	ОбновитьФорму();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	АдресВыгрузки = ПредопределенноеЗначение("Справочник.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "БанковскийСчет" Тогда
		ОткрытьЗначение(Элемент.ТекущиеДанные.БанковскийСчет);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскиеСчетаВыбранПриИзменении(Элемент)
	
	Если Элементы.БанковскиеСчета.ТекущиеДанные.Выбран Тогда
		НомерСтроки = 0;
		ТекущийНомер = Элементы.БанковскиеСчета.ТекущаяСтрока + 1;
		Для Каждого Строка ИЗ БанковскиеСчета Цикл
			НомерСтроки = НомерСтроки + 1;
			Если НомерСтроки <> ТекущийНомер Тогда
				Строка.Выбран = Ложь;
			КонецЕсли;
		КонецЦикла
	КонецЕсли
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация     = Параметры.Организация;
	СтруктураДанных = ПолучитьСтруктуруРеквизитов();
	ЭлектронныеДокументыПереопределяемый.ПолучитьРеквизитыОрганизацииДляВыгрузкиВФайл(Организация, СтруктураДанных);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураДанных);
	
	ТаблБанковскиеСчета = ЭлектронныеДокументыПереопределяемый.ПолучитьБанковскиеСчета(Организация);
	БанковскиеСчета.Загрузить(ТаблБанковскиеСчета);
	
	Если СпособВыгрузки = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
		АдресВыгрузки = ПолучитьУчетнуюЗапись();
	КонецЕсли;
	
	ИндивидуальныйПредприниматель = ЭлектронныеДокументыПовтИсп.НайтиПеречисление("ЮрФизЛицо", "ИндивидуальныйПредприниматель");

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(СпособВыгрузки) Тогда
		СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог");
	КонецЕсли;
	
	Если СпособВыгрузки = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту")
		И НЕ ЗначениеЗаполнено(АдресВыгрузки) Тогда
		АдресВыгрузки = ПолучитьУчетнуюЗапись();
	КонецЕсли;
		
	ОбновитьФорму();
	
КонецПроцедуры
