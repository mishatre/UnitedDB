
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИдентификаторСессии = Параметры.Сессия.Идентификатор;

	ВнешнийПодключаемыйМодуль = ЭлектронныеДокументыСлужебныйКлиент.ВнешнийПодключаемыйМодуль(Параметры.СоглашениеЭД);
	
	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьОдноразовыйПарольРасширеннойАутентификацииПоSMS(Параметры.Сертификат,
		                                                                                             Параметры.Сессия);
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки одноразового пароля расширенной аутентификации.
									|Код ошибки: %1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
															ШаблонОшибки,
															ДеталиОшибки.Код,
															ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка соединения'");
		ЭлектронныеДокументыСлужебныйВызовСервера.ОбработатьИсключениеПоЭДНаСервере(Операция,
																					ПодробноеПредставлениеОшибки,
																					ТекстСообщения,
																					1);
		Отказ = Истина;
	КонецПопытки;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Готово(Команда)

	Если ПустаяСтрока(Пароль) Тогда
		ТекстСообщения = НСтр("ru = 'Для продолжения укажите одноразовый пароль.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Пароль");
		Возврат;
	КонецЕсли;

	Результат = Новый Структура;
	Результат.Вставить("Пароль", Пароль);

	Закрыть(Результат);

КонецПроцедуры

