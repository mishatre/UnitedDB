
Перем мМонопольныйРежимПередЗаписью;
Перем мТипУдалениеДанных;
Перем мТипРегистрСведенийНаборЗаписейОбъектыДоступаДокументов;
Перем мЭтоНовыйЭлемент;

Перем мТаблицаОрганизацийДляВыгрузкиПередЗаписью;
Перем мОтборПоДатеДокумента;

Перем мИнформацияОНаличиеОрганизацииВРегистре;
Перем мИнформацияОБазовыхТипах;

Перем мКоличествоОрганизацийВУзле;
Перем мСоответствиеОрганизацийИУзлов;

Перем мСтарыйТипОбъектаОтправки;
Перем мИмяСтарогоБазовогоТипа;


Функция СообщитьИнформациюПользователюПослеСозданияНовогоУзла() Экспорт
	
	НужноПерезапуститьВсеПодключенияКИБ = Ложь;
	
	Если мЭтоНовыйЭлемент 
		И НЕ ПараметрыСеанса.ИспользованиеРИБ
		И НЕ мМонопольныйРежимПередЗаписью Тогда
		
		НужноПерезапуститьВсеПодключенияКИБ = Истина;
		
	КонецЕсли;
	
	Если мОтборПоДатеДокумента <> ДатаНачалаВыгрузкиДокументов
		ИЛИ ОпределитьЕстьОтличияВТабличнойЧасти() Тогда
		
		НужноПерезапуститьВсеПодключенияКИБ = Истина;
		
	КонецЕсли;
	
	Если НужноПерезапуститьВсеПодключенияКИБ Тогда
		
		Если мМонопольныйРежимПередЗаписью Тогда
			
			ПолныеПрава.ОпределитьПараметрыСеансаДляОбменаДанными();
			Возврат "";
			
		Иначе	
			
			Возврат "Для корректной работы механизма обмена данными необходимо завершить работу всех пользователей и перезапустить текущий сеанс работы 1С:Предприятия.";	
			
		КонецЕсли;
		
	Иначе
		
		Если мЭтоНовыйЭлемент Тогда
			
			ПолныеПрава.ОпределитьПараметрыСеансаДляОбменаДанными();	
			
		КонецЕсли;
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

Функция СравнитьТаблицыПоПолю(Таблица1, Таблица2, ИмяПоля)
	
	Если Таблица1.Количество() <> Таблица2.Количество() Тогда
		
		Возврат Истина;
		
	КонецЕсли;
		
	Для Номер = 0 По Таблица1.Количество() - 1 Цикл
		
		Если Таблица1[Номер][ИмяПоля] <> Таблица2[Номер][ИмяПоля] Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
	
КонецФункции

Функция ОпределитьЕстьОтличияВТабличнойЧасти()
	
	Если мТаблицаОрганизацийДляВыгрузкиПередЗаписью = Неопределено Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	НаличиеОтличий = СравнитьТаблицыПоПолю(Организации, мТаблицаОрганизацийДляВыгрузкиПередЗаписью, "Организация");
		
	Возврат НаличиеОтличий;
	
КонецФункции


Процедура ПередЗаписью(Отказ)
	
	мЭтоНовыйЭлемент = ЭтоНовый();
	мМонопольныйРежимПередЗаписью = ОбщегоНазначения.ОпределитьТекущийРежимРаботыМонопольный();
	
	Если Не мЭтоНовыйЭлемент Тогда
		
		мТаблицаОрганизацийДляВыгрузкиПередЗаписью = Ссылка.Организации.Выгрузить();
		мОтборПоДатеДокумента = Ссылка.ДатаНачалаВыгрузкиДокументов;
		
	Иначе
		
		мТаблицаОрганизацийДляВыгрузкиПередЗаписью = Неопределено;
		мОтборПоДатеДокумента = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	СтрокаСообщенияПользователю = СообщитьИнформациюПользователюПослеСозданияНовогоУзла();
	
	#Если Клиент Тогда
	Сообщить(СтрокаСообщенияПользователю);
	#КонецЕсли		
		
КонецПроцедуры

Функция ПолучитьИмяБазовогоТипаПоТипуОбъекта(ТипОбъекта) Экспорт
	
	МетаданныеТипа = Метаданные.НайтиПоТипу(ТипОбъекта);
	
	Если Метаданные.РегистрыСведений.Содержит(МетаданныеТипа) Тогда
	
		Возврат "РегистрыСведений";
		
	ИначеЕсли Метаданные.Документы.Содержит(МетаданныеТипа) Тогда
		
		Возврат "Документы";
		
	ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеТипа) Тогда
		
		Возврат "Справочники";	
		
	Иначе
	   
		Возврат "";
		
	КонецЕсли;
			
КонецФункции

Процедура УбратьИзРегистраДанныеНеНужныеДляПереносаПоОрганизации(ЭлементДанных)
	
	// режем по организации
	ТекущаяОрганизация = Неопределено;
	ТекущееРешение = Ложь;
	
	Позиция = ЭлементДанных.Количество() - 1;
	
	Пока Позиция >= 0 Цикл
		
		СтрокаНабора = ЭлементДанных[Позиция];
		Если СтрокаНабора.Организация <> ТекущаяОрганизация Тогда
			
			ТекущаяОрганизация = СтрокаНабора.Организация;
			ТекущееРешение = (ЭтотОбъект.Организации.Найти(ТекущаяОрганизация, "Организация") <> Неопределено); 
			
		КонецЕсли;
		
		Если НЕ ТекущееРешение Тогда
			ЭлементДанных.Удалить(Позиция);
		КонецЕсли;
		
		Позиция = Позиция - 1;
		
	КонецЦикла;	

КонецПроцедуры

Процедура ОпределитьТипОтправкиДанных(ЭлементДанных, ОтправкаЭлемента) Экспорт
	
	ТипОбъекта = ТипЗнч(ЭлементДанных);
	
	Если ТипОбъекта = мТипУдалениеДанных Тогда
		
		// удаление объекта просто отсылаем как есть
		Возврат;
		
	КонецЕсли;
	
	Если мСоответствиеОрганизацийИУзлов = Неопределено Тогда
			
		мСоответствиеОрганизацийИУзлов = ПроцедурыОбменаУТУПП.ПолучитьСоответствиеУзловИОрганизаций();
		
	КонецЕсли;	
	
	Если мСтарыйТипОбъектаОтправки = ТипОбъекта Тогда
		
		ИмяБазовогоТипа = мИмяСтарогоБазовогоТипа;
		
	Иначе	
	
		ИмяБазовогоТипа = мИнформацияОБазовыхТипах.Получить(ТипОбъекта);
		
		Если ИмяБазовогоТипа = Неопределено Тогда
		
			ИмяБазовогоТипа = ПолучитьИмяБазовогоТипаПоТипуОбъекта(ТипОбъекта);
			мИнформацияОБазовыхТипах.Вставить(ТипОбъекта, ИмяБазовогоТипа);
		
		КонецЕсли;
		
		мИмяСтарогоБазовогоТипа = ИмяБазовогоТипа;
		мСтарыйТипОбъектаОтправки = ТипОбъекта;
	
	КонецЕсли;
	
	Если ИмяБазовогоТипа = "Справочники"
		ИЛИ ИмяБазовогоТипа = "Документы" Тогда
		
		ВыгружатьДляВсехУзлов = Ложь;
		
		МассивУзловДляПереноса = ПроцедурыОбменаУТУПП.ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(ЭлементДанных, 
			ИмяБазовогоТипа, ТипОбъекта, , ВыгружатьДляВсехУзлов, Ложь, мСоответствиеОрганизацийИУзлов, ЛОЖЬ);
			
		// если нужно для всех узлов выгружать - то ничего проверять не нужно
		Если ВыгружатьДляВсехУзлов Тогда
			Возврат;
		КонецЕсли;
			
		НомерВМассиве = МассивУзловДляПереноса.Найти(ЭтотОбъект.Ссылка);
		
		Если НомерВМассиве = Неопределено Тогда
			
			// передаем информацию об удалении
			ОтправкаЭлемента = ОтправкаЭлементаДанных.Удалить;	
			
		Иначе
			
			// объект должен ехать, нужно определить удовлетворяет ли он ограничениям по дате
			Если ИмяБазовогоТипа = "Документы"
				И ЗначениеЗаполнено(ДатаНачалаВыгрузкиДокументов)
				И ЭлементДанных.Дата < ДатаНачалаВыгрузкиДокументов Тогда
				
				// просто не выгружаем этот документ и все - игнорируем при выгрузке
				ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;	
				
			КонецЕсли;
			
		КонецЕсли;			
				
	ИначеЕсли ИмяБазовогоТипа = "РегистрыСведений" Тогда
		
		Если мКоличествоОрганизацийВУзле = Неопределено Тогда
			
			мКоличествоОрганизацийВУзле = ЭтотОбъект.Организации.Количество();
			
		КонецЕсли;
		
		Если мКоличествоОрганизацийВУзле = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипОбъекта = мТипРегистрСведенийНаборЗаписейОбъектыДоступаДокументов Тогда
			
			ВыгружатьДляВсехУзлов = Ложь;
			
			ДокументСсылка = ЭлементДанных.Отбор.ДокументСсылка.Значение;
			
			МассивУзловДляПереноса = ПроцедурыОбменаУТУПП.ОпределитьМассивУзловДляРегистрацииПроизвольногоТипа(ДокументСсылка, "Документы", , , 
				ВыгружатьДляВсехУзлов, Ложь, мСоответствиеОрганизацийИУзлов, Ложь);				
			
			Если ВыгружатьДляВсехУзлов Тогда
				Возврат;
			КонецЕсли;
				
			НомерВМассиве = МассивУзловДляПереноса.Найти(ЭтотОбъект.Ссылка);
			
			Если НомерВМассиве = Неопределено Тогда
				
				// передаем информацию об удалении
				ОтправкаЭлемента = ОтправкаЭлементаДанных.Удалить;	
				
			КонецЕсли;
			
		Иначе 
		
			// нужно вычистить те строки которые для которых нужная организация не переносится
			НаличиеОрганизации = мИнформацияОНаличиеОрганизацииВРегистре.Получить(ТипОбъекта);
				
			Если НаличиеОрганизации = Неопределено Тогда
				
				МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта);					
				НаличиеОрганизации = (МетаданныеОбъекта.Измерения.Найти("Организация") <> Неопределено);
				мИнформацияОНаличиеОрганизацииВРегистре.Вставить(ТипОбъекта, НаличиеОрганизации);
				
			КонецЕсли;
			
			Если НаличиеОрганизации Тогда
					
				УбратьИзРегистраДанныеНеНужныеДляПереносаПоОрганизации(ЭлементДанных);
			
			КонецЕсли;
			
		КонецЕсли;
								
	КонецЕсли;	
	
КонецПроцедуры


мТипУдалениеДанных = Тип("УдалениеОбъекта");
мТипРегистрСведенийНаборЗаписейОбъектыДоступаДокументов = Тип("РегистрСведенийНаборЗаписей.ОбъектыДоступаДокументов");

мИнформацияОНаличиеОрганизацииВРегистре = Новый Соответствие;
мИнформацияОБазовыхТипах = Новый Соответствие;
мСтарыйТипОбъектаОтправки = Неопределено;
мСоответствиеОрганизацийИУзлов = Неопределено;
