
Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;

	ДанныеХранилища = Хранилище.Получить();
	ТекстФайлаЗаписан = Ложь;
	Если ТипЗнч(ДанныеХранилища) = Тип("ДвоичныеДанные") Тогда
		ВременныйФайл = ПолучитьИмяВременногоФайла(РаботаСФайлами.ПолучитьРасширениеФайла(ИмяФайла));
		Попытка
			ДанныеХранилища.Записать(ВременныйФайл);
			Извлечение = Новый ИзвлечениеТекста(ВременныйФайл);
			ТекстФайла = Новый ХранилищеЗначения(Извлечение.ПолучитьТекст());
			ТекстФайлаЗаписан = Истина;
		Исключение
			ТекстФайлаЗаписан = Ложь;
		КонецПопытки;
		УдалитьФайлы(ВременныйФайл);
	КонецЕсли;
	
	Если НЕ ТекстФайлаЗаписан Тогда
		ТекстФайла = Новый ХранилищеЗначения("");
	КонецЕсли; 
	
	Если ТипЗнч(Объект) = Тип("ДокументСсылка.ЭлектронноеПисьмо")
	   И ТипЗнч(Объект.Предмет) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Предмет = Объект.Предмет;
	Иначе
		Предмет = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;

КонецПроцедуры


