////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриЗакрытииВладельца = Истина;
	
	Заголовок           = Параметры.Заголовок;
	КомпоновщикНастроек = Параметры.КомпоновщикНастроек;
	ПериодДанных        = Параметры.ПериодДанных;
	
	Если Параметры.ВыборПериода Тогда
		ВыгружатьЗаПериод = ЗначениеЗаполнено(ПериодДанных);
	Иначе
		ВыгружатьЗаПериод = Ложь;
		Элементы.ВыгружатьЗаПериод.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьПериодаОтбора();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ
//

&НаКлиенте
Процедура ВыгружатьЗаПериодПриИзменении(Элемент)
	УстановитьВидимостьПериодаОтбора();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
//

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОповеститьОВыборе(Новый Структура(
		"ДействиеВыбора, КомпоновщикНастроек, ПериодДанных",
		Параметры.ДействиеВыбора, 
		КомпоновщикНастроек, ?(ВыгружатьЗаПериод, ПериодДанных, Новый СтандартныйПериод)
	));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

&НаСервере 
Процедура УстановитьВидимостьПериодаОтбора()
	Элементы.ПериодДанных.Видимость = ВыгружатьЗаПериод;
	
	Текст = СокрЛП(Элементы.ВыгружатьЗаПериод.Заголовок);
	Если Элементы.ПериодДанных.Видимость Тогда
		Если Прав(Текст, 1)<>":" Тогда
			Элементы.ВыгружатьЗаПериод.Заголовок = Текст + ":";
		КонецЕсли;
	Иначе
		Если Прав(Текст, 1)=":" Тогда
			Элементы.ВыгружатьЗаПериод.Заголовок = Лев(Текст, СтрДлина(Текст)-1);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

