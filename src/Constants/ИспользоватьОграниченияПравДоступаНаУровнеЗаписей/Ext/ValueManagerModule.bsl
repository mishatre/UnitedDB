
Перем мСтароеЗначение;

Процедура ЗаполнитьНаборДанными(НаборЗаписей, ГруппаПользователей)

	Для каждого ЭлементПеречисления Из Перечисления.ВидыОбъектовДоступа Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ГруппаПользователей = ГруппаПользователей;
		НоваяЗапись.ВидОбъектаДоступа = ЭлементПеречисления;
	КонецЦикла; 

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.Значение Тогда
		мСтароеЗначение = Константы.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей.Получить();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.Значение И ЭтотОбъект.Значение <> мСтароеЗначение Тогда
	
		// Найдем группы пользовтелей, для которых совсем не определены виды объектов доступа
		Запрос = Новый Запрос;
		
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	СправочникГруппыПользователей.Ссылка КАК ГруппаПользователейСсылка
		|ИЗ
		|	Справочник.ГруппыПользователей КАК СправочникГруппыПользователей
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|		(
		|			ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ГруппаПользователей
		|			ИЗ
		|				РегистрСведений.НазначениеВидовОбъектовДоступа
		|		) КАК ВложенныйЗапрос
		|		ПО ВложенныйЗапрос.ГруппаПользователей = СправочникГруппыПользователей.Ссылка
		|ГДЕ
		|	ВложенныйЗапрос.ГруппаПользователей ЕСТЬ NULL
		|";
		
		// Зададим для этих групп пользователей так, что ограничения действуют
		// по всем возможным видам объектов доступа
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
		
			НаборЗаписей = РегистрыСведений.НазначениеВидовОбъектовДоступа.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ГруппаПользователей.Использование = Истина;
			НаборЗаписей.Отбор.ГруппаПользователей.Значение = Выборка.ГруппаПользователейСсылка;
			ЗаполнитьНаборДанными(НаборЗаписей, Выборка.ГруппаПользователейСсылка);
			
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ВызватьИсключение ("Не удалось обработать запись константы использования ограничения прав доступа на уровне записей по причине: " + ОписаниеОшибки());
			КонецПопытки;
		
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры
