
//#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Аутентификация = ЭлектронныеДокументыСлужебный.ПараметрыАутентификацииНаСайте();
	Если Аутентификация <> Неопределено Тогда
		Логин  = Аутентификация.Логин;
		Пароль = Аутентификация.Пароль;
	КонецЕсли;
	
	ЗапомнитьПароль = Не ПустаяСтрока(Пароль);
КонецПроцедуры

//#КонецОбласти

//#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПерейтиКРегистрацииНаСайтеНажатие(Элемент)
	
	ЭлектронныеДокументыСлужебныйКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке("https://login.1c.ru/");
	
КонецПроцедуры

//#КонецОбласти

//#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьДанныеАутентификацииИПродолжить()
	
	Если ПустаяСтрока(Логин) И Не ПустаяСтрока(Пароль) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Введите код пользователя для авторизации на сайте фирмы 1С.'"),, "Логин");
		Возврат;
	КонецЕсли;
		
	Если ПустаяСтрока(Логин) Тогда
		СохранитьДанныеАутентификации(Неопределено);
		Результат = КодВозвратаДиалога.Отмена;
	Иначе
		Результат = Новый Структура("Логин,Пароль", Логин, Пароль);
		
		Билет = БилетНаСайт(Результат);
		Если Не ЗначениеЗаполнено(Билет) Тогда
			Возврат;
		КонецЕсли;
		СохранитьДанныеАутентификации(Новый Структура("Логин,Пароль", Логин, ?(ЗапомнитьПароль, Пароль, "")))
	КонецЕсли;
	
	Закрыть(Результат);
КонецПроцедуры

//#КонецОбласти

//#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция БилетНаСайт(Знач Аутентификация)
	
	Билет = ЭлектронныеДокументыСлужебный.БилетНаСайтПоддержки(Аутентификация);
	Возврат Билет;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьДанныеАутентификации(Знач Аутентификация)
	
	ЭлектронныеДокументыСлужебный.СохранитьПараметрыАутентификацииНаСайте(Аутентификация);
	
КонецПроцедуры

//#КонецОбласти
