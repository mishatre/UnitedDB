////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЭлектронныеДокументыСлужебныйВызовСервера.ПолучитьЗначениеФункциональнойОпции("ИспользоватьОбменЭД") Тогда
		ТекстСообщения = ЭлектронныеДокументыСлужебныйВызовСервера.ТекстСообщенияОНеобходимостиНастройкиСистемы("РаботаСЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимОткрытияОкна") Тогда
		ЭтаФорма.РежимОткрытияОкна = Параметры.РежимОткрытияОкна;
	КонецЕсли;
	
	Если Параметры.Свойство("НеОтображатьБыстрыеОтборы") Тогда
		Элементы.ГруппаБыстрыеОтборы.Видимость = Ложь;
	КонецЕсли;
	
	ИмяСправочникаБанки = ЭлектронныеДокументыПовтИсп.ПолучитьИмяПрикладногоСправочника("Банки");
	Если ЗначениеЗаполнено(ИмяСправочникаБанки) Тогда
		СписокСоглашенийСБанками.ТекстЗапроса = СтрЗаменить(СписокСоглашенийСБанками.ТекстЗапроса,
															"КлассификаторБанковРФ",
															ИмяСправочникаБанки);
	КонецЕсли;
	
	Контрагент         = Параметры.Контрагент;
	ПрофильНастроекЭДО = Параметры.ПрофильНастроекЭДО;
	Организация        = Параметры.Организация;
	
	СписокСоглашенийСКонтрагентами.Параметры.УстановитьЗначениеПараметра("ПрофильНастроекЭДО", ПрофильНастроекЭДО);
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокСоглашенийСКонтрагентами.Отбор, "Контрагент",
			Контрагент, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если ЭлектронныеДокументыПовтИсп.ИспользуетсяДополнительнаяАналитикаКонтрагентовСправочникПартнеры() Тогда
		
		СписокСоглашенийСКонтрагентами.ТекстЗапроса = СтрЗаменить(
			СписокСоглашенийСКонтрагентами.ТекстЗапроса, """Партнер""", "Соглашение.Контрагент.Партнер");
		Если ЗначениеЗаполнено(Параметры.Партнер) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокСоглашенийСКонтрагентами.Отбор, "Партнер",
				Параметры.Партнер, ВидСравненияКомпоновкиДанных.ВИерархии);
		КонецЕсли;
	Иначе
		СписокСоглашенийСКонтрагентами.ТекстЗапроса = СтрЗаменить(
			СписокСоглашенийСКонтрагентами.ТекстЗапроса, """Партнер"" КАК Партнер,", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент)
		ИЛИ ЗначениеЗаполнено(ПрофильНастроекЭДО)
		ИЛИ ЗначениеЗаполнено(Параметры.Партнер)
		ИЛИ Параметры.НастройкиЭДОСКонтрагентами Тогда
		
		Этаформа.АвтоЗаголовок = Ложь;
		Этаформа.Заголовок = НСтр("ru = 'Настройки ЭДО с контрагентами'");
		Элементы.ГруппаСоглашения.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ГруппаСоглашения.ТекущаяСтраница    = Элементы.ГруппаНастроек;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Банк) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(СписокСоглашенийСБанками.Отбор, "Банк",
			Параметры.Банк, ВидСравненияКомпоновкиДанных.Равно);
			
		Этаформа.АвтоЗаголовок = Ложь;
		Этаформа.Заголовок = НСтр("ru = 'Настройки ЭДО с банками'");
		Элементы.ГруппаСоглашения.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ГруппаСоглашения.ТекущаяСтраница = Элементы.ГруппаСоглашенияСБанками;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокСоглашенийСБанками.Отбор, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокСоглашенийСКонтрагентами.Отбор, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			СписокСоглашенийМеждуОрганизациями.Отбор, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	// Скроем группу "Всех соглашений" от пользователей с ограниченными правами или принудительно.
	СкрытьЗакладкуВсехСоглашений = Не Пользователи.ЭтоПолноправныйПользователь()
									ИЛИ ЗначениеЗаполнено(Параметры.Контрагент)
									ИЛИ ЗначениеЗаполнено(Параметры.Организация)
									ИЛИ ЗначениеЗаполнено(Параметры.Банк)
									ИЛИ ЗначениеЗаполнено(Параметры.ПрофильНастроекЭДО)
									ИЛИ ЗначениеЗаполнено(Параметры.Партнер)
									ИЛИ Параметры.НастройкиЭДОСКонтрагентами;
	
	Элементы.ГруппаВсеСоглашения.Видимость = Не СкрытьЗакладкуВсехСоглашений;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлектронныеДокументыСлужебныйКлиент.ЗаполнитьДанныеСлужбыПоддержки(ТелефонСлужбыПоддержки, АдресЭлектроннойПочтыСлужбыПоддержки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Элементы.СписокСоглашенийСКонтрагентами.Обновить();
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОткрытьСсылкуНаСтатьюПо1СБухфон(Команда)
	ЭлектронныеДокументыСлужебныйКлиент.ОткрытьИнструкциюПо1СБухфон();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	УстановитьОтборПоКонтрагенту(ЭтаФорма, Контрагент);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьОтборПоОрганизации(ЭтаФорма, Организация);
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	УстановитьОтборПоСтатусуПодключения(ЭтаФорма, СтатусПодключения);
КонецПроцедуры

&НаКлиенте
Процедура СостояниеСоглашенияПриИзменении(Элемент)
	УстановитьОтборПоСостояниюСоглашения(ЭтаФорма, СостояниеСоглашения);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ СписокНастроекСКонтрагентами

&НаКлиенте
Процедура СоздатьЭлементСпискаНастроекСКонтрагентами(Команда)
	
	ПараметрыФормы = Новый Структура;
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация",        Организация);
	ЗначенияЗаполнения.Вставить("ПрофильНастроекЭДО", ПрофильНастроекЭДО);
	ЗначенияЗаполнения.Вставить("Контрагент",         Контрагент);
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ СписокИнтеркампани

&НаКлиенте
Процедура СписокИнтеркампаниПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	Параметр = ?(Копирование, Новый Структура("ЗначениеКопирования", Элементы.СписокИнтеркампани.ТекущаяСтрока),
		Новый Структура);
	
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаИнтеркампани", Параметр,
		Элементы.СписокИнтеркампани);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ТАБЛИЦЫ СписокСоглашенийСБанками

&НаКлиенте
Процедура СписокСоглашенийСБанкамиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	Параметр = ?(Копирование, Новый Структура("ЗначениеКопирования", Элементы.СписокСоглашенийСБанками.ТекущаяСтрока), Новый Структура);
	Параметр.Вставить("Организация", Организация);
	Параметр.Вставить("Контрагент", Параметры.Банк);
	ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлементаБанк", Параметр, Элементы.СписокСоглашенийСБанками);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоКонтрагенту(Форма, ЗначениеОтбора)
	
	ИспользоватьОтбор = ЗначениеЗаполнено(ЗначениеОтбора);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
										Форма.СписокСоглашенийСКонтрагентами.Отбор,
										"Контрагент",
										ЗначениеОтбора,
										ВидСравненияКомпоновкиДанных.Равно,
										,
										ИспользоватьОтбор);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоОрганизации(Форма, ЗначениеОтбора)
	
	ИспользоватьОтбор = ЗначениеЗаполнено(ЗначениеОтбора);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
										Форма.СписокСоглашенийСКонтрагентами.Отбор,
										"Организация",
										ЗначениеОтбора,
										ВидСравненияКомпоновкиДанных.Равно,
										,
										ИспользоватьОтбор);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСтатусуПодключения(Форма, ЗначениеОтбора)
	
	ИспользоватьОтбор = ЗначениеЗаполнено(ЗначениеОтбора);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
										Форма.СписокСоглашенийСКонтрагентами.Отбор,
										"СтатусПодключения",
										ЗначениеОтбора,
										ВидСравненияКомпоновкиДанных.Равно,
										,
										ИспользоватьОтбор);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСостояниюСоглашения(Форма, ЗначениеОтбора)
	
	ИспользоватьОтбор = ЗначениеЗаполнено(ЗначениеОтбора);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
										Форма.СписокСоглашенийСКонтрагентами.Отбор,
										"СостояниеСоглашения",
										ЗначениеОтбора,
										ВидСравненияКомпоновкиДанных.Равно,
										,
										ИспользоватьОтбор);
КонецПроцедуры
